#include "mmb.cuh"
#include "unary.cuh"
#include <unordered_map>
#include <map>
#include <utility>
#include "mmid.cuh"
#include <cstdlib>
#include <cstring>
#include <vector>
#include <unordered_set>

namespace {

typedef float v8f  __attribute__((ext_vector_type(8)));

// Fragment-layout shim.  Both gens compute a 16x16x16 tile with fp32 accumulation; only the lane
// bookkeeping differs (gated_delta_net_chunked_bf16.cu vs ..._gfx11.cu):
//   gfx11: 16 halfs/lane (the whole K row), accumulator m = 2*e + hi
//   gfx12:  8 halfs/lane ("two runs of four": k = 4*hi + {0..3} and 4*hi + 8 + {0..3}), m = 8*hi + e
#if defined(RDNA4)
typedef short    mmb_frag_t      __attribute__((ext_vector_type(8)));
typedef __bf16   mmb_bf16_frag_t __attribute__((ext_vector_type(8)));
typedef _Float16 mmb_f16_frag_t  __attribute__((ext_vector_type(8)));
#define MMB_ACC_M(e, hi) (8*(hi) + (e))
// K-contiguous fragment from a 16-wide LDS row: this lane's two runs of four.
__device__ __forceinline__ mmb_frag_t mmb_ld_frag(const uint16_t * p, const int hi) {
    const uint16_t * q = p + 4 * hi;
    return __builtin_bit_cast(mmb_frag_t, (uint2[2]){*reinterpret_cast<const uint2 *>(q), *reinterpret_cast<const uint2 *>(q + 8)});
}
#else
typedef short mmb_frag_t      __attribute__((ext_vector_type(16)));
typedef short mmb_bf16_frag_t __attribute__((ext_vector_type(16)));
typedef short mmb_f16_frag_t  __attribute__((ext_vector_type(16)));
#define MMB_ACC_M(e, hi) (2*(e) + (hi))
// gfx11 keeps the whole 16-half row in one lane.  A macro (not a function) so the preprocessed
// source at every call site is the original expression -- the gfx11 asm stays bit-identical.
#define mmb_ld_frag(p, hi) (__builtin_bit_cast(mmb_frag_t, (uint4[2]){*reinterpret_cast<const uint4 *>(p), *reinterpret_cast<const uint4 *>((p) + 8)}))
#endif
constexpr int MMB_BK = 64, MMB_NT = 256, MMB_LDS_STRIDE = MMB_BK + 8;

__device__ __forceinline__ uint16_t mmb_f2bf(float f) { uint32_t u = __float_as_uint(f); u += 0x7fffu + ((u >> 16) & 1u); return (uint16_t)(u >> 16); }
// RNE float->bf16.  `v_cvt_pk_bf16_f32` does not exist on gfx11, and `__bf16` lowers to a
// truncating convert (10/4096 values differ from RNE), so the rounding is done in integer and the
// pair is assembled with `v_perm_b32` (bytes 2,3 of each rounded word) -- one instruction where the
// shift/or form needed two, and no `>>16` per value.  Selector 0x03020706 = {ua.b2, ua.b3, ub.b2,
// ub.b3}: v_perm's byte 0..3 come from its second operand, 4..7 from its first.  Bit-identical
// (incl. +0/-0, `d == 0` weights) -- verified 1M random pairs, `PPL`/greedy/width gates.
// The shift/or form.  Kept for the IQ3 dequants: they are LUT-latency bound and the v_perm form,
// which needs all eight rounded words before it can start, is measurably slower there
// (IQ3_S GLU 1318 -> 1355 ms without DBUF, 1225 -> 1258 with).  Every other dequant prefers perm.
__device__ __forceinline__ uint32_t mmb_pack2_so(float a, float b) { return (uint32_t)mmb_f2bf(a) | ((uint32_t)mmb_f2bf(b) << 16); }

__device__ __forceinline__ uint32_t mmb_pack2(float a, float b) {
    uint32_t ua = __float_as_uint(a), ub = __float_as_uint(b);
    ua += 0x7fffu + ((ua >> 16) & 1u);
    ub += 0x7fffu + ((ub >> 16) & 1u);
    return __builtin_amdgcn_perm(ua, ub, 0x03020706u);
}
__constant__ int8_t mmb_kv_iq4nl[16] = {-127, -104, -83, -65, -49, -35, -22, -10, 1, 13, 25, 38, 53, 69, 89, 113};
__device__ __forceinline__ float mmb_h2f(uint16_t h) { return (float) __builtin_bit_cast(_Float16, h); }

// Portable WMMA dispatch: gfx12 renamed the builtins and uses the 8-half fragment above; gfx11 keeps
// the first-gen builtin and its 16-half fragment.  The kernels are runtime-gated to RDNA3_5/RDNA4
// (mmb_enabled), so only the matching arm is ever launched.
__device__ __forceinline__ v8f mmb_wmma_bf16(mmb_frag_t a, mmb_frag_t b, v8f c) {
#if defined(RDNA4)
    return __builtin_amdgcn_wmma_f32_16x16x16_bf16_w32_gfx12(__builtin_bit_cast(mmb_bf16_frag_t, a), __builtin_bit_cast(mmb_bf16_frag_t, b), c);
#else
    return __builtin_amdgcn_wmma_f32_16x16x16_bf16_w32(a, b, c);
#endif
}
__device__ __forceinline__ v8f mmb_wmma_f16(mmb_frag_t a, mmb_frag_t b, v8f c) {
#if defined(RDNA4)
    return __builtin_amdgcn_wmma_f32_16x16x16_f16_w32_gfx12(__builtin_bit_cast(mmb_f16_frag_t, a), __builtin_bit_cast(mmb_f16_frag_t, b), c);
#else
    return __builtin_amdgcn_wmma_f32_16x16x16_f16_w32(a, b, c);
#endif
}

__global__ void mmb_cvt_f32_bf16(const float * __restrict__ x, uint16_t * __restrict__ y, const size_t n) {
    size_t i = ((size_t)blockIdx.x * blockDim.x + threadIdx.x) * 8;
    if (i + 8 <= n) {
        const float4 a = *(const float4 *)(x + i), b = *(const float4 *)(x + i + 4);
        uint4 o; o.x = mmb_pack2(a.x, a.y); o.y = mmb_pack2(a.z, a.w); o.z = mmb_pack2(b.x, b.y); o.w = mmb_pack2(b.z, b.w);
        *(uint4 *)(y + i) = o;
    } else {
        for (; i < n; ++i) y[i] = mmb_f2bf(x[i]);
    }
}

// dequantize one weight row's 64-value quarter of an IQ4_XS super-block (136 B: half d, uint16
// scales_h, uint8 scales_l[4], qs[128]).  Same nibble LUT as IQ4_NL; the 6-bit sub-block scale is
// split scale = (scales_l[q4] nibble) | (scales_h bits 4*q4 .. +1) << 4, then value = d*(scale-32)*kv.
__device__ __forceinline__ void mmb_dq_row_iq4xs(const uint8_t * base, const int q4, uint32_t * arow) {
    const float dh = mmb_h2f(*(const uint16_t *) base);
    const uint32_t sh = *(const uint16_t *)(base + 2);
    const uint32_t sl = *(const uint8_t  *)(base + 4 + q4);
    const uint32_t * qA = (const uint32_t *)(base + 8 + 32 * q4);
    const uint32_t * qB = (const uint32_t *)(base + 8 + 32 * q4 + 16);
    const int sA = (int)((sl & 0xf) | (((sh >> (4 * q4))     & 3) << 4));
    const int sB = (int)((sl >> 4)  | (((sh >> (4 * q4 + 2)) & 3) << 4));
    const float dA = dh * (float)(sA - 32), dB = dh * (float)(sB - 32);
    const uint32_t L0 = 0x3f2d1801u, L1 = 0x766a5d4fu, L2 = 0xa6998d81u, L3 = 0xf1d9c5b5u;
#pragma unroll
    for (int blk = 0; blk < 2; ++blk) {
        const float d = blk ? dB : dA; const float md = -128.0f * d; const uint32_t * q = blk ? qB : qA; uint32_t * out = arow + blk * 16;
#pragma unroll
        for (int w = 0; w < 4; ++w) {
            const uint32_t v = q[w];
            const uint32_t nib[2] = { v & 0x0F0F0F0Fu, (v >> 4) & 0x0F0F0F0Fu };
            float x[2][4];
#pragma unroll
            for (int h = 0; h < 2; ++h) {
                const uint32_t n = nib[h];
                const uint32_t sel = n & 0x07070707u;
                const uint32_t pA = __builtin_amdgcn_perm(L1, L0, sel);
                const uint32_t pB = __builtin_amdgcn_perm(L3, L2, sel);
                const uint32_t m  = ((n >> 3) & 0x01010101u) * 0xFFu;
                const uint32_t u  = (pA & ~m) | (pB & m);
                x[h][0] = fmaf((float)(u & 0xFFu), d, md);
                x[h][1] = fmaf((float)((u >> 8) & 0xFFu), d, md);
                x[h][2] = fmaf((float)((u >> 16) & 0xFFu), d, md);
                x[h][3] = fmaf((float)(u >> 24), d, md);
            }
            out[2*w]         = mmb_pack2(x[0][0], x[0][1]); out[2*w + 1]     = mmb_pack2(x[0][2], x[0][3]);
            out[8 + 2*w]     = mmb_pack2(x[1][0], x[1][1]); out[8 + 2*w + 1] = mmb_pack2(x[1][2], x[1][3]);
        }
    }
}

// dequantize one weight row's 64-value quarter of a Q3_K super-block (110 B: hmask[32], qs[64],
// scales[12], half d) into 64 bf16.  Quarter q4: half n = q4>>1; two 32-value groups at j =
// 2*(q4&1)+jj, 2-bit field shift 2*j, high bit hmask bit (4n+j); 16-value sub-blocks is0 carry the
// 6-bit scale.  value = d*(scale-32) * ((qs>>shift)&3 - (high ? 0 : 4)).
__device__ __forceinline__ void mmb_dq_row_q3k(const uint8_t * base, const int q4, uint32_t * arow) {
    const uint8_t * hm = base;
    const uint8_t * qn = base + 32 + 32 * (q4 >> 1);
    const uint8_t * sc = base + 96;
    const float d = mmb_h2f(*(const uint16_t *)(base + 108));
    const int n = q4 >> 1;
#pragma unroll
    for (int jj = 0; jj < 2; ++jj) {
        const int j = 2 * (q4 & 1) + jj;
        const int shift = 2 * j;
        const uint32_t m = 1u << (4 * n + j);
        uint32_t * out = arow + 16 * jj;
#pragma unroll
        for (int is0 = 0; is0 < 2; ++is0) {
            const int is = 8 * n + 2 * j + is0;
            int us;
            if (is < 4)       us = (sc[is] & 0xF) | (((sc[is + 8] >> 0) & 3) << 4);
            else if (is < 8)  us = (sc[is] & 0xF) | (((sc[is + 4] >> 2) & 3) << 4);
            else if (is < 12) us = (sc[is - 8] >> 4) | (((sc[is]     >> 4) & 3) << 4);
            else              us = (sc[is - 8] >> 4) | (((sc[is - 4] >> 6) & 3) << 4);
            const float dl = d * (float)(us - 32);
#pragma unroll
            for (int l = 0; l < 16; l += 2) {
                const int li = 16 * is0 + l;
                const float v0 = dl * (float)((int)((qn[li]     >> shift) & 3) - ((hm[li]     & m) ? 0 : 4));
                const float v1 = dl * (float)((int)((qn[li + 1] >> shift) & 3) - ((hm[li + 1] & m) ? 0 : 4));
                out[li / 2] = mmb_pack2(v0, v1);
            }
        }
    }
}

// dequantize one weight row's 64-value quarter of an IQ3_XXS super-block (98 B: half d, qs[96]) into
// 64 bf16.  Quarter q4 = groups ib = 2*q4, 2*q4+1; each group is 4x8 values from the 256-entry grid
// with signs + a 4-bit sub-scale packed into the gas word at qs[64 + 4*ib].
__device__ __forceinline__ void mmb_dq_row_iq3xxs(const uint8_t * base, const int q4, uint32_t * arow) {
    const float d0 = mmb_h2f(*(const uint16_t *) base);
    const uint8_t * qs = base + 2;
#pragma unroll
    for (int g = 0; g < 2; ++g) {
        const int ib = 2 * q4 + g;
        const uint8_t * q3 = qs + 8 * ib;
        const uint32_t aux32 = *(const uint32_t *)(qs + 64 + 4 * ib);
        const float d = d0 * (0.5f + (float)(aux32 >> 28)) * 0.5f;
        uint32_t * out = arow + 16 * g;
#pragma unroll
        for (int il = 0; il < 4; ++il) {
            const uint32_t grid1 = iq3xxs_grid[q3[2 * il]];
            const uint32_t grid2 = iq3xxs_grid[q3[2 * il + 1]];
            const uint32_t signs = ksigns_iq2xs[(aux32 >> (7 * il)) & 127];
            float v[8];
#pragma unroll
            for (int j = 0; j < 4; ++j) {
                const float g1 = d * (float)((grid1 >> (8 * j)) & 0xFFu);
                const float g2 = d * (float)((grid2 >> (8 * j)) & 0xFFu);
                v[j]     = (signs >> j)       & 1u ? -g1 : g1;
                v[4 + j] = (signs >> (4 + j)) & 1u ? -g2 : g2;
            }
            out[4 * il + 0] = mmb_pack2_so(v[0], v[1]);
            out[4 * il + 1] = mmb_pack2_so(v[2], v[3]);
            out[4 * il + 2] = mmb_pack2_so(v[4], v[5]);
            out[4 * il + 3] = mmb_pack2_so(v[6], v[7]);
        }
    }
}

// dequantize one weight row's two consecutive IQ4_NL blocks (36 bytes) into 64 bf16 in LDS.
// LUT held in registers as (kv + 128) bytes and applied with v_perm_b32 (4 nibbles per op pair) instead of a per-lane
// indexed constant array (which lowers to one scalar-byte memory load per element). The value kv*d is produced as
// fma(kv+128, d, -128*d): -128*d is exact, so the single rounding equals RN(kv*d) -> bitwise the same BF16 as before.
__device__ __forceinline__ void mmb_dq_row36(const uint4 w0, const uint4 w1, const uint32_t w2, uint32_t * arow) {
    const uint32_t ws[9] = {w0.x, w0.y, w0.z, w0.w, w1.x, w1.y, w1.z, w1.w, w2};
    const float d0 = mmb_h2f((uint16_t)(ws[0] & 0xffff)), d1 = mmb_h2f((uint16_t)(ws[4] >> 16));
    const uint32_t q0[4] = { (ws[0] >> 16) | (ws[1] << 16), (ws[1] >> 16) | (ws[2] << 16), (ws[2] >> 16) | (ws[3] << 16), (ws[3] >> 16) | (ws[4] << 16) };
    const uint32_t q1[4] = { ws[5], ws[6], ws[7], ws[8] };
    // kv + 128 = {1,24,45,63,79,93,106,118,129,141,153,166,181,197,217,241} packed little-endian, 4 per dword
    const uint32_t L0 = 0x3f2d1801u, L1 = 0x766a5d4fu, L2 = 0xa6998d81u, L3 = 0xf1d9c5b5u;
#pragma unroll
    for (int blk = 0; blk < 2; ++blk) {
        const float d = blk ? d1 : d0; const float md = -128.0f * d; const uint32_t * q = blk ? q1 : q0; uint32_t * out = arow + blk * 16;
#pragma unroll
        for (int w = 0; w < 4; ++w) {
            const uint32_t v = q[w];
            const uint32_t nib[2] = { v & 0x0F0F0F0Fu, (v >> 4) & 0x0F0F0F0Fu };
            float x[2][4];
#pragma unroll
            for (int h = 0; h < 2; ++h) {
                const uint32_t n = nib[h];
                const uint32_t sel = n & 0x07070707u;
                const uint32_t pA = __builtin_amdgcn_perm(L1, L0, sel);   // entries 0..7
                const uint32_t pB = __builtin_amdgcn_perm(L3, L2, sel);   // entries 8..15
                const uint32_t m  = ((n >> 3) & 0x01010101u) * 0xFFu;      // 0xFF where the nibble >= 8
                const uint32_t u  = (pA & ~m) | (pB & m);
                x[h][0] = fmaf((float)(u & 0xFFu), d, md);
                x[h][1] = fmaf((float)((u >> 8) & 0xFFu), d, md);
                x[h][2] = fmaf((float)((u >> 16) & 0xFFu), d, md);
                x[h][3] = fmaf((float)(u >> 24), d, md);
            }
            out[2*w] = mmb_pack2(x[0][0], x[0][1]); out[2*w + 1] = mmb_pack2(x[0][2], x[0][3]);
            out[8 + 2*w] = mmb_pack2(x[1][0], x[1][1]); out[8 + 2*w + 1] = mmb_pack2(x[1][2], x[1][3]);
        }
    }
}

// dequantize one weight row's two consecutive Q8_0 blocks (68 bytes: d0 qs0[32] d1 qs1[32]) into 64 bf16 in LDS
__device__ __forceinline__ void mmb_dq_row68(const uint4 w0, const uint4 w1, const uint4 w2, const uint4 w3, const uint32_t w4, uint32_t * arow) {
    const uint32_t ws[17] = {w0.x,w0.y,w0.z,w0.w, w1.x,w1.y,w1.z,w1.w, w2.x,w2.y,w2.z,w2.w, w3.x,w3.y,w3.z,w3.w, w4};
    const float d0 = mmb_h2f((uint16_t)(ws[0] & 0xffff)), d1 = mmb_h2f((uint16_t)(ws[8] >> 16));
#pragma unroll
    for (int blk = 0; blk < 2; ++blk) {
        const float d = blk ? d1 : d0; uint32_t * out = arow + blk * 16;
#pragma unroll
        for (int w = 0; w < 8; ++w) {
            const uint32_t v = blk ? ws[9 + w] : ((ws[w] >> 16) | (ws[w + 1] << 16));
            const float e0 = d * (float)(int8_t)(v      ), e1 = d * (float)(int8_t)(v >>  8);
            const float e2 = d * (float)(int8_t)(v >> 16), e3 = d * (float)(int8_t)(v >> 24);
            out[2*w] = mmb_pack2(e0, e1); out[2*w + 1] = mmb_pack2(e2, e3);
        }
    }
}

// dequantize one weight row's 64-value quarter of a Q4_K super-block. A 256-value super-block is
// 144 B: half2 dm at +0, scales[12] at +4, qs[128] at +16; the quarter (ksh & 3) takes qs 32 bytes
// at +16 + 32*(ksh&3). Low nibbles -> values 0..31, high nibbles -> 32..63; sub-block scales are
// is = 2*(ksh&3) and is+1 via get_scale_min_k4. On-the-fly so a 120 GB model needs no bf16 shadow.
__device__ __forceinline__ void mmb_dq_row_q4k(const uint4 hdr, const uint4 q0, const uint4 q1, const int ksh, uint32_t * arow) {
    const float dall = mmb_h2f((uint16_t)(hdr.x & 0xffff));
    const float dmin = mmb_h2f((uint16_t)(hdr.x >> 16));
    auto sbyte = [&](const int i) -> uint32_t {
        const uint32_t w = (i < 4) ? hdr.y : (i < 8) ? hdr.z : hdr.w;
        return (w >> (8 * (i & 3))) & 0xFFu;
    };
    auto gsm = [&](const int j, float & dd, float & mm) {
        const uint32_t q0b = sbyte(j), q4b = sbyte(j + 4);
        uint32_t sc, mi;
        if (j < 4) { sc = q0b & 63u; mi = q4b & 63u; }
        else       { sc = (q4b & 0xFu) | ((sbyte(j - 4) >> 6) << 4); mi = (q4b >> 4) | ((q0b >> 6) << 4); }
        dd = dall * (float) sc; mm = dmin * (float) mi;
    };
    const int is = 2 * (ksh & 3);
    float d1, m1, d2, m2; gsm(is, d1, m1); gsm(is + 1, d2, m2);
    const uint32_t qs[8] = {q0.x, q0.y, q0.z, q0.w, q1.x, q1.y, q1.z, q1.w};
#pragma unroll
    for (int w = 0; w < 8; ++w) {
        const uint32_t v = qs[w];
        const uint32_t lo = v & 0x0F0F0F0Fu, hi = (v >> 4) & 0x0F0F0F0Fu;
        arow[2*w]             = mmb_pack2(fmaf((float)( lo        & 0xFFu), d1, -m1), fmaf((float)((lo >>  8) & 0xFFu), d1, -m1));
        arow[2*w + 1]         = mmb_pack2(fmaf((float)((lo >> 16) & 0xFFu), d1, -m1), fmaf((float)((lo >> 24)      ), d1, -m1));
        arow[16 + 2*w]        = mmb_pack2(fmaf((float)( hi        & 0xFFu), d2, -m2), fmaf((float)((hi >>  8) & 0xFFu), d2, -m2));
        arow[16 + 2*w + 1]    = mmb_pack2(fmaf((float)((hi >> 16) & 0xFFu), d2, -m2), fmaf((float)((hi >> 24)      ), d2, -m2));
    }
}

// dequantize one weight row's two consecutive Q5_1 blocks (48 B: per 32 values, half2 dm + qh[4] +
// qs[16]) into 64 bf16. value = d * ((qs nibble) | (qh bit << 4)) + m.
__device__ __forceinline__ void mmb_dq_row_q5_1(const uint4 w0, const uint4 w1, const uint4 w2, uint32_t * arow) {
    const uint32_t ws[12] = {w0.x,w0.y,w0.z,w0.w, w1.x,w1.y,w1.z,w1.w, w2.x,w2.y,w2.z,w2.w};
#pragma unroll
    for (int blk = 0; blk < 2; ++blk) {
        const uint32_t * b = ws + blk * 6;
        const float d = mmb_h2f((uint16_t)(b[0] & 0xffff));
        const float m = mmb_h2f((uint16_t)(b[0] >> 16));
        const uint32_t qh = b[1];
        uint32_t * out = arow + blk * 16;
#pragma unroll
        for (int w = 0; w < 4; ++w) {
            const uint32_t v = b[2 + w];
            const uint32_t lo = v & 0x0F0F0F0Fu, hi = (v >> 4) & 0x0F0F0F0Fu;
            const uint32_t bl = (qh >> (4 * w))      & 0xFu;
            const uint32_t bh = (qh >> (4 * w + 16)) & 0xFu;
            const uint32_t sl = (bl & 1 ? 0x10u : 0u) | (bl & 2 ? 0x1000u : 0u) | (bl & 4 ? 0x100000u : 0u) | (bl & 8 ? 0x10000000u : 0u);
            const uint32_t sh = (bh & 1 ? 0x10u : 0u) | (bh & 2 ? 0x1000u : 0u) | (bh & 4 ? 0x100000u : 0u) | (bh & 8 ? 0x10000000u : 0u);
            const uint32_t vl = lo | sl, vh = hi | sh;
            out[2*w]         = mmb_pack2(fmaf((float)( vl        & 0xFFu), d, m), fmaf((float)((vl >>  8) & 0xFFu), d, m));
            out[2*w + 1]     = mmb_pack2(fmaf((float)((vl >> 16) & 0xFFu), d, m), fmaf((float)((vl >> 24)      ), d, m));
            out[8 + 2*w]     = mmb_pack2(fmaf((float)( vh        & 0xFFu), d, m), fmaf((float)((vh >>  8) & 0xFFu), d, m));
            out[8 + 2*w + 1] = mmb_pack2(fmaf((float)((vh >> 16) & 0xFFu), d, m), fmaf((float)((vh >> 24)      ), d, m));
        }
    }
}

// dequantize one weight row's two consecutive Q4_0 blocks (36 B: per 32 values, half d + qs[16])
// into 64 bf16. value = d * (nibble - 8).  Same 18-byte footprint as IQ4_NL, so the load shape and
// output ordering match mmb_dq_row36; only the value map differs.
__device__ __forceinline__ void mmb_dq_row_q4_0(const uint8_t * base, uint32_t * arow) {
#pragma unroll
    for (int blk = 0; blk < 2; ++blk) {
        const uint8_t * b = base + blk * 18;
        const float d = mmb_h2f(*(const uint16_t *) b), md = -8.0f * d;
        const uint32_t * q = (const uint32_t *)(b + 2);
        uint32_t * out = arow + blk * 16;
#pragma unroll
        for (int w = 0; w < 4; ++w) {
            const uint32_t v = q[w];
            const uint32_t lo = v & 0x0F0F0F0Fu, hi = (v >> 4) & 0x0F0F0F0Fu;
            out[2*w]         = mmb_pack2(fmaf((float)( lo        & 0xFFu), d, md), fmaf((float)((lo >>  8) & 0xFFu), d, md));
            out[2*w + 1]     = mmb_pack2(fmaf((float)((lo >> 16) & 0xFFu), d, md), fmaf((float)((lo >> 24)      ), d, md));
            out[8 + 2*w]     = mmb_pack2(fmaf((float)( hi        & 0xFFu), d, md), fmaf((float)((hi >>  8) & 0xFFu), d, md));
            out[8 + 2*w + 1] = mmb_pack2(fmaf((float)((hi >> 16) & 0xFFu), d, md), fmaf((float)((hi >> 24)      ), d, md));
        }
    }
}

// dequantize one weight row's two consecutive Q4_1 blocks (40 B: per 32 values, half2 dm + qs[16])
// into 64 bf16. value = d * nibble + m.
__device__ __forceinline__ void mmb_dq_row_q4_1(const uint8_t * base, uint32_t * arow) {
#pragma unroll
    for (int blk = 0; blk < 2; ++blk) {
        const uint8_t * b = base + blk * 20;
        const uint32_t dm = *(const uint32_t *) b;
        const float d = mmb_h2f((uint16_t)(dm & 0xffff)), m = mmb_h2f((uint16_t)(dm >> 16));
        const uint32_t * q = (const uint32_t *)(b + 4);
        uint32_t * out = arow + blk * 16;
#pragma unroll
        for (int w = 0; w < 4; ++w) {
            const uint32_t v = q[w];
            const uint32_t lo = v & 0x0F0F0F0Fu, hi = (v >> 4) & 0x0F0F0F0Fu;
            out[2*w]         = mmb_pack2(fmaf((float)( lo        & 0xFFu), d, m), fmaf((float)((lo >>  8) & 0xFFu), d, m));
            out[2*w + 1]     = mmb_pack2(fmaf((float)((lo >> 16) & 0xFFu), d, m), fmaf((float)((lo >> 24)      ), d, m));
            out[8 + 2*w]     = mmb_pack2(fmaf((float)( hi        & 0xFFu), d, m), fmaf((float)((hi >>  8) & 0xFFu), d, m));
            out[8 + 2*w + 1] = mmb_pack2(fmaf((float)((hi >> 16) & 0xFFu), d, m), fmaf((float)((hi >> 24)      ), d, m));
        }
    }
}

// dequantize one weight row's two consecutive Q5_0 blocks (44 B: per 32 values, half d + qh[4] +
// qs[16]) into 64 bf16. value = d * ((qs nibble) | (qh 5th bit << 4) - 16).  Same field layout as
// Q5_1; the 5th bit selection mirrors mmb_dq_row_q5_1 exactly (qh bit e in both halves).
__device__ __forceinline__ void mmb_dq_row_q5_0(const uint8_t * base, uint32_t * arow) {
#pragma unroll
    for (int blk = 0; blk < 2; ++blk) {
        const uint8_t * b = base + blk * 22;
        const float d = mmb_h2f(*(const uint16_t *) b), md = -16.0f * d;
        const uint32_t qh = *(const uint32_t *)(b + 2);
        const uint32_t * q = (const uint32_t *)(b + 6);
        uint32_t * out = arow + blk * 16;
#pragma unroll
        for (int w = 0; w < 4; ++w) {
            const uint32_t v = q[w];
            const uint32_t lo = v & 0x0F0F0F0Fu, hi = (v >> 4) & 0x0F0F0F0Fu;
            const uint32_t bl = (qh >> (4 * w))      & 0xFu;
            const uint32_t bh = (qh >> (4 * w + 16)) & 0xFu;
            const uint32_t sl = (bl & 1 ? 0x10u : 0u) | (bl & 2 ? 0x1000u : 0u) | (bl & 4 ? 0x100000u : 0u) | (bl & 8 ? 0x10000000u : 0u);
            const uint32_t sh = (bh & 1 ? 0x10u : 0u) | (bh & 2 ? 0x1000u : 0u) | (bh & 4 ? 0x100000u : 0u) | (bh & 8 ? 0x10000000u : 0u);
            const uint32_t vl = lo | sl, vh = hi | sh;
            out[2*w]         = mmb_pack2(fmaf((float)( vl        & 0xFFu), d, md), fmaf((float)((vl >>  8) & 0xFFu), d, md));
            out[2*w + 1]     = mmb_pack2(fmaf((float)((vl >> 16) & 0xFFu), d, md), fmaf((float)((vl >> 24)      ), d, md));
            out[8 + 2*w]     = mmb_pack2(fmaf((float)( vh        & 0xFFu), d, md), fmaf((float)((vh >>  8) & 0xFFu), d, md));
            out[8 + 2*w + 1] = mmb_pack2(fmaf((float)((vh >> 16) & 0xFFu), d, md), fmaf((float)((vh >> 24)      ), d, md));
        }
    }
}

// dequantize one weight row's two consecutive MXFP4 blocks (34 B: per 32 values, uint8 e (E8M0)
// shared exponent + qs[16]) into 64 bf16. value = 2^(e-127) * kvalues_mxfp4[nibble] * 0.5 (the
// table holds the doubled E2M1 values).  Same nibble positions as Q4_0, so the output ordering
// mirrors mmb_dq_row_q4_0.
__device__ __forceinline__ void mmb_dq_row_mxfp4(const uint8_t * base, uint32_t * arow) {
#pragma unroll
    for (int blk = 0; blk < 2; ++blk) {
        const uint8_t * b = base + blk * 17;
        const float d = ggml_cuda_e8m0_to_fp32(b[0]) * 0.5f;
        const uint32_t * q = (const uint32_t *)(b + 1);
        uint32_t * out = arow + blk * 16;
#pragma unroll
        for (int w = 0; w < 4; ++w) {
            const uint32_t v = q[w];
            const uint32_t lo = v & 0x0F0F0F0Fu, hi = (v >> 4) & 0x0F0F0F0Fu;
            out[2*w]         = mmb_pack2(d * (float)kvalues_mxfp4[ lo        & 0xFu], d * (float)kvalues_mxfp4[(lo >>  8) & 0xFu]);
            out[2*w + 1]     = mmb_pack2(d * (float)kvalues_mxfp4[(lo >> 16) & 0xFu], d * (float)kvalues_mxfp4[(lo >> 24)      & 0xFu]);
            out[8 + 2*w]     = mmb_pack2(d * (float)kvalues_mxfp4[ hi        & 0xFu], d * (float)kvalues_mxfp4[(hi >>  8) & 0xFu]);
            out[8 + 2*w + 1] = mmb_pack2(d * (float)kvalues_mxfp4[(hi >> 16) & 0xFu], d * (float)kvalues_mxfp4[(hi >> 24)      & 0xFu]);
        }
    }
}

// dequantize one weight row's NVFP4 block (36 B: 64 values, uint8 d[4] UE4M3 per-16-element scales
// + qs[32]) into 64 bf16.  value = ue4m3(d[sub]) * kvalues_mxfp4[nibble] (the UE4M3 scale is halved
// and the E2M1 table doubled, so the product is exact).  One 64-value block per K-step.
__device__ __forceinline__ void mmb_dq_row_nvfp4(const uint8_t * base, uint32_t * arow) {
    const uint8_t * sc = base;
    const uint32_t * q = (const uint32_t *)(base + 4);
#pragma unroll
    for (int s = 0; s < 4; ++s) {
        const float d = ggml_cuda_ue4m3_to_fp32(sc[s]);
        uint32_t * out = arow + s * 8;
#pragma unroll
        for (int w = 0; w < 2; ++w) {
            const uint32_t v = q[s * 2 + w];
            const uint32_t lo = v & 0x0F0F0F0Fu, hi = (v >> 4) & 0x0F0F0F0Fu;
            out[2*w]     = mmb_pack2(d * (float)kvalues_mxfp4[ lo        & 0xFu], d * (float)kvalues_mxfp4[(lo >>  8) & 0xFu]);
            out[2*w + 1] = mmb_pack2(d * (float)kvalues_mxfp4[(lo >> 16) & 0xFu], d * (float)kvalues_mxfp4[(lo >> 24)      & 0xFu]);
            out[4 + 2*w]     = mmb_pack2(d * (float)kvalues_mxfp4[ hi        & 0xFu], d * (float)kvalues_mxfp4[(hi >>  8) & 0xFu]);
            out[4 + 2*w + 1] = mmb_pack2(d * (float)kvalues_mxfp4[(hi >> 16) & 0xFu], d * (float)kvalues_mxfp4[(hi >> 24)      & 0xFu]);
        }
    }
}

// dequantize one weight row's 64-value quarter of an IQ3_S super-block (110 B: half d, qs[64],
// qh[8], signs[32], scales[4]) into 64 bf16. q4 = ksh & 3 selects the 64-value quarter
// (groups ib = 2*q4, 2*q4+1). Fields are non-contiguous, so this loads straight from the row base
// (called from store_lds, not the register prefetch).  value = d*(1+2*scale_nibble) * grid[3-bit]*sign.
__device__ __forceinline__ void mmb_dq_row_iq3s(const uint8_t * base, const int q4, uint32_t * arow) {
    const float d = mmb_h2f(*(const uint16_t *) base);
    const uint8_t sc = *(const uint8_t *)(base + 106 + q4);
    const uint8_t * qs = base + 2 + 16 * q4;
    const uint8_t * qh = base + 66 + 2 * q4;
    const uint8_t * sg = base + 74 + 8 * q4;
#pragma unroll
    for (int g = 0; g < 2; ++g) {
        const float dg = d * (float)(1 + 2 * ((sc >> (4 * g)) & 0xf));
        uint32_t * out = arow + 16 * g;
        const uint32_t qhg = qh[g];
#pragma unroll
        for (int il = 0; il < 4; ++il) {
            const uint32_t grid1 = iq3s_grid[qs[8*g + 2*il    ] | ((qhg << (8 - 2*il)) & 256)];
            const uint32_t grid2 = iq3s_grid[qs[8*g + 2*il + 1] | ((qhg << (7 - 2*il)) & 256)];
            const uint32_t signs = sg[4*g + il];
            float v[8];
#pragma unroll
            for (int j = 0; j < 4; ++j) {
                const float g1 = dg * (float)((grid1 >> (8*j)) & 0xFFu);
                const float g2 = dg * (float)((grid2 >> (8*j)) & 0xFFu);
                v[j]     = (signs >> j)     & 1u ? -g1 : g1;
                v[4 + j] = (signs >> (4+j)) & 1u ? -g2 : g2;
            }
            out[4*il + 0] = mmb_pack2_so(v[0], v[1]);
            out[4*il + 1] = mmb_pack2_so(v[2], v[3]);
            out[4*il + 2] = mmb_pack2_so(v[4], v[5]);
            out[4*il + 3] = mmb_pack2_so(v[6], v[7]);
        }
    }
}

// dequantize one weight row's 64-value quarter of an IQ2_S super-block (82 B: half d, qs[64],
// qh[8], scales[8]) into 64 bf16.  q4 = ksh & 3 selects the 64-value quarter (sub-blocks
// ib = 2*q4, 2*q4+1); each 32-value sub-block is 4 groups of 8.  Fields are non-contiguous, so this
// loads straight from the row base.  value = d*(0.5 + scale_nibble)*0.25 * iq2s_grid[idx][j] * sign,
// idx = qs[4*ib+l] | ((qh[ib] << (8-2*l)) & 0x300).
__device__ __forceinline__ void mmb_dq_row_iq2s(const uint8_t * base, const int q4, uint32_t * arow) {
    const float d = mmb_h2f(*(const uint16_t *) base);
    const uint8_t * qs     = base + 2;    // [0..31] grid-index low byte, [32..63] signs
    const uint8_t * qh     = base + 66;   // 8 x 2-bit index highs
    const uint8_t * scales = base + 74;   // 8 x 2 x 4-bit
#pragma unroll
    for (int g = 0; g < 2; ++g) {
        const int ib = 2*q4 + g;
        const uint8_t sc = scales[ib];
        uint32_t * out = arow + 16*g;
#pragma unroll
        for (int l = 0; l < 4; ++l) {
            const float dl = d * (0.5f + (float)((sc >> (4*(l/2))) & 0xf)) * 0.25f;
            const uint64_t grid = iq2s_grid[qs[4*ib + l] | ((qh[ib] << (8 - 2*l)) & 0x300)];
            const uint8_t signs = qs[32 + 4*ib + l];
            float v[8];
#pragma unroll
            for (int j = 0; j < 8; ++j) {
                const float gv = dl * (float)((grid >> (8*j)) & 0xFFu);
                v[j] = (signs >> j) & 1u ? -gv : gv;
            }
            out[4*l + 0] = mmb_pack2_so(v[0], v[1]);
            out[4*l + 1] = mmb_pack2_so(v[2], v[3]);
            out[4*l + 2] = mmb_pack2_so(v[4], v[5]);
            out[4*l + 3] = mmb_pack2_so(v[6], v[7]);
        }
    }
}

// dequantize one weight row's 64-value quarter of an IQ2_XS super-block (74 B: half d, qs[64],
// scales[8]) into 64 bf16.  q4 = ksh & 3 selects the quarter (sub-blocks ib = 2*q4, 2*q4+1); each
// sub-block is 4 uint16 = 4 groups of 8.  idx = q2[l]: low 9 bits -> iq2xs_grid[512], bits 9..15 ->
// ksigns_iq2xs[128]; value = d*(0.5 + scales[ib] nibble)*0.25 * grid[j] * sign.
__device__ __forceinline__ void mmb_dq_row_iq2xs(const uint8_t * base, const int q4, uint32_t * arow) {
    const float d = mmb_h2f(*(const uint16_t *) base);
    const uint16_t * qs = (const uint16_t *)(base + 2);   // QK_K/8 uint16
    const uint8_t  * scales = base + 66;                  // QK_K/32
#pragma unroll
    for (int g = 0; g < 2; ++g) {
        const int ib = 2*q4 + g;
        const uint16_t * q2 = qs + 4*ib;
        const uint8_t sc = scales[ib];
        uint32_t * out = arow + 16*g;
#pragma unroll
        for (int l = 0; l < 4; ++l) {
            const uint32_t idx  = q2[l];
            const uint64_t grid = iq2xs_grid[idx & 511];
            const float    dl   = d * (0.5f + (float)((sc >> (4*(l/2))) & 0xf)) * 0.25f;
            const uint8_t signs = ksigns_iq2xs[idx >> 9];
            float v[8];
#pragma unroll
            for (int j = 0; j < 8; ++j) {
                const float gv = dl * (float)((grid >> (8*j)) & 0xFFu);
                v[j] = (signs >> j) & 1u ? -gv : gv;
            }
            out[4*l + 0] = mmb_pack2_so(v[0], v[1]);
            out[4*l + 1] = mmb_pack2_so(v[2], v[3]);
            out[4*l + 2] = mmb_pack2_so(v[4], v[5]);
            out[4*l + 3] = mmb_pack2_so(v[6], v[7]);
        }
    }
}

// dequantize one weight row's 64-value quarter of an IQ2_XXS super-block (66 B: half d, qs[32] as
// uint16) into 64 bf16.  q4 = ksh & 3 selects the quarter (sub-blocks ib = 2*q4, 2*q4+1); each
// sub-block is 8 bytes = 2 uint32: bytes 0..3 are four 8-bit iq2xxs_grid[256] indices, the second
// uint32 holds the 4-bit scale (bits 28..31) and four 7-bit ksigns_iq2xs indices (bits 0,7,14,21).
__device__ __forceinline__ void mmb_dq_row_iq2xxs(const uint8_t * base, const int q4, uint32_t * arow) {
    const float d = mmb_h2f(*(const uint16_t *) base);
    const uint8_t * qs = base + 2;
#pragma unroll
    for (int g = 0; g < 2; ++g) {
        const int ib = 2*q4 + g;
        const uint32_t a0 = *(const uint32_t *)(qs + 8*ib);
        const uint32_t a1 = *(const uint32_t *)(qs + 8*ib + 4);
        const uint8_t * aux8 = (const uint8_t *) &a0;
        const float db = d * (0.5f + (float)(a1 >> 28)) * 0.25f;
        uint32_t * out = arow + 16*g;
#pragma unroll
        for (int l = 0; l < 4; ++l) {
            const uint64_t grid = iq2xxs_grid[aux8[l]];
            const uint8_t signs = ksigns_iq2xs[(a1 >> (7*l)) & 127];
            float v[8];
#pragma unroll
            for (int j = 0; j < 8; ++j) {
                const float gv = db * (float)((grid >> (8*j)) & 0xFFu);
                v[j] = (signs >> j) & 1u ? -gv : gv;
            }
            out[4*l + 0] = mmb_pack2_so(v[0], v[1]);
            out[4*l + 1] = mmb_pack2_so(v[2], v[3]);
            out[4*l + 2] = mmb_pack2_so(v[4], v[5]);
            out[4*l + 3] = mmb_pack2_so(v[6], v[7]);
        }
    }
}

// PARTS-way split of the above across the threads that share one A row, so that a BM < MMB_NT tile
// (the GLU's BM=64) does not run the A dequant on only MMB_NT/BM of the warps.  Thread `p` takes the
// `il` groups {p, p+PARTS, ...} for both g, writing the same LDS dwords as the whole-row form.
template <int PARTS>
__device__ __forceinline__ void mmb_dq_row_iq3s_p(const uint8_t * base, const int q4, uint32_t * arow, const int p) {
    const float d = mmb_h2f(*(const uint16_t *) base);
    const uint8_t sc = *(const uint8_t *)(base + 106 + q4);
    const uint8_t * qs = base + 2 + 16 * q4;
    const uint8_t * qh = base + 66 + 2 * q4;
    const uint8_t * sg = base + 74 + 8 * q4;
#pragma unroll
    for (int g = 0; g < 2; ++g) {
        const float dg = d * (float)(1 + 2 * ((sc >> (4 * g)) & 0xf));
        uint32_t * out = arow + 16 * g;
        const uint32_t qhg = qh[g];
#pragma unroll
        for (int il = p; il < 4; il += PARTS) {
            const uint32_t grid1 = iq3s_grid[qs[8*g + 2*il    ] | ((qhg << (8 - 2*il)) & 256)];
            const uint32_t grid2 = iq3s_grid[qs[8*g + 2*il + 1] | ((qhg << (7 - 2*il)) & 256)];
            const uint32_t signs = sg[4*g + il];
            float v[8];
#pragma unroll
            for (int j = 0; j < 4; ++j) {
                const float g1 = dg * (float)((grid1 >> (8*j)) & 0xFFu);
                const float g2 = dg * (float)((grid2 >> (8*j)) & 0xFFu);
                v[j]     = (signs >> j)     & 1u ? -g1 : g1;
                v[4 + j] = (signs >> (4+j)) & 1u ? -g2 : g2;
            }
            out[4*il + 0] = mmb_pack2_so(v[0], v[1]);
            out[4*il + 1] = mmb_pack2_so(v[2], v[3]);
            out[4*il + 2] = mmb_pack2_so(v[4], v[5]);
            out[4*il + 3] = mmb_pack2_so(v[6], v[7]);
        }
    }
}

// Register-resident form of the split dequant above.  `mmb_iq3s_preload` runs inside the
// `load_regs` prefetch (which overlaps the previous K-step's WMMA) and `mmb_dq_iq3s_r` then works
// purely out of registers, so the dequant phase that runs exposed between the WMMA phases issues no
// global loads at all.  Only the `il` groups this thread owns are fetched (4/PARTS of them), and the
// two `qs` bytes a grid lookup needs are adjacent, so they come back as one uint16.
template <int PARTS>
struct mmb_iq3s_r {
    uint32_t d, sc, qh;
    uint16_t qs[2][4 / PARTS];
    uint8_t  sg[2][4 / PARTS];
};

template <int PARTS>
__device__ __forceinline__ mmb_iq3s_r<PARTS> mmb_iq3s_preload(const uint8_t * base, const int q4, const int p) {
    mmb_iq3s_r<PARTS> r;
    r.d  = *(const uint16_t *) base;
    r.sc = *(const uint8_t *)(base + 106 + q4);
    r.qh = *(const uint16_t *)(base + 66 + 2 * q4);
    const uint8_t * qs = base + 2 + 16 * q4;
    const uint8_t * sg = base + 74 + 8 * q4;
#pragma unroll
    for (int g = 0; g < 2; ++g)
#pragma unroll
        for (int it = 0; it < 4 / PARTS; ++it) {
            const int il = p + it * PARTS;
            r.qs[g][it] = *(const uint16_t *)(qs + 8 * g + 2 * il);
            r.sg[g][it] = sg[4 * g + il];
        }
    return r;
}

template <int PARTS>
__device__ __forceinline__ void mmb_dq_iq3s_r(const mmb_iq3s_r<PARTS> & R, uint32_t * arow, const int p) {
    const float d = mmb_h2f((uint16_t) R.d);
    const uint8_t sc = (uint8_t) R.sc;
#pragma unroll
    for (int g = 0; g < 2; ++g) {
        const float dg = d * (float)(1 + 2 * ((sc >> (4 * g)) & 0xf));
        uint32_t * out = arow + 16 * g;
        const uint32_t qhg = (R.qh >> (8 * g)) & 0xff;
#pragma unroll
        for (int it = 0; it < 4 / PARTS; ++it) {
            const int il = p + it * PARTS;
            const uint32_t qs2 = R.qs[g][it];
            const uint32_t grid1 = iq3s_grid[(qs2 & 0xffu)     | ((qhg << (8 - 2*il)) & 256)];
            const uint32_t grid2 = iq3s_grid[(qs2 >> 8)        | ((qhg << (7 - 2*il)) & 256)];
            const uint32_t signs = R.sg[g][it];
            float v[8];
#pragma unroll
            for (int j = 0; j < 4; ++j) {
                const float g1 = dg * (float)((grid1 >> (8*j)) & 0xFFu);
                const float g2 = dg * (float)((grid2 >> (8*j)) & 0xFFu);
                v[j]     = (signs >> j)     & 1u ? -g1 : g1;
                v[4 + j] = (signs >> (4+j)) & 1u ? -g2 : g2;
            }
            out[4*il + 0] = mmb_pack2_so(v[0], v[1]);
            out[4*il + 1] = mmb_pack2_so(v[2], v[3]);
            out[4*il + 2] = mmb_pack2_so(v[4], v[5]);
            out[4*il + 3] = mmb_pack2_so(v[6], v[7]);
        }
    }
}

// dequantize one weight row's 64-value quarter of a Q5_K super-block (176 B: half2 dm, scales[12],
// qh[32], qs[128]) into 64 bf16.  Same affine form as Q4_K plus the 5th bit from qh (bit 2*q4 for
// the low nibbles, 2*q4+1 for the high nibbles).  Loaded from the row base in store_lds.
__device__ __forceinline__ void mmb_dq_row_q5k(const uint8_t * base, const int q4, uint32_t * arow) {
    const uint4 hdr = *(const uint4 *) base;
    const float dall = mmb_h2f((uint16_t)(hdr.x & 0xffff));
    const float dmin = mmb_h2f((uint16_t)(hdr.x >> 16));
    auto sbyte = [&](const int i) -> uint32_t {
        const uint32_t w = (i < 4) ? hdr.y : (i < 8) ? hdr.z : hdr.w;
        return (w >> (8 * (i & 3))) & 0xFFu;
    };
    auto gsm = [&](const int j, float & dd, float & mm) {
        const uint32_t q0b = sbyte(j), q4b = sbyte(j + 4);
        uint32_t sc, mi;
        if (j < 4) { sc = q0b & 63u; mi = q4b & 63u; }
        else       { sc = (q4b & 0xFu) | ((sbyte(j - 4) >> 6) << 4); mi = (q4b >> 4) | ((q0b >> 6) << 4); }
        dd = dall * (float) sc; mm = dmin * (float) mi;
    };
    const int is = 2 * q4;
    float d1, m1, d2, m2; gsm(is, d1, m1); gsm(is + 1, d2, m2);
    const uint8_t * qh = base + 16;
    const uint8_t * qs = base + 48 + 32 * q4;
    const int bit_lo = 2 * q4, bit_hi = 2 * q4 + 1;
#pragma unroll
    for (int j = 0; j < 32; j += 2) {
        const uint8_t p0 = qs[j], p1 = qs[j+1], h0 = qh[j], h1 = qh[j+1];
        const float lo0 = fmaf((float)((p0 & 0xF) + (((h0 >> bit_lo) & 1) ? 16 : 0)), d1, -m1);
        const float lo1 = fmaf((float)((p1 & 0xF) + (((h1 >> bit_lo) & 1) ? 16 : 0)), d1, -m1);
        const float hi0 = fmaf((float)((p0 >> 4) + (((h0 >> bit_hi) & 1) ? 16 : 0)), d2, -m2);
        const float hi1 = fmaf((float)((p1 >> 4) + (((h1 >> bit_hi) & 1) ? 16 : 0)), d2, -m2);
        arow[j/2]      = mmb_pack2(lo0, lo1);
        arow[16 + j/2] = mmb_pack2(hi0, hi1);
    }
}

// dequantize one weight row's 64-value quarter of a Q6_K super-block (210 B: ql[128], qh[64],
// scales[16] int8, half d) into 64 bf16.  value = d * scales[..] * (q6 - 32); q6 = 4-bit ql |
// (2-bit qh << 4).  Quarter q4: ip = q4>>1 (half), high = q4&1 (ql nibble), bit sh = high?4:0.
__device__ __forceinline__ void mmb_dq_row_q6k(const uint8_t * base, const int q4, uint32_t * arow) {
    const int ip = q4 >> 1, high = q4 & 1;
    const float d = mmb_h2f(*(const uint16_t *)(base + 208));
    const uint8_t * ql = base + 64 * ip;
    const uint8_t * qh = base + 128 + 32 * ip;
    const int8_t  * sc = (const int8_t *)(base + 192);
    const int sh = high ? 4 : 0;
    const int ib = 8 * ip + (high ? 4 : 0);
#pragma unroll
    for (int il = 0; il < 32; il += 2) {
        float va[2], vb[2];
#pragma unroll
        for (int t = 0; t < 2; ++t) {
            const int j = il + t;
            const uint8_t qa = ql[j], qb = ql[32 + j], h = qh[j];
            const int sa = ib + (j >> 4);
            const int a6 = (high ? (qa >> 4) : (qa & 0xF)) | (((h >> sh)       & 3) << 4);
            const int b6 = (high ? (qb >> 4) : (qb & 0xF)) | (((h >> (sh + 2)) & 3) << 4);
            va[t] = d * (float) sc[sa]     * (float)(a6 - 32);
            vb[t] = d * (float) sc[sa + 2] * (float)(b6 - 32);
        }
        arow[il/2]      = mmb_pack2(va[0], va[1]);
        arow[16 + il/2] = mmb_pack2(vb[0], vb[1]);
    }
}

template <typename DRowFn>
__device__ __forceinline__ void mmb_store_tile(const v8f & acc, float * __restrict__ stg, float * __restrict__ D, uint16_t * __restrict__ Dh,
        const bool store_f32, const int M, DRowFn drow, const int n_base, const int m_base, const int lane) {
    const int cm = lane & 15, cn = lane >> 4;
#pragma unroll
    for (int e = 0; e < 8; ++e) { stg[MMB_ACC_M(e, cn) * 16 + cm] = acc[e]; }
    __syncthreads();
    const int n = lane >> 1, half = lane & 1;
    const int dr = drow(n_base + n);
    if (dr >= 0) {
        const float4 v0 = *(const float4 *)(stg + n * 16 + half * 8), v1 = *(const float4 *)(stg + n * 16 + half * 8 + 4);
        const size_t base = (size_t)dr * M + m_base + half * 8;
        const bool full = m_base + 16 <= M;
        if (store_f32) {
            if (full && (M & 3) == 0) { *(float4 *)(D + base) = v0; *(float4 *)(D + base + 4) = v1; }
            else { const float vv[8] = {v0.x, v0.y, v0.z, v0.w, v1.x, v1.y, v1.z, v1.w};
#pragma unroll
                   for (int k = 0; k < 8; ++k) { if (m_base + half * 8 + k < M) D[base + k] = vv[k]; } }
        }
        if (Dh) {
            if (full && (M & 7) == 0) { *(uint4 *)(Dh + base) = make_uint4(mmb_pack2(v0.x, v0.y), mmb_pack2(v0.z, v0.w), mmb_pack2(v1.x, v1.y), mmb_pack2(v1.z, v1.w)); }
            else { const float vv[8] = {v0.x, v0.y, v0.z, v0.w, v1.x, v1.y, v1.z, v1.w};
#pragma unroll
                   for (int k = 0; k < 8; ++k) { if (m_base + half * 8 + k < M) Dh[base + k] = mmb_f2bf(vv[k]); } }
        }
    }
    __syncthreads();
}
template <int BM, int BN, int WTM, int WTN, int WTYPE, bool TAIL, bool DBUF, bool DBUF2, typename XRowFn, typename DRowFn>
__device__ __forceinline__ void mmb_tile_gemm(const uint8_t * __restrict__ Wbase, const size_t wrow_bytes, const int a_rows,
        const uint16_t * __restrict__ Xh, const int K, XRowFn xrow, float * __restrict__ D, uint16_t * __restrict__ Dh, const bool store_f32, const int M, DRowFn drow, const int m0,
        const int n_cols, uint16_t * As, uint16_t * Bs) {
    constexpr int WAVES_M = BM / WTM, TM = WTM / 16, TN = WTN / 16;
    constexpr int A_ITEMS = (BM + MMB_NT - 1) / MMB_NT;
    // WTYPE 5 (IQ3_S): when BM < MMB_NT the row-per-thread A mapping leaves warps idle, so the
    // threads sharing a row split its 8 (g,il) groups instead (see mmb_dq_row_iq3s_p).
    constexpr bool SPLIT_A = (WTYPE == 5 && MMB_NT > BM && MMB_NT % BM == 0);
    constexpr int PARTS = SPLIT_A ? (MMB_NT / BM) : 1;
    constexpr int B_ITEMS = (BN * 8) / MMB_NT;
    const int tid = threadIdx.x, lane = tid & 31, wave = tid >> 5;
    const int wm = wave % WAVES_M, wn = wave / WAVES_M;
    uint4 a0[A_ITEMS], a1[A_ITEMS], a3[A_ITEMS], a4[A_ITEMS], a5[A_ITEMS], a6[A_ITEMS], a7[A_ITEMS], a8[A_ITEMS]; uint32_t a2[A_ITEMS];
    uint4 bst[B_ITEMS];
    int brow[B_ITEMS];
#pragma unroll
    for (int i = 0; i < B_ITEMS; ++i) { const int c = tid + i * MMB_NT; brow[i] = xrow(c >> 3); }

    auto load_regs = [&](const int ks) {
#pragma unroll
        for (int i = 0; i < A_ITEMS; ++i) {
            const int row = tid + i * MMB_NT;
            if (row < BM && row < a_rows) {
                if constexpr (WTYPE == 0) { const uint8_t * p = Wbase + (size_t)row * wrow_bytes + (size_t)ks * 36;
                    a0[i] = *(const uint4 *)(p); a1[i] = *(const uint4 *)(p + 16); a2[i] = *(const uint32_t *)(p + 32); }
                else if constexpr (WTYPE == 1) { const uint8_t * p = Wbase + (size_t)row * wrow_bytes + (size_t)ks * 68;
                    a0[i] = *(const uint4 *)(p); a1[i] = *(const uint4 *)(p + 16); a3[i] = *(const uint4 *)(p + 32); a4[i] = *(const uint4 *)(p + 48); a2[i] = *(const uint32_t *)(p + 64); }
                else if constexpr (WTYPE == 3) { const uint8_t * p = Wbase + (size_t)row * wrow_bytes + (size_t)(ks >> 2) * 144;
                    a0[i] = *(const uint4 *)(p); a1[i] = *(const uint4 *)(p + 16 + (size_t)(ks & 3) * 32); a3[i] = *(const uint4 *)(p + 32 + (size_t)(ks & 3) * 32); }
                else if constexpr (WTYPE == 4) { const uint8_t * p = Wbase + (size_t)row * wrow_bytes + (size_t)ks * 48;
                    a0[i] = *(const uint4 *)(p); a1[i] = *(const uint4 *)(p + 16); a3[i] = *(const uint4 *)(p + 32); }
                else if constexpr (WTYPE == 5) { /* IQ3_S: fields non-contiguous, store_lds loads from Wbase */ }
                else if constexpr (WTYPE >= 6) { /* K-quant / i-quant: store_lds loads from Wbase */ }
                else { const uint4 * p = (const uint4 *)(Wbase + (size_t)row * wrow_bytes + (size_t)ks * 128);
                    a0[i] = p[0]; a1[i] = p[1]; a3[i] = p[2]; a4[i] = p[3]; a5[i] = p[4]; a6[i] = p[5]; a7[i] = p[6]; a8[i] = p[7]; }
            } else { a0[i] = make_uint4(0,0,0,0); a1[i] = make_uint4(0,0,0,0); a3[i] = make_uint4(0,0,0,0); a4[i] = make_uint4(0,0,0,0); a5[i] = a6[i] = a7[i] = a8[i] = make_uint4(0,0,0,0); a2[i] = 0; }
        }
#pragma unroll
        for (int i = 0; i < B_ITEMS; ++i) {
            const int c = tid + i * MMB_NT; const int off = (c & 7) * 8;
            bst[i] = (brow[i] >= 0) ? *(const uint4 *)(Xh + (size_t)brow[i] * K + ks * MMB_BK + off) : make_uint4(0,0,0,0);
        }
    };
    auto store_lds_a = [&](int ksh, const int abo) {
#pragma unroll
        for (int i = 0; i < A_ITEMS; ++i) { const int row = SPLIT_A ? (tid % BM) : (tid + i * MMB_NT); if (row < BM) {
            if constexpr (WTYPE == 0) mmb_dq_row36(a0[i], a1[i], a2[i], (uint32_t *)(As + (abo + row) * MMB_LDS_STRIDE));
            else if constexpr (WTYPE == 1) mmb_dq_row68(a0[i], a1[i], a3[i], a4[i], a2[i], (uint32_t *)(As + (abo + row) * MMB_LDS_STRIDE));
            else if constexpr (WTYPE == 3) mmb_dq_row_q4k(a0[i], a1[i], a3[i], ksh, (uint32_t *)(As + (abo + row) * MMB_LDS_STRIDE));
            else if constexpr (WTYPE == 4) mmb_dq_row_q5_1(a0[i], a1[i], a3[i], (uint32_t *)(As + (abo + row) * MMB_LDS_STRIDE));
            else if constexpr (SPLIT_A) {
                constexpr int PARTS = MMB_NT / BM;
                mmb_dq_row_iq3s_p<PARTS>(Wbase + (size_t)row * wrow_bytes + (size_t)(ksh >> 2) * 110, ksh & 3,
                                         (uint32_t *)(As + (abo + row) * MMB_LDS_STRIDE), tid / BM);
            }
            else if constexpr (WTYPE == 5) mmb_dq_row_iq3s(Wbase + (size_t)row * wrow_bytes + (size_t)(ksh >> 2) * 110, ksh & 3, (uint32_t *)(As + (abo + row) * MMB_LDS_STRIDE));
            else if constexpr (WTYPE == 6) mmb_dq_row_q5k(Wbase + (size_t)row * wrow_bytes + (size_t)(ksh >> 2) * 176, ksh & 3, (uint32_t *)(As + (abo + row) * MMB_LDS_STRIDE));
            else if constexpr (WTYPE == 7) mmb_dq_row_q6k(Wbase + (size_t)row * wrow_bytes + (size_t)(ksh >> 2) * 210, ksh & 3, (uint32_t *)(As + (abo + row) * MMB_LDS_STRIDE));
            else if constexpr (WTYPE == 8) mmb_dq_row_iq4xs(Wbase + (size_t)row * wrow_bytes + (size_t)(ksh >> 2) * 136, ksh & 3, (uint32_t *)(As + (abo + row) * MMB_LDS_STRIDE));
            else if constexpr (WTYPE == 9) mmb_dq_row_q3k(Wbase + (size_t)row * wrow_bytes + (size_t)(ksh >> 2) * 110, ksh & 3, (uint32_t *)(As + (abo + row) * MMB_LDS_STRIDE));
            else if constexpr (WTYPE == 10) mmb_dq_row_iq3xxs(Wbase + (size_t)row * wrow_bytes + (size_t)(ksh >> 2) * 98, ksh & 3, (uint32_t *)(As + (abo + row) * MMB_LDS_STRIDE));
            else if constexpr (WTYPE == 11) mmb_dq_row_q4_0(Wbase + (size_t)row * wrow_bytes + (size_t)ksh * 36, (uint32_t *)(As + (abo + row) * MMB_LDS_STRIDE));
            else if constexpr (WTYPE == 12) mmb_dq_row_q4_1(Wbase + (size_t)row * wrow_bytes + (size_t)ksh * 40, (uint32_t *)(As + (abo + row) * MMB_LDS_STRIDE));
            else if constexpr (WTYPE == 13) mmb_dq_row_q5_0(Wbase + (size_t)row * wrow_bytes + (size_t)ksh * 44, (uint32_t *)(As + (abo + row) * MMB_LDS_STRIDE));
            else if constexpr (WTYPE == 14) mmb_dq_row_mxfp4(Wbase + (size_t)row * wrow_bytes + (size_t)ksh * 34, (uint32_t *)(As + (abo + row) * MMB_LDS_STRIDE));
            else if constexpr (WTYPE == 15) mmb_dq_row_nvfp4(Wbase + (size_t)row * wrow_bytes + (size_t)ksh * 36, (uint32_t *)(As + (abo + row) * MMB_LDS_STRIDE));
            else if constexpr (WTYPE == 16) mmb_dq_row_iq2s(Wbase + (size_t)row * wrow_bytes + (size_t)(ksh >> 2) * 82, ksh & 3, (uint32_t *)(As + (abo + row) * MMB_LDS_STRIDE));
            else if constexpr (WTYPE == 17) mmb_dq_row_iq2xs(Wbase + (size_t)row * wrow_bytes + (size_t)(ksh >> 2) * 74, ksh & 3, (uint32_t *)(As + (abo + row) * MMB_LDS_STRIDE));
            else if constexpr (WTYPE == 18) mmb_dq_row_iq2xxs(Wbase + (size_t)row * wrow_bytes + (size_t)(ksh >> 2) * 66, ksh & 3, (uint32_t *)(As + (abo + row) * MMB_LDS_STRIDE));
            else { uint4 * d = (uint4 *)(As + (abo + row) * MMB_LDS_STRIDE); d[0] = a0[i]; d[1] = a1[i]; d[2] = a3[i]; d[3] = a4[i]; d[4] = a5[i]; d[5] = a6[i]; d[6] = a7[i]; d[7] = a8[i]; } } }
    };
    auto store_lds_b = [&](const int bbo) {
#pragma unroll
        for (int i = 0; i < B_ITEMS; ++i) { const int c = tid + i * MMB_NT; *(uint4 *)(Bs + (bbo + (c >> 3)) * MMB_LDS_STRIDE + (c & 7) * 8) = bst[i]; }
    };

    v8f acc[TM][TN];
#pragma unroll
    for (int i = 0; i < TM; ++i)
#pragma unroll
        for (int j = 0; j < TN; ++j)
#pragma unroll
            for (int e = 0; e < 8; ++e) acc[i][j][e] = 0.f;

    bool jact[TN];
#pragma unroll
    for (int j = 0; j < TN; ++j) jact[j] = !TAIL || (wn * WTN + j * 16) < n_cols;   // whole fragments past the valid columns are never stored
    const int nks = K / MMB_BK;
    load_regs(0); store_lds_a(0, 0); store_lds_b(0); __syncthreads();
    for (int ks = 0; ks < nks; ++ks) {
        if (ks + 1 < nks) load_regs(ks + 1);
        // DBUF: the NEXT K-step's weight dequant writes the other A buffer, so it overlaps this
        // step's WMMA instead of serializing behind the A single-buffer barrier hazard (this is the
        // session-21 win; it requires As to be 2x).  DBUF2 additionally double-buffers B to drop the
        // second barrier -- MEASURED REFUTED (GLU 1225 -> 1433 ms): the extra LDS traffic costs more
        // than the removed barrier buys.  Kept guarded so it can be re-tested cheaply.
        if (DBUF && ks + 1 < nks) store_lds_a(ks + 1, ((ks + 1) & 1) * BM);
        if (DBUF2 && ks + 1 < nks) store_lds_b(((ks + 1) & 1) * BN);
        const int ab = DBUF ? ((ks & 1) * BM) : 0;
        const int bb = DBUF2 ? ((ks & 1) * BN) : 0;
#pragma unroll
        for (int kk = 0; kk < MMB_BK; kk += 16) {
            mmb_frag_t a[TM], b[TN]; const int r = lane & 15;
#pragma unroll
            for (int i = 0; i < TM; ++i) { const uint16_t * p = As + (ab + wm * WTM + i * 16 + r) * MMB_LDS_STRIDE + kk;
                a[i] = mmb_ld_frag(p, lane >> 4); }
#pragma unroll
            for (int j = 0; j < TN; ++j) { if constexpr (TAIL) { if (!jact[j]) continue; } const uint16_t * p = Bs + (bb + wn * WTN + j * 16 + r) * MMB_LDS_STRIDE + kk;
                b[j] = mmb_ld_frag(p, lane >> 4); }
#pragma unroll
            for (int i = 0; i < TM; ++i)
#pragma unroll
                for (int j = 0; j < TN; ++j) { if constexpr (TAIL) { if (!jact[j]) continue; } acc[i][j] = mmb_wmma_bf16(b[j], a[i], acc[i][j]); }
        }
        __syncthreads();
        if constexpr (!DBUF2) {
            if (!DBUF && ks + 1 < nks) store_lds_a(ks + 1, 0);
            if (ks + 1 < nks) store_lds_b(0);
            __syncthreads();
        }
    }
    // epilogue through LDS (per-wave 1 KB stage in the now-free A/B tile area); tiles are 16 rows, a_rows is a
    // multiple of 32 in this model, so whole tiles are either valid or beyond a_rows
    float * stg = (float *)((BN * MMB_LDS_STRIDE * 2 >= MMB_NT / 32 * 1024) ? Bs : As) + wave * 256;
#pragma unroll
    for (int i = 0; i < TM; ++i) {
        const int ml = wm * WTM + i * 16; const bool ok = ml < a_rows;
#pragma unroll
        for (int j = 0; j < TN; ++j) {
            if (ok && jact[j]) mmb_store_tile(acc[i][j], stg, D, Dh, store_f32, M, drow, wn * WTN + j * 16, m0 + ml, lane);
            else { __syncthreads(); __syncthreads(); }
        }
    }
}

template <int BM, int BN, int WTM, int WTN, int WTYPE, bool DBUF = false, bool DBUF2 = false>
__global__ void __launch_bounds__(MMB_NT, 2)
mmb_dense_kernel(const uint8_t * __restrict__ W, const uint16_t * __restrict__ Xh, float * __restrict__ D, uint16_t * __restrict__ Dh, const bool store_f32, const int M, const int K, const int T) {
    __shared__ __align__(16) uint16_t As[(DBUF ? 2 : 1) * BM * MMB_LDS_STRIDE];
    __shared__ __align__(16) uint16_t Bs[(DBUF2 ? 2 : 1) * BN * MMB_LDS_STRIDE];
    const int m0 = blockIdx.x * BM, t0 = blockIdx.y * BN;
    const size_t wrow_bytes = WTYPE == 2 ? (size_t) K * 2 :
                              WTYPE == 3 ? (size_t)(K / 256) * 144 :
                              WTYPE == 4 ? (size_t)(K / 32) * 24 :
                              WTYPE == 5 ? (size_t)(K / 256) * 110 :
                              WTYPE == 6 ? (size_t)(K / 256) * 176 :
                              WTYPE == 7 ? (size_t)(K / 256) * 210 :
                              WTYPE == 8 ? (size_t)(K / 256) * 136 :
                              WTYPE == 9 ? (size_t)(K / 256) * 110 :
                              WTYPE == 10 ? (size_t)(K / 256) * 98 :
                              WTYPE == 11 ? (size_t)(K / 32) * 18 :
                              WTYPE == 12 ? (size_t)(K / 32) * 20 :
                              WTYPE == 13 ? (size_t)(K / 32) * 22 :
                              WTYPE == 14 ? (size_t)(K / 32) * 17 :
                              WTYPE == 15 ? (size_t)(K / 64) * 36 :
                              WTYPE == 16 ? (size_t)(K / 256) * 82 :
                              WTYPE == 17 ? (size_t)(K / 256) * 74 :
                              WTYPE == 18 ? (size_t)(K / 256) * 66 :
                              (size_t)(K / 32) * (WTYPE == 0 ? 18 : 34);
    mmb_tile_gemm<BM, BN, WTM, WTN, WTYPE, false, DBUF, DBUF2>(W + (size_t)m0 * wrow_bytes, wrow_bytes, M - m0, Xh, K,
        [&](int i) { return (t0 + i < T) ? t0 + i : -1; }, D, Dh, store_f32, M, [&](int i) { return (t0 + i < T) ? t0 + i : -1; }, m0, T - t0, As, Bs);
}

#if defined(__HIP_PLATFORM_AMD__)
__device__ __forceinline__ float gm_mul_rn(const float a, const float b) { float r; asm("v_mul_f32_e32 %0, %1, %2" : "=v"(r) : "v"(a), "v"(b)); return r; }
__device__ __forceinline__ float gm_add_rn(const float a, const float b) { float r; asm("v_add_f32_e32 %0, %1, %2" : "=v"(r) : "v"(a), "v"(b)); return r; }
#else
__device__ __forceinline__ float gm_mul_rn(const float a, const float b) { return __fmul_rn(a, b); }
__device__ __forceinline__ float gm_add_rn(const float a, const float b) { return __fadd_rn(a, b); }
#endif
__device__ __forceinline__ float gm_sigmoid(const float x) { return 1.0f / (1.0f + expf(-x)); }
__device__ __forceinline__ float gm_bf2f(const uint16_t h) { return __uint_as_float(((uint32_t) h) << 16); }

template <int HC>
__global__ void __launch_bounds__(MMB_NT, 2)
hc_gate_mix_kernel(const uint8_t * __restrict__ W, const uint16_t * __restrict__ Lo, const uint16_t * __restrict__ Xn, float * __restrict__ Out,
        uint16_t * __restrict__ OutH, const bool store_f32,
        const int E, const int K, const int T, const float scale, const float bias) {
    constexpr int CH = 32, BN = 128, BM = HC * CH;
    static_assert(BM <= MMB_NT, "one A row per thread");
    __shared__ __align__(16) uint16_t As[BM * MMB_LDS_STRIDE];
    __shared__ __align__(16) uint16_t Bs[BN * MMB_LDS_STRIDE];
    const int tid = threadIdx.x, lane = tid & 31, wave = tid >> 5;
    const int wm = wave & 1, wn = wave >> 1;                 // wave: 16 channels (all HC streams) x 32 tokens
    const int e0 = blockIdx.x * CH, t0 = blockIdx.y * BN;
    const size_t wrow_bytes = (size_t)(K / 32) * 18;
    constexpr int B_ITEMS = (BN * 8) / MMB_NT;
    uint4 a0 = make_uint4(0,0,0,0), a1 = make_uint4(0,0,0,0); uint32_t a2 = 0; uint4 bst[B_ITEMS]; int brow[B_ITEMS];
    const uint8_t * arow = W;
    if (tid < BM) { const int c = tid / CH, i = tid - c * CH; arow = W + (size_t)(c * E + e0 + i) * wrow_bytes; }
#pragma unroll
    for (int i = 0; i < B_ITEMS; ++i) { const int c = tid + i * MMB_NT; const int t = t0 + (c >> 3); brow[i] = t < T ? t : -1; }
    auto load_regs = [&](const int ks) {
        if (tid < BM) { const uint8_t * p = arow + (size_t)ks * 36; a0 = *(const uint4 *)(p); a1 = *(const uint4 *)(p + 16); a2 = *(const uint32_t *)(p + 32); }
#pragma unroll
        for (int i = 0; i < B_ITEMS; ++i) { const int c = tid + i * MMB_NT; const int off = (c & 7) * 8;
            bst[i] = (brow[i] >= 0) ? *(const uint4 *)(Lo + (size_t)brow[i] * K + ks * MMB_BK + off) : make_uint4(0,0,0,0); }
    };
    auto store_lds = [&]() {
        if (tid < BM) mmb_dq_row36(a0, a1, a2, (uint32_t *)(As + tid * MMB_LDS_STRIDE));
#pragma unroll
        for (int i = 0; i < B_ITEMS; ++i) { const int c = tid + i * MMB_NT; *(uint4 *)(Bs + (c >> 3) * MMB_LDS_STRIDE + (c & 7) * 8) = bst[i]; }
    };
    v8f acc[HC][2];
#pragma unroll
    for (int c = 0; c < HC; ++c)
#pragma unroll
        for (int j = 0; j < 2; ++j)
#pragma unroll
            for (int e = 0; e < 8; ++e) acc[c][j][e] = 0.f;
    const int nks = K / MMB_BK;
    load_regs(0); store_lds(); __syncthreads();
    for (int ks = 0; ks < nks; ++ks) {
        if (ks + 1 < nks) load_regs(ks + 1);
#pragma unroll
        for (int kk = 0; kk < MMB_BK; kk += 16) {
            mmb_frag_t a[HC], b[2]; const int r = lane & 15;
#pragma unroll
            for (int c = 0; c < HC; ++c) { const uint16_t * p = As + (c * CH + wm * 16 + r) * MMB_LDS_STRIDE + kk;
                a[c] = mmb_ld_frag(p, lane >> 4); }
#pragma unroll
            for (int j = 0; j < 2; ++j) { const uint16_t * p = Bs + (wn * 32 + j * 16 + r) * MMB_LDS_STRIDE + kk;
                b[j] = mmb_ld_frag(p, lane >> 4); }
#pragma unroll
            for (int c = 0; c < HC; ++c)
#pragma unroll
                for (int j = 0; j < 2; ++j) acc[c][j] = mmb_wmma_bf16(b[j], a[c], acc[c][j]);
        }
        __syncthreads();
        if (ks + 1 < nks) store_lds();
        __syncthreads();
    }
    // epilogue: lane holds channel (lane & 15) of the wave's 16 and tokens 2e + (lane >> 4) of each 16-token fragment
    const int cm = lane & 15, cn = lane >> 4; const int ch = e0 + wm * 16 + cm;
#pragma unroll
    for (int j = 0; j < 2; ++j) {
#pragma unroll
        for (int e = 0; e < 8; ++e) {
            const int t = t0 + wn * 32 + j * 16 + MMB_ACC_M(e, cn);
            if (t >= T) continue;
            const uint16_t * xr = Xn + (size_t)t * ((size_t)HC * E) + ch;
            float s = 0.f;
#pragma unroll
            for (int c = 0; c < HC; ++c) {
                const float g = __uint_as_float(((uint32_t) mmb_f2bf(acc[c][j][e])) << 16);   // the gate GEMM's BF16 epilogue rounding
                const float term = gm_mul_rn(gm_bf2f(xr[(size_t)c * E]), gm_sigmoid(g));
                s = (c == 0) ? term : gm_add_rn(s, term);
            }
            const float o = scale * s + bias;
            if (store_f32) Out[(size_t)t * E + ch] = o;
            if (OutH) OutH[(size_t)t * E + ch] = mmb_f2bf(o);
        }
    }
}

template <int BM, int BN, int WTM, int WTN, int WTYPE, bool DBUF = false, bool DBUF2 = false>
__global__ void __launch_bounds__(MMB_NT, 2)
mmb_routed_kernel(const uint8_t * __restrict__ W, const size_t expert_bytes, const uint16_t * __restrict__ Xh, float * __restrict__ D,
        uint16_t * __restrict__ Dh, const bool store_f32,
        const int32_t * __restrict__ ids_src, const int32_t * __restrict__ ids_dst, const int32_t * __restrict__ bounds,
        const uint32_t * __restrict__ desc, const int M, const int K) {
    __shared__ __align__(16) uint16_t As[(DBUF ? 2 : 1) * BM * MMB_LDS_STRIDE];
    __shared__ __align__(16) uint16_t Bs[(DBUF2 ? 2 : 1) * BN * MMB_LDS_STRIDE];
    const uint32_t dsc = desc[blockIdx.y];
    if (dsc == UINT32_MAX) return;   // uniform across the block, before any barrier
    const int e = dsc & 0xffff, jt = dsc >> 16;
    const int r0 = bounds[e] + jt * BN, cnt = bounds[e + 1] - r0;
    const int m0 = blockIdx.x * BM;
    const size_t wrow_bytes = WTYPE == 3 ? (size_t)(K / 256) * 144 : WTYPE == 4 ? (size_t)(K / 32) * 24 : WTYPE == 5 ? (size_t)(K / 256) * 110 : WTYPE == 6 ? (size_t)(K / 256) * 176 : WTYPE == 7 ? (size_t)(K / 256) * 210 : WTYPE == 8 ? (size_t)(K / 256) * 136 : WTYPE == 9 ? (size_t)(K / 256) * 110 : WTYPE == 10 ? (size_t)(K / 256) * 98 : WTYPE == 11 ? (size_t)(K / 32) * 18 : WTYPE == 12 ? (size_t)(K / 32) * 20 : WTYPE == 13 ? (size_t)(K / 32) * 22 : WTYPE == 14 ? (size_t)(K / 32) * 17 : WTYPE == 15 ? (size_t)(K / 64) * 36 : WTYPE == 16 ? (size_t)(K / 256) * 82 : WTYPE == 17 ? (size_t)(K / 256) * 74 : WTYPE == 18 ? (size_t)(K / 256) * 66 : WTYPE == 1 ? (size_t)(K / 32) * 34 : (size_t)(K / 32) * 18;
    mmb_tile_gemm<BM, BN, WTM, WTN, WTYPE, true, DBUF, DBUF2>(W + (size_t)e * expert_bytes + (size_t)m0 * wrow_bytes, wrow_bytes, M - m0, Xh, K,
        [&](int i) { return (i < cnt) ? ids_src[r0 + i] : -1; }, D, Dh, store_f32, M, [&](int i) { return (i < cnt) ? ids_dst[r0 + i] : -1; }, m0, cnt, As, Bs);
}

template <int BM, int BN, int WTM, int WTN, int WTYPE, bool TAIL, bool DBUF, bool DBUF2, typename XRowFn, typename DRowFn>
__device__ __forceinline__ void mmb_tile_gemm_glu(const uint8_t * __restrict__ Wg, const uint8_t * __restrict__ Wu, const size_t wrow_bytes, const int a_rows,
        const uint16_t * __restrict__ Xh, const int K, XRowFn xrow, float * __restrict__ D, uint16_t * __restrict__ Dh, const bool store_f32, const int M, DRowFn drow, const int m0,
        const int n_cols, uint16_t * Ag, uint16_t * Au, uint16_t * Bs) {
    constexpr int WAVES_M = BM / WTM, TM = WTM / 16, TN = WTN / 16;
    constexpr int A_ITEMS = (BM + MMB_NT - 1) / MMB_NT;
    // WTYPE 5 (IQ3_S): when BM < MMB_NT the row-per-thread A mapping leaves warps idle, so the
    // threads sharing a row split its 8 (g,il) groups instead (see mmb_dq_row_iq3s_p).
    constexpr bool SPLIT_A = (WTYPE == 5 && MMB_NT > BM && MMB_NT % BM == 0);
    constexpr int PARTS = SPLIT_A ? (MMB_NT / BM) : 1;
    constexpr int B_ITEMS = (BN * 8) / MMB_NT;
    const int tid = threadIdx.x, lane = tid & 31, wave = tid >> 5;
    const int wm = wave % WAVES_M, wn = wave / WAVES_M;
    uint4 g0[A_ITEMS], g1[A_ITEMS], g3[A_ITEMS], g4[A_ITEMS], u0[A_ITEMS], u1[A_ITEMS], u3[A_ITEMS], u4[A_ITEMS]; uint32_t g2[A_ITEMS], u2[A_ITEMS];
    mmb_iq3s_r<PARTS> iqr[2];   // [0] = gate, [1] = up (WTYPE 5 split path only)
    uint4 bst[B_ITEMS];
    int brow[B_ITEMS];
#pragma unroll
    for (int i = 0; i < B_ITEMS; ++i) { const int c = tid + i * MMB_NT; brow[i] = xrow(c >> 3); }
    auto load_regs = [&](const int ks) {
        if constexpr (SPLIT_A) {
            // IQ3_S, split A: preload this thread's block fields for step `ks` into registers, where
            // the loads overlap the previous K-step's WMMA, so store_lds_a becomes pure ALU.  NOTE the
            // row is `tid % BM` (every thread is active) and NOT the A_ITEMS loop's `tid + i*MMB_NT`
            // -- hoisting this out of that loop's `row < BM` guard is load-bearing (put it inside and
            // only threads 0..BM-1 run it, all taking part 0, and the output is garbage).
            const int prow = tid % BM;
            const size_t off = (size_t)prow * wrow_bytes + (size_t)(ks >> 2) * 110;
            iqr[0] = mmb_iq3s_preload<PARTS>(Wg + off, ks & 3, tid / BM);
            iqr[1] = mmb_iq3s_preload<PARTS>(Wu + off, ks & 3, tid / BM);
        }
#pragma unroll
        for (int i = 0; i < A_ITEMS; ++i) {
            const int row = tid + i * MMB_NT;
            if (row < BM && row < a_rows) {
                if constexpr (WTYPE == 0) {
                    const uint8_t * pg = Wg + (size_t)row * wrow_bytes + (size_t)ks * 36;
                    const uint8_t * pu = Wu + (size_t)row * wrow_bytes + (size_t)ks * 36;
                    g0[i] = *(const uint4 *)(pg); g1[i] = *(const uint4 *)(pg + 16); g2[i] = *(const uint32_t *)(pg + 32);
                    u0[i] = *(const uint4 *)(pu); u1[i] = *(const uint4 *)(pu + 16); u2[i] = *(const uint32_t *)(pu + 32);
                } else if constexpr (WTYPE == 1) {
                    const uint8_t * pg = Wg + (size_t)row * wrow_bytes + (size_t)ks * 68;
                    const uint8_t * pu = Wu + (size_t)row * wrow_bytes + (size_t)ks * 68;
                    g0[i] = *(const uint4 *)(pg); g1[i] = *(const uint4 *)(pg + 16); g3[i] = *(const uint4 *)(pg + 32); g4[i] = *(const uint4 *)(pg + 48); g2[i] = *(const uint32_t *)(pg + 64);
                    u0[i] = *(const uint4 *)(pu); u1[i] = *(const uint4 *)(pu + 16); u3[i] = *(const uint4 *)(pu + 32); u4[i] = *(const uint4 *)(pu + 48); u2[i] = *(const uint32_t *)(pu + 64);
                } else if constexpr (WTYPE == 3) {
                    const uint8_t * pg = Wg + (size_t)row * wrow_bytes + (size_t)(ks >> 2) * 144;
                    const uint8_t * pu = Wu + (size_t)row * wrow_bytes + (size_t)(ks >> 2) * 144;
                    const size_t qo = 16 + (size_t)(ks & 3) * 32;
                    g0[i] = *(const uint4 *)(pg); g1[i] = *(const uint4 *)(pg + qo); g3[i] = *(const uint4 *)(pg + qo + 16);
                    u0[i] = *(const uint4 *)(pu); u1[i] = *(const uint4 *)(pu + qo); u3[i] = *(const uint4 *)(pu + qo + 16);
                } else if constexpr (WTYPE == 4) {
                    const uint8_t * pg = Wg + (size_t)row * wrow_bytes + (size_t)ks * 48;
                    const uint8_t * pu = Wu + (size_t)row * wrow_bytes + (size_t)ks * 48;
                    g0[i] = *(const uint4 *)(pg); g1[i] = *(const uint4 *)(pg + 16); g3[i] = *(const uint4 *)(pg + 32);
                    u0[i] = *(const uint4 *)(pu); u1[i] = *(const uint4 *)(pu + 16); u3[i] = *(const uint4 *)(pu + 32);
                } else if constexpr (WTYPE == 5) { /* IQ3_S: fields non-contiguous, store_lds loads from Wg/Wu */ }
                else if constexpr (WTYPE >= 6) { /* K-quant / i-quant: store_lds loads from Wg/Wu */ }
            } else { g0[i] = g1[i] = u0[i] = u1[i] = g3[i] = u3[i] = g4[i] = u4[i] = make_uint4(0,0,0,0); g2[i] = u2[i] = 0; }
        }
#pragma unroll
        for (int i = 0; i < B_ITEMS; ++i) {
            const int c = tid + i * MMB_NT; const int off = (c & 7) * 8;
            bst[i] = (brow[i] >= 0) ? *(const uint4 *)(Xh + (size_t)brow[i] * K + ks * MMB_BK + off) : make_uint4(0,0,0,0);
        }
    };
    auto store_lds_a = [&](int ksh, const int abo) {
#pragma unroll
        for (int i = 0; i < A_ITEMS; ++i) { const int row = SPLIT_A ? (tid % BM) : (tid + i * MMB_NT); if (row < BM) {
            if constexpr (WTYPE == 0) {
                mmb_dq_row36(g0[i], g1[i], g2[i], (uint32_t *)(Ag + (abo + row) * MMB_LDS_STRIDE));
                mmb_dq_row36(u0[i], u1[i], u2[i], (uint32_t *)(Au + (abo + row) * MMB_LDS_STRIDE));
            } else if constexpr (WTYPE == 1) {
                mmb_dq_row68(g0[i], g1[i], g3[i], g4[i], g2[i], (uint32_t *)(Ag + (abo + row) * MMB_LDS_STRIDE));
                mmb_dq_row68(u0[i], u1[i], u3[i], u4[i], u2[i], (uint32_t *)(Au + (abo + row) * MMB_LDS_STRIDE));
            } else if constexpr (WTYPE == 3) {
                mmb_dq_row_q4k(g0[i], g1[i], g3[i], ksh, (uint32_t *)(Ag + (abo + row) * MMB_LDS_STRIDE));
                mmb_dq_row_q4k(u0[i], u1[i], u3[i], ksh, (uint32_t *)(Au + (abo + row) * MMB_LDS_STRIDE));
            } else if constexpr (WTYPE == 4) {
                mmb_dq_row_q5_1(g0[i], g1[i], g3[i], (uint32_t *)(Ag + (abo + row) * MMB_LDS_STRIDE));
                mmb_dq_row_q5_1(u0[i], u1[i], u3[i], (uint32_t *)(Au + (abo + row) * MMB_LDS_STRIDE));
            } else if constexpr (SPLIT_A) {
                mmb_dq_iq3s_r<PARTS>(iqr[0], (uint32_t *)(Ag + (abo + row) * MMB_LDS_STRIDE), tid / BM);
                mmb_dq_iq3s_r<PARTS>(iqr[1], (uint32_t *)(Au + (abo + row) * MMB_LDS_STRIDE), tid / BM);
            } else if constexpr (WTYPE == 5) {
                mmb_dq_row_iq3s(Wg + (size_t)row * wrow_bytes + (size_t)(ksh >> 2) * 110, ksh & 3, (uint32_t *)(Ag + (abo + row) * MMB_LDS_STRIDE));
                mmb_dq_row_iq3s(Wu + (size_t)row * wrow_bytes + (size_t)(ksh >> 2) * 110, ksh & 3, (uint32_t *)(Au + (abo + row) * MMB_LDS_STRIDE));
            } else if constexpr (WTYPE == 6) {
                mmb_dq_row_q5k(Wg + (size_t)row * wrow_bytes + (size_t)(ksh >> 2) * 176, ksh & 3, (uint32_t *)(Ag + (abo + row) * MMB_LDS_STRIDE));
                mmb_dq_row_q5k(Wu + (size_t)row * wrow_bytes + (size_t)(ksh >> 2) * 176, ksh & 3, (uint32_t *)(Au + (abo + row) * MMB_LDS_STRIDE));
            } else if constexpr (WTYPE == 7) {
                mmb_dq_row_q6k(Wg + (size_t)row * wrow_bytes + (size_t)(ksh >> 2) * 210, ksh & 3, (uint32_t *)(Ag + (abo + row) * MMB_LDS_STRIDE));
                mmb_dq_row_q6k(Wu + (size_t)row * wrow_bytes + (size_t)(ksh >> 2) * 210, ksh & 3, (uint32_t *)(Au + (abo + row) * MMB_LDS_STRIDE));
            } else if constexpr (WTYPE == 8) {
                mmb_dq_row_iq4xs(Wg + (size_t)row * wrow_bytes + (size_t)(ksh >> 2) * 136, ksh & 3, (uint32_t *)(Ag + (abo + row) * MMB_LDS_STRIDE));
                mmb_dq_row_iq4xs(Wu + (size_t)row * wrow_bytes + (size_t)(ksh >> 2) * 136, ksh & 3, (uint32_t *)(Au + (abo + row) * MMB_LDS_STRIDE));
            } else if constexpr (WTYPE == 9) {
                mmb_dq_row_q3k(Wg + (size_t)row * wrow_bytes + (size_t)(ksh >> 2) * 110, ksh & 3, (uint32_t *)(Ag + (abo + row) * MMB_LDS_STRIDE));
                mmb_dq_row_q3k(Wu + (size_t)row * wrow_bytes + (size_t)(ksh >> 2) * 110, ksh & 3, (uint32_t *)(Au + (abo + row) * MMB_LDS_STRIDE));
            } else if constexpr (WTYPE == 10) {
                mmb_dq_row_iq3xxs(Wg + (size_t)row * wrow_bytes + (size_t)(ksh >> 2) * 98, ksh & 3, (uint32_t *)(Ag + (abo + row) * MMB_LDS_STRIDE));
                mmb_dq_row_iq3xxs(Wu + (size_t)row * wrow_bytes + (size_t)(ksh >> 2) * 98, ksh & 3, (uint32_t *)(Au + (abo + row) * MMB_LDS_STRIDE));
            } else if constexpr (WTYPE == 11) {
                mmb_dq_row_q4_0(Wg + (size_t)row * wrow_bytes + (size_t)ksh * 36, (uint32_t *)(Ag + (abo + row) * MMB_LDS_STRIDE));
                mmb_dq_row_q4_0(Wu + (size_t)row * wrow_bytes + (size_t)ksh * 36, (uint32_t *)(Au + (abo + row) * MMB_LDS_STRIDE));
            } else if constexpr (WTYPE == 12) {
                mmb_dq_row_q4_1(Wg + (size_t)row * wrow_bytes + (size_t)ksh * 40, (uint32_t *)(Ag + (abo + row) * MMB_LDS_STRIDE));
                mmb_dq_row_q4_1(Wu + (size_t)row * wrow_bytes + (size_t)ksh * 40, (uint32_t *)(Au + (abo + row) * MMB_LDS_STRIDE));
            } else if constexpr (WTYPE == 13) {
                mmb_dq_row_q5_0(Wg + (size_t)row * wrow_bytes + (size_t)ksh * 44, (uint32_t *)(Ag + (abo + row) * MMB_LDS_STRIDE));
                mmb_dq_row_q5_0(Wu + (size_t)row * wrow_bytes + (size_t)ksh * 44, (uint32_t *)(Au + (abo + row) * MMB_LDS_STRIDE));
            } else if constexpr (WTYPE == 14) {
                mmb_dq_row_mxfp4(Wg + (size_t)row * wrow_bytes + (size_t)ksh * 34, (uint32_t *)(Ag + (abo + row) * MMB_LDS_STRIDE));
                mmb_dq_row_mxfp4(Wu + (size_t)row * wrow_bytes + (size_t)ksh * 34, (uint32_t *)(Au + (abo + row) * MMB_LDS_STRIDE));
            } else if constexpr (WTYPE == 15) {
                mmb_dq_row_nvfp4(Wg + (size_t)row * wrow_bytes + (size_t)ksh * 36, (uint32_t *)(Ag + (abo + row) * MMB_LDS_STRIDE));
                mmb_dq_row_nvfp4(Wu + (size_t)row * wrow_bytes + (size_t)ksh * 36, (uint32_t *)(Au + (abo + row) * MMB_LDS_STRIDE));
            } else if constexpr (WTYPE == 16) {
                mmb_dq_row_iq2s(Wg + (size_t)row * wrow_bytes + (size_t)(ksh >> 2) * 82, ksh & 3, (uint32_t *)(Ag + (abo + row) * MMB_LDS_STRIDE));
                mmb_dq_row_iq2s(Wu + (size_t)row * wrow_bytes + (size_t)(ksh >> 2) * 82, ksh & 3, (uint32_t *)(Au + (abo + row) * MMB_LDS_STRIDE));
            } else if constexpr (WTYPE == 17) {
                mmb_dq_row_iq2xs(Wg + (size_t)row * wrow_bytes + (size_t)(ksh >> 2) * 74, ksh & 3, (uint32_t *)(Ag + (abo + row) * MMB_LDS_STRIDE));
                mmb_dq_row_iq2xs(Wu + (size_t)row * wrow_bytes + (size_t)(ksh >> 2) * 74, ksh & 3, (uint32_t *)(Au + (abo + row) * MMB_LDS_STRIDE));
            } else if constexpr (WTYPE == 18) {
                mmb_dq_row_iq2xxs(Wg + (size_t)row * wrow_bytes + (size_t)(ksh >> 2) * 66, ksh & 3, (uint32_t *)(Ag + (abo + row) * MMB_LDS_STRIDE));
                mmb_dq_row_iq2xxs(Wu + (size_t)row * wrow_bytes + (size_t)(ksh >> 2) * 66, ksh & 3, (uint32_t *)(Au + (abo + row) * MMB_LDS_STRIDE));
            } } }
    };
    auto store_lds_b = [&](const int bbo) {
#pragma unroll
        for (int i = 0; i < B_ITEMS; ++i) { const int c = tid + i * MMB_NT; *(uint4 *)(Bs + (bbo + (c >> 3)) * MMB_LDS_STRIDE + (c & 7) * 8) = bst[i]; }
    };
    v8f accg[TM][TN], accu[TM][TN];
#pragma unroll
    for (int i = 0; i < TM; ++i)
#pragma unroll
        for (int j = 0; j < TN; ++j)
#pragma unroll
            for (int e = 0; e < 8; ++e) { accg[i][j][e] = 0.f; accu[i][j][e] = 0.f; }
    bool jact[TN];
#pragma unroll
    for (int j = 0; j < TN; ++j) jact[j] = !TAIL || (wn * WTN + j * 16) < n_cols;
    const int nks = K / MMB_BK;
    load_regs(0); store_lds_a(0, 0); store_lds_b(0); __syncthreads();
    for (int ks = 0; ks < nks; ++ks) {
        if (ks + 1 < nks) load_regs(ks + 1);
        // DBUF: the NEXT K-step's gate+up dequant writes the other A buffers, overlapping this step's
        // WMMA.  Only enabled for WTYPE 5 (IQ3_S): it is a -6.7 % win on the Flash-Next IQ3_S GLU but
        // a +5.8 % loss on the 35B Q4_K GLU.  DBUF2 (double-buffer B, one sync) is MEASURED REFUTED.
        if (DBUF && ks + 1 < nks) store_lds_a(ks + 1, ((ks + 1) & 1) * BM);
        if (DBUF2 && ks + 1 < nks) store_lds_b(((ks + 1) & 1) * BN);
        const int ab = DBUF ? ((ks & 1) * BM) : 0;
        const int bb = DBUF2 ? ((ks & 1) * BN) : 0;
#pragma unroll
        for (int kk = 0; kk < MMB_BK; kk += 16) {
            mmb_frag_t ag[TM], au[TM], b[TN]; const int r = lane & 15;
#pragma unroll
            for (int i = 0; i < TM; ++i) { const int off = (ab + wm * WTM + i * 16 + r) * MMB_LDS_STRIDE + kk;
                ag[i] = mmb_ld_frag(Ag + off, lane >> 4);
                au[i] = mmb_ld_frag(Au + off, lane >> 4); }
#pragma unroll
            for (int j = 0; j < TN; ++j) { if constexpr (TAIL) { if (!jact[j]) continue; } const uint16_t * p = Bs + (bb + wn * WTN + j * 16 + r) * MMB_LDS_STRIDE + kk;
                b[j] = mmb_ld_frag(p, lane >> 4); }
#pragma unroll
            for (int i = 0; i < TM; ++i)
#pragma unroll
                for (int j = 0; j < TN; ++j) {
                    if constexpr (TAIL) { if (!jact[j]) continue; }
                    accg[i][j] = mmb_wmma_bf16(b[j], ag[i], accg[i][j]);
                    accu[i][j] = mmb_wmma_bf16(b[j], au[i], accu[i][j]);
                }
        }
        __syncthreads();
        if constexpr (!DBUF2) {
            if (!DBUF && ks + 1 < nks) store_lds_a(ks + 1, 0);
            if (ks + 1 < nks) store_lds_b(0);
            __syncthreads();
        }
    }
    float * stg = (float *)((BN * MMB_LDS_STRIDE * 2 >= MMB_NT / 32 * 1024) ? Bs : Ag) + wave * 256;
#pragma unroll
    for (int i = 0; i < TM; ++i) {
        const int ml = wm * WTM + i * 16; const bool ok = ml < a_rows;
#pragma unroll
        for (int j = 0; j < TN; ++j) {
            if (ok && jact[j]) {
                v8f v;
#pragma unroll
                for (int e = 0; e < 8; ++e) { v[e] = ggml_cuda_op_silu_single(accg[i][j][e]) * accu[i][j][e]; }
                mmb_store_tile(v, stg, D, Dh, store_f32, M, drow, wn * WTN + j * 16, m0 + ml, lane);
            } else { __syncthreads(); __syncthreads(); }
        }
    }
}

template <int BM, int BN, int WTM, int WTN, int WTYPE, bool DBUF = false, bool DBUF2 = false>
__global__ void __launch_bounds__(MMB_NT, 2)
mmb_routed_glu_kernel(const uint8_t * __restrict__ Wg, const uint8_t * __restrict__ Wu, const size_t expert_bytes, const uint16_t * __restrict__ Xh,
        float * __restrict__ D, uint16_t * __restrict__ Dh, const bool store_f32,
        const int32_t * __restrict__ ids_src, const int32_t * __restrict__ ids_dst, const int32_t * __restrict__ bounds,
        const uint32_t * __restrict__ desc, const int M, const int K) {
    __shared__ __align__(16) uint16_t Ag[(DBUF ? 2 : 1) * BM * MMB_LDS_STRIDE];
    __shared__ __align__(16) uint16_t Au[(DBUF ? 2 : 1) * BM * MMB_LDS_STRIDE];
    __shared__ __align__(16) uint16_t Bs[(DBUF2 ? 2 : 1) * BN * MMB_LDS_STRIDE];
    const uint32_t dsc = desc[blockIdx.y];
    if (dsc == UINT32_MAX) return;
    const int e = dsc & 0xffff, jt = dsc >> 16;
    const int r0 = bounds[e] + jt * BN, cnt = bounds[e + 1] - r0;
    const int m0 = blockIdx.x * BM;
    const size_t wrow_bytes = WTYPE == 3 ? (size_t)(K / 256) * 144 : WTYPE == 4 ? (size_t)(K / 32) * 24 : WTYPE == 5 ? (size_t)(K / 256) * 110 : WTYPE == 6 ? (size_t)(K / 256) * 176 : WTYPE == 7 ? (size_t)(K / 256) * 210 : WTYPE == 8 ? (size_t)(K / 256) * 136 : WTYPE == 9 ? (size_t)(K / 256) * 110 : WTYPE == 10 ? (size_t)(K / 256) * 98 : WTYPE == 11 ? (size_t)(K / 32) * 18 : WTYPE == 12 ? (size_t)(K / 32) * 20 : WTYPE == 13 ? (size_t)(K / 32) * 22 : WTYPE == 14 ? (size_t)(K / 32) * 17 : WTYPE == 15 ? (size_t)(K / 64) * 36 : WTYPE == 16 ? (size_t)(K / 256) * 82 : WTYPE == 17 ? (size_t)(K / 256) * 74 : WTYPE == 18 ? (size_t)(K / 256) * 66 : WTYPE == 1 ? (size_t)(K / 32) * 34 : (size_t)(K / 32) * 18;
    mmb_tile_gemm_glu<BM, BN, WTM, WTN, WTYPE, true, DBUF, DBUF2>(Wg + (size_t)e * expert_bytes + (size_t)m0 * wrow_bytes, Wu + (size_t)e * expert_bytes + (size_t)m0 * wrow_bytes, wrow_bytes, M - m0, Xh, K,
        [&](int i) { return (i < cnt) ? ids_src[r0 + i] : -1; }, D, Dh, store_f32, M, [&](int i) { return (i < cnt) ? ids_dst[r0 + i] : -1; }, m0, cnt, Ag, Au, Bs);
}

// F32 x F32 -> F32 GEMM with F32-equivalent precision on WMMA: each operand is split into F16 hi + F16 lo at tile load and
// the product is accumulated as hi*hi + hi*lo + lo*hi in F32 (lo*lo ~2^-22 relative, dropped). Used for the F32 MoE router.
__device__ __forceinline__ void mmb_split2(float x, uint16_t & hi, uint16_t & lo) {
    hi = __builtin_bit_cast(uint16_t, (_Float16) x); lo = __builtin_bit_cast(uint16_t, (_Float16) (x - (float) __builtin_bit_cast(_Float16, hi)));
}

// ---------------------------------------------------------------------------
// tiny-M F32 (the hc *_inject pair: M = hc = 4, K = 10240, T = 2048).
//
// Neither existing tile can help here.  M=4 offers no M parallelism, so rocBLAS launches
// ceil(T/32)=64 blocks and the 128-row MMB tile pads the A panel 32x -- and BOTH read the 84 MB
// activation exactly once (the memory floor is ~0.33 ms) yet measure 1.332 / 1.825 ms, i.e. ~63
// GB/s.  The same part sustains ~330 GB/s on rms_norm_f32 (5.9 % of prefill, 1044 ms for
// 84 MB read + 84 MB write per launch), so this is a *parallelism* wall, not a bandwidth or
// arithmetic one: 16-64 blocks cannot keep enough loads in flight.
//
// The fix is to make the threads the parallelism.  One warp per token; every lane accumulates a
// k-strided partial *for all M rows*, so X is read once, coalesced (consecutive lanes read
// consecutive float4), and reused across M in registers.  grid = ceil(T / (MMB_NT/32)) = 256
// blocks at T=2048, a 4-16x increase.  W is [M][K] and tiny (164 KB), so its panel reads are L2.
//
// Not bit-identical to rocBLAS (different summation order, and plain fp32 FMA rather than the
// f16 hi/lo split) -- a prefill numerics change like the rest of the F32 work, so it is PPL-gated.
// TT tokens per warp, default **1** (measured best).  The motivation for TT>1 was W traffic: each
// lane walks a k-strided slice, so a warp collectively covers all of W (164 KB), and at 2048 warps
// that is 336 MB from L2 against X's 84 MB from DRAM.  Amortising it does not pay: TT=2 and TT=4
// measured *worse* (pp8192 954.8 / 952.3 vs 957.4 at TT=1), so L2 serves the W panels well enough
// that the extra live registers and reduced block count cost more than the traffic saved.  Kept as
// a knob because the balance is a property of this cache, not of the algorithm.
template<int MMAX, int TT, bool XBF16 = false>
__global__ void __launch_bounds__(MMB_NT)
mmb_tiny_m_f32_kernel(const float * __restrict__ W, const float * __restrict__ X,
                      float * __restrict__ D, const int M, const int K, const int T) {
    constexpr int NWARPS = MMB_NT / 32;
    const int lane = threadIdx.x & 31, w = threadIdx.x >> 5;
    const int t0 = (blockIdx.x * NWARPS + w) * TT;
    const int nk4 = K >> 2;   // caller guarantees K % 4 == 0
    float4 acc[TT][MMAX];
#pragma unroll
    for (int i = 0; i < TT; ++i)
#pragma unroll
        for (int m = 0; m < MMAX; ++m) acc[i][m] = make_float4(0.f, 0.f, 0.f, 0.f);
    const float4   * xrow[TT];
    const uint16_t * xhrow[TT];
#pragma unroll
    for (int i = 0; i < TT; ++i) {
        const int t = t0 + i;
        // out-of-range tokens get a valid dummy row; their results are dropped on the way out
        const int tr = t < T ? t : 0;
        xrow[i]  = (const float4 *) (X + (size_t) tr * K);
        xhrow[i] = (const uint16_t *) X + (size_t) tr * K;
    }
    for (int k4 = lane; k4 < nk4; k4 += 32) {
        const int k = k4 << 2;
        float4 xv[TT], wv[MMAX];
#pragma unroll
        for (int i = 0; i < TT; ++i) {
            if constexpr (XBF16) {
                // HC16: the producer marked this activation BF16-only, so X is a bf16 buffer (the
                // F32 tensor was never written).  RNE-rounded on the way in, so the value matches
                // exactly what the old F32->bf16 conversion produced (a numerics change vs rocBLAS
                // only in that the GEMM now consumes the same rounded activation as the bf16 path).
                const uint2 p = *(const uint2 *) (xhrow[i] + k);
                xv[i] = make_float4(gm_bf2f((uint16_t) p.x), gm_bf2f((uint16_t) (p.x >> 16)),
                                    gm_bf2f((uint16_t) p.y), gm_bf2f((uint16_t) (p.y >> 16)));
            } else {
                xv[i] = xrow[i][k4];
            }
        }
#pragma unroll
        for (int m = 0; m < MMAX; ++m) wv[m] = (m < M) ? *(const float4 *) (W + (size_t) m * K + k) : make_float4(0.f, 0.f, 0.f, 0.f);
#pragma unroll
        for (int i = 0; i < TT; ++i)
#pragma unroll
            for (int m = 0; m < MMAX; ++m) {
                acc[i][m].x += wv[m].x * xv[i].x; acc[i][m].y += wv[m].y * xv[i].y;
                acc[i][m].z += wv[m].z * xv[i].z; acc[i][m].w += wv[m].w * xv[i].w;
            }
    }
    // lane partial -> warp total, one shuffle tree reused for every (token, row)
#pragma unroll
    for (int i = 0; i < TT; ++i) {
        const int t = t0 + i;
        if (t >= T) continue;
#pragma unroll
        for (int m = 0; m < MMAX; ++m) {
            if (m >= M) break;
            float s = (acc[i][m].x + acc[i][m].y) + (acc[i][m].z + acc[i][m].w);
#pragma unroll
            for (int off = 16; off > 0; off >>= 1) s += __shfl_down(s, off);
            if (lane == 0) D[(size_t) t * M + m] = s;
        }
    }
}
template <int BM, int BN, int WTM, int WTN, bool TWO>
__global__ void __launch_bounds__(MMB_NT, 2)
mmb_f32split_kernel(const float * __restrict__ W, const float * __restrict__ X, float * __restrict__ D, const int M, const int K, const int T) {
    constexpr int BKs = 32, LS = BKs + 8, WAVES_M = BM / WTM, TM = WTM / 16, TN = WTN / 16;
    __shared__ __align__(16) uint16_t Ah[BM * LS], Al[BM * LS], Bh[BN * LS], Bl[BN * LS];
    const int tid = threadIdx.x, lane = tid & 31, wave = tid >> 5, wm = wave % WAVES_M, wn = wave / WAVES_M;
    const int m0 = blockIdx.x * BM, t0 = blockIdx.y * BN;
    v8f acc[TM][TN];
#pragma unroll
    for (int i = 0; i < TM; ++i)
#pragma unroll
        for (int j = 0; j < TN; ++j)
#pragma unroll
            for (int e = 0; e < 8; ++e) acc[i][j][e] = 0.f;
    constexpr int A_CH = BM * BKs / 4, B_CH = BN * BKs / 4;
    for (int k0 = 0; k0 < K; k0 += BKs) {
        for (int idx = tid; idx < A_CH; idx += MMB_NT) { const int row = idx >> 3, c4 = (idx & 7) * 4; const int m = m0 + row;
            float4 v = make_float4(0.f,0.f,0.f,0.f); if (m < M) v = *(const float4 *)(W + (size_t) m * K + k0 + c4);
            uint16_t h[4], l[4]; mmb_split2(v.x,h[0],l[0]); mmb_split2(v.y,h[1],l[1]); mmb_split2(v.z,h[2],l[2]); mmb_split2(v.w,h[3],l[3]);
            *(uint2 *)(Ah + row * LS + c4) = make_uint2((uint32_t)h[0] | ((uint32_t)h[1] << 16), (uint32_t)h[2] | ((uint32_t)h[3] << 16));
            *(uint2 *)(Al + row * LS + c4) = make_uint2((uint32_t)l[0] | ((uint32_t)l[1] << 16), (uint32_t)l[2] | ((uint32_t)l[3] << 16)); }
        for (int idx = tid; idx < B_CH; idx += MMB_NT) { const int row = idx >> 3, c4 = (idx & 7) * 4; const int t = t0 + row;
            float4 v = make_float4(0.f,0.f,0.f,0.f); if (t < T) v = *(const float4 *)(X + (size_t) t * K + k0 + c4);
            uint16_t h[4], l[4]; mmb_split2(v.x,h[0],l[0]); mmb_split2(v.y,h[1],l[1]); mmb_split2(v.z,h[2],l[2]); mmb_split2(v.w,h[3],l[3]);
            *(uint2 *)(Bh + row * LS + c4) = make_uint2((uint32_t)h[0] | ((uint32_t)h[1] << 16), (uint32_t)h[2] | ((uint32_t)h[3] << 16));
            *(uint2 *)(Bl + row * LS + c4) = make_uint2((uint32_t)l[0] | ((uint32_t)l[1] << 16), (uint32_t)l[2] | ((uint32_t)l[3] << 16)); }
        __syncthreads();
        const int r = lane & 15;
#pragma unroll
        for (int kk = 0; kk < BKs; kk += 16) {
            mmb_frag_t ah[TM], al[TM], bh[TN], bl[TN];
#pragma unroll
            for (int i = 0; i < TM; ++i) { const int off = (wm * WTM + i * 16 + r) * LS + kk;
                ah[i] = mmb_ld_frag(Ah + off, lane >> 4);
                al[i] = mmb_ld_frag(Al + off, lane >> 4); }
#pragma unroll
            for (int j = 0; j < TN; ++j) { const int off = (wn * WTN + j * 16 + r) * LS + kk;
                bh[j] = mmb_ld_frag(Bh + off, lane >> 4);
                bl[j] = mmb_ld_frag(Bl + off, lane >> 4); }
#pragma unroll
            for (int i = 0; i < TM; ++i)
#pragma unroll
                for (int j = 0; j < TN; ++j) {
                    acc[i][j] = mmb_wmma_f16(bh[j], ah[i], acc[i][j]);
                    acc[i][j] = mmb_wmma_f16(bl[j], ah[i], acc[i][j]);
                    if constexpr (!TWO) acc[i][j] = mmb_wmma_f16(bh[j], al[i], acc[i][j]);
                }
        }
        __syncthreads();
    }
    const int cm = lane & 15, cn = lane >> 4;
#pragma unroll
    for (int i = 0; i < TM; ++i) { const int m = m0 + wm * WTM + i * 16 + cm; if (m >= M) continue;
#pragma unroll
        for (int j = 0; j < TN; ++j)
#pragma unroll
            for (int e = 0; e < 8; ++e) { const int t = t0 + wn * WTN + j * 16 + MMB_ACC_M(e, cn); if (t < T) D[(size_t) t * M + m] = acc[i][j][e]; } }
}

// two tile classes: experts with >= thresh rows get BN_BIG-row tiles, the rest BN_SMALL-row tiles (fewer wasted rows on tiny experts)
__global__ void mmb_build_desc2(const int32_t * __restrict__ bounds, uint32_t * __restrict__ desc_big, uint32_t * __restrict__ desc_small,
        const int E, const int nbig_max, const int nsmall_max, const int BN_BIG, const int BN_SMALL, const int thresh) {
    __shared__ int sb[1024], ss[1024];
    const int e = threadIdx.x;
    for (int i = e; i < nbig_max;   i += blockDim.x) desc_big[i]   = UINT32_MAX;
    for (int i = e; i < nsmall_max; i += blockDim.x) desc_small[i] = UINT32_MAX;
    int cnt = (e < E) ? bounds[e + 1] - bounds[e] : 0;
    const bool big = cnt >= thresh;
    const int tb = big ? (cnt + BN_BIG - 1) / BN_BIG : 0;
    const int ts = big ? 0 : (cnt + BN_SMALL - 1) / BN_SMALL;
    sb[e] = tb; ss[e] = ts;
    __syncthreads();
    for (int off = 1; off < 1024; off <<= 1) {
        const int vb = (e >= off) ? sb[e - off] : 0, vs = (e >= off) ? ss[e - off] : 0;
        __syncthreads();
        sb[e] += vb; ss[e] += vs;
        __syncthreads();
    }
    const int bb = sb[e] - tb, bs = ss[e] - ts;
    for (int jt = 0; jt < tb; ++jt) { const int idx = bb + jt; if (idx < nbig_max)   desc_big[idx]   = (uint32_t)e | ((uint32_t)jt << 16); }
    for (int jt = 0; jt < ts; ++jt) { const int idx = bs + jt; if (idx < nsmall_max) desc_small[idx] = (uint32_t)e | ((uint32_t)jt << 16); }
}

struct mmb_cache_entry { const ggml_tensor * root; const void * data; size_t n; ggml_cuda_pool_alloc<uint16_t> * buf; };
// Pinned producer slots (pinned until the next producer of the same slot): 0 = generic MMB-GEMM
// activation copies, 1 = HC gate, 2 = GLU, 3 = routed MoE output, 4 = HC normalized stream xn.
// Slot 4 exists because xn is the one activation with *delayed* consumers (dsv4_hc_pre src[0] and
// the tiny-M inject GEMMs), so its copy cannot share the aggressively-reused generic slot 0 -- and
// dsv4_hc_pre itself reads x from the slot while writing its own output to slot 0.
static constexpr int MMB_SLOT_COUNT = 5;
// All state the MMB BF16-activation machinery needs for one backend context: the per-graph activation
// cache, the pinned producer slots, the BF16-only/copy marks and their lifetime.  It is per context
// because two llama_contexts (the MTP target and its draft head, say) interleave their
// optimise/compute calls; sharing the cache or the marks across them let one context free the
// other's activation buffers mid-compute or see its marks.  The op implementations run synchronously
// inside a backend compute, so a single "active context" pointer set by
// ggml_backend_cuda_graph_optimize / _graph_compute selects the right state without threading ctx
// through every call site.
struct mmb_ctx_state {
    std::vector<mmb_cache_entry> cache;
    mmb_cache_entry slots[MMB_SLOT_COUNT] = {};
    size_t slot_cap[MMB_SLOT_COUNT] = {0, 0, 0, 0, 0};
    std::unordered_set<const ggml_tensor *> bf16_only;
    // "also emit a BF16 copy" mark: the F32 output stays valid; producers that can emit a BF16 side
    // copy write it into slot 0 and the MMB activation conversion finds and skips it.
    std::unordered_set<const ggml_tensor *> bf16_copy;
    // BF16-only tensors that must not use the generic slot 0 (their copy has to outlive other producers).
    std::unordered_map<const ggml_tensor *, int> bf16_slot;
    // Mark lifetime: set after every backend compute, so the next optimize pass for this context
    // clears and rebuilds the marks.  `first_split` is the first node of the graph whose marks are
    // current, so re-optimizing the same graph (without an intervening compute, e.g. reserve then
    // alloc) clears as well.
    bool after_compute = true;
    const void * first_split = nullptr;
};
static std::unordered_map<const ggml_backend_cuda_context *, mmb_ctx_state> g_mmb_state;
static const ggml_backend_cuda_context * g_mmb_active_ctx = nullptr;
static mmb_ctx_state & mmb_state() {
    static mmb_ctx_state empty;
    if (g_mmb_active_ctx == nullptr) { return empty; }
    return g_mmb_state[g_mmb_active_ctx];
}

// ---------------------------------------------------------------------------------------------
// Per-arch tuning defaults (S11 of wip/mmb-general/gfx1201-porting.md, §7 point 3).
//
// Every `mmb_*` tunable is now `env override || arch default`, and the arch default is selected from
// the device cc -- so gfx1151 (RDNA3_5), gfx1201 (RDNA4) and gfx1100 (RDNA3_0) can hold *different*
// values without the user setting anything.  The previous state was env-only with gfx1151 values, so
// no arch could differ without a wall of env vars -- which also made profiler runs hard to trust
// (the porting plan §12.5 env-flakiness note).
//
// The RDNA4 row carries only values that have been **measured** on gfx1201 (records under
// wip/mmb-general/); every other field keeps the gfx1151 value, which is exactly the pre-S11
// behaviour.  There is no invented per-arch tuning here -- this table is what makes the per-arch
// tuning *possible* (and visible: `GGML_CUDA_MMB_CFG=1` dumps the resolved config once).
//
// The two weight-type policies (`mmb_wtype_mask`, `mmb_dense_tmask`) and the master gate
// (`mmb_enabled`) already select on the cc and are left where they are; they are documented in the
// same dump.
enum mmb_geom_id { MMB_GEOM_GFX11_SPLIT = 0, MMB_GEOM_R4_256x128 = 1 };

struct mmb_arch_cfg {
    int  min_t          = 512;   // prefill-only: n_tokens >= this may use mmb
    int  glu_thresh     = 32;    // expert rows >= this take the BN=128 routed-GLU tile
    int  routed_thresh  = 32;    // expert rows >= this take the BN=128 routed tile
    int  tall_mode      = 2;     // 0 off, 1 narrow tall-M tile, 2 wide (iq4_nl HC down/inject)
    int  tiny_m         = 1;     // warp-per-token F32 kernel for M <= 8 (the hc *_inject pair)
    int  tiny_tt        = 1;     // tokens per warp in that kernel
    int  f32split_mode  = 1;     // 0 off, 1 shape-aware, 2 every F32 GEMM through mmb
    int  f32split_min_k = 0;
    int  f32split_min_m = 128;   // the MoE router (M=512) wins on mmb; the M=4 inject pair does not
    int  cache_max      = 4;     // activation-conversion cache entries
    int  shadow_mode    = 0;     // bf16 weight shadow
    int  shadow_cap_mb  = 6144;
    int  iq3xxs_glu     = 0;     // fused IQ3_XXS routed GLU (measured a net loss on gfx1151)
    int  hc16           = 1, down16 = 0, gatemix = 1, blk16 = 0, res16 = 0;   // hc16 + gatemix default ON (policy 2026-09-21; gatemix is RDNA3_5-only at its call site)
    int  glu            = 1;     // fused routed gate+up+GLU
    int  bf16w          = 1;     // BF16 dense weights
    int  routed         = 1;     // the routed MoE (MUL_MAT_ID) path + its fused GLU
    int  dense_geom     = MMB_GEOM_GFX11_SPLIT;  // see mmb_geom_id
};

static mmb_arch_cfg mmb_arch_defaults(const int cc) {
    mmb_arch_cfg c;   // the gfx1151 / pre-S11 values for every field
    if (GGML_CUDA_CC_IS_RDNA4(cc)) {
        // S10: the gfx1151-tuned 128x256/128x128 dense split is LDS-bound (55296 B -> 1 workgroup
        // per CU) and leaves half of the 256 threads idle in the A dequant; a single 256x128 tile
        // (WTM=64, WTN=64, TM=TN=4, the same 55296 B) makes the IQ3_S dense GEMM beat the delivery
        // MMQ by 4.5 %.  See wip/mmb-general/gfx1201-s10-dense-geometry.md.
        c.dense_geom = MMB_GEOM_R4_256x128;
        // S12: the routed MoE path LOSES on RDNA4 on every model measured, because the delivery's
        // block-13 `mul_mat_q_routed_compact` is already a good fused expert kernel and standing it
        // down costs an extra `mm_ids_helper` launch.  Interleaved r=3, two rounds (pp8192 /
        // pp32768), routed ON -> OFF:
        //   35B-A3B UD-Q3_K_M (qwen35moe)   -1.44 / -1.39 %  ->  -0.05 / -0.09 %
        //   Flash-Next IQ4_XS (qwen4exp)    +2.3  / +1.8  %  ->  +6.6  / +5.4  %
        // i.e. the routed path was *masking* most of the qwen4exp HC win.  This reverses S7's
        // routed decision -- S7's +6.7 % MoE number does not reproduce on the unmodified S7 binary
        // (it now measures -1.4 %), see gfx1201-s10-dense-geometry.md §6.  The sub-knobs below
        // (glu/iq3xxs_glu/routed_thresh) are therefore inert on RDNA4.
        c.routed = 0;
        // S13: the F32 split tile had been gated behind mmb_dense_flag() (S7 bundled them), which is
        // off on RDNA4 -- so it never ran.  It should: measured against the delivery at each depth it
        // is -0.3 % at pp8192, +0.9 % at pp32768, +1.00 % at pp65536 and +1.22 % at pp98304 on
        // Flash-Next IQ4_XS (interleaved r=3), i.e. it costs a fraction of a percent at the start and
        // pays >1 % once the context is deep -- which is where the time actually goes.  The tiny-M
        // kernel (below) is the bigger win but flatter.  See the f32w comment and
        // wip/mmb-general/gfx1201-s13-f32-hc16.md.
        c.f32split_mode = 1;
        c.tiny_m       = 1;
        // gatemix: the kernel's arch-aware `mmb_frag_t`/`mmb_wmma_bf16`/`MMB_ACC_M` shim runs on
        // gfx12 too, and the fusion is bit-identical to the GEMM+sigmoid+mix chain.  Ported to RDNA4
        // 2026-09-24 (gfx1201): +5.8 %/+5.4 %/+5.3 % qwen4exp IQ4_NL prefill at pp8192/32768/65536,
        // byte-identical greedy text.  `LLAMA_HC_GATEMIX=0` is the opt-out.
        c.gatemix      = 1;
    }
    if (GGML_CUDA_CC_IS_RDNA3_0(cc)) {
        // gfx1100 S6/S7: the F32 MoE-router split TILE is a loss here (gemma-26B-A4B -3.1 %,
        // 35B-A3B -3.0 % at pp8192 and -2.4 % at pp32768), unlike RDNA4 where it wins at depth.
        // Disable the split tile on RDNA3_0; the tiny-M kernel is qwen4exp-only on gfx1100 and
        // keeps its default.  NOTE: gfx1100 was only measured to pp32768 -- re-check pp65536+
        // before finalising (see gfx1100-porting.md 14.5).
        c.f32split_mode = 0;
        // gatemix: the kernel's gfx11 WMMA builtin is shared with gfx1151 (ported 2026-09-23, the
        // call site now accepts RDNA3), but it stays default OFF on RDNA3_0 -- qwen4exp (the only
        // model with IQ4_NL HC gates) does not fit on gfx1100, so end-to-end A/B must be done on
        // gfx1151/gfx1201 first.  LLAMA_HC_GATEMIX=1 is the opt-in.
        c.gatemix       = 0;
    }
    // TODO(S12): the routed/GLU thresholds, `tall_mode`, `tiny_m*`, `f32split_*` and `cache_max` are
    // still the gfx1151 values on every arch.  Give RDNA4 its own once they are measured per arch.
    return c;
}

static const mmb_arch_cfg & mmb_cfg() {
    static const mmb_arch_cfg c = mmb_arch_defaults(ggml_cuda_info().devices[0].cc);
    return c;
}

// Print the resolved per-arch config + every env override exactly once (GGML_CUDA_MMB_CFG=1, or
// GGML_CUDA_MMB_LOG=1), so a profiler run records what was actually used.
static void mmb_cfg_dump_once() {
    static bool done = false;
    if (done) return;
    done = true;
    const bool on = (getenv("GGML_CUDA_MMB_CFG") && atoi(getenv("GGML_CUDA_MMB_CFG")) != 0) ||
                    (getenv("GGML_CUDA_MMB_LOG") && atoi(getenv("GGML_CUDA_MMB_LOG")) != 0);
    if (!on) return;
    const int cc = ggml_cuda_info().devices[0].cc;
    const mmb_arch_cfg & c = mmb_cfg();
    fprintf(stderr, "MMB_CFG cc=0x%x dense_geom=%d min_t=%d glu_thresh=%d routed_thresh=%d tall=%d "
                    "tiny_m=%d/%d f32split=%d(min_m=%d,min_k=%d) cache=%d shadow=%d/%dMB "
                    "hc16=%d down16=%d gatemix=%d blk16=%d res16=%d glu=%d bf16w=%d iq3xxs_glu=%d routed=%d\n",
            cc, c.dense_geom, c.min_t, c.glu_thresh, c.routed_thresh, c.tall_mode,
            c.tiny_m, c.tiny_tt, c.f32split_mode, c.f32split_min_m, c.f32split_min_k, c.cache_max,
            c.shadow_mode, c.shadow_cap_mb, c.hc16, c.down16, c.gatemix, c.blk16, c.res16,
            c.glu, c.bf16w, c.iq3xxs_glu, c.routed);
}

static size_t mmb_cache_max() { static const int v = getenv("GGML_CUDA_MMB_CACHE") ? atoi(getenv("GGML_CUDA_MMB_CACHE")) : mmb_cfg().cache_max; return (size_t) v; }
static const ggml_tensor * mmb_root(const ggml_tensor * t) { return t->view_src ? t->view_src : t; }
static uint16_t * mmb_cache_insert(ggml_backend_cuda_context & ctx, const ggml_tensor * t, const size_t n) {
    std::vector<mmb_cache_entry> & cache = mmb_state().cache;
    if (cache.size() >= mmb_cache_max()) { delete cache.front().buf; cache.erase(cache.begin()); }
    auto * buf = new ggml_cuda_pool_alloc<uint16_t>(ctx.pool(), n);
    cache.push_back({mmb_root(t), t->data, n, buf});
    return buf->get();
}
static const uint16_t * mmb_bf16_activation(ggml_backend_cuda_context & ctx, const ggml_tensor * src1, const size_t n, cudaStream_t stream) {
    const ggml_tensor * root = mmb_root(src1);
    for (auto & e : mmb_state().slots) if (e.buf && e.root == root && e.data == src1->data && e.n == n) return e.buf->get();
    for (auto & e : mmb_state().cache) if (e.root == root && e.data == src1->data && e.n == n) return e.buf->get();
    uint16_t * buf = mmb_cache_insert(ctx, src1, n);
    { static const int lg = getenv("LLAMA_MMB_CVT_LOG") ? atoi(getenv("LLAMA_MMB_CVT_LOG")) : 0; static unsigned cnt = 0;
      if (lg && (int) cnt++ < (lg > 1 ? lg : 200)) fprintf(stderr, "MMB_CVT %s op=%s ne=[%lld,%lld,%lld,%lld] view_src=%s src0=%s(%s) n=%zu data=%p root=%p\n", src1->name, ggml_op_name(src1->op), (long long) src1->ne[0], (long long) src1->ne[1], (long long) src1->ne[2], (long long) src1->ne[3], src1->view_src ? src1->view_src->name : "-", src1->src[0] ? src1->src[0]->name : "-", src1->src[0] ? ggml_op_name(src1->src[0]->op) : "-", n, src1->data, (const void *) root); }
    mmb_cvt_f32_bf16<<<(unsigned)((n / 8 + 255) / 256), 256, 0, stream>>>((const float *) src1->data, buf, n);
    return buf;
}

// Shadow BF16 copies of IQ4_NL dense weights: dequantised once (same LUT*scale -> BF16 RNE as mmb_dq_row36, so the
// WMMA inputs are bitwise identical) so the dense GEMM runs the dequant-free WTYPE=2 path.
__global__ void mmb_dq_q6k_bf16_kernel(const uint8_t * __restrict__ W, uint16_t * __restrict__ out, const size_t nblocks) {
    const size_t b = (size_t) blockIdx.x * blockDim.x + threadIdx.x;
    if (b >= nblocks) return;
    const uint8_t * p  = W + b * 210;
    const uint8_t * ql = p, * qh = p + 128;
    const int8_t  * sc = (const int8_t *) (p + 192);
    const float d = mmb_h2f(*(const uint16_t *) (p + 208));
    uint16_t * o = out + b * 256;
    for (int n = 0; n < 2; ++n) {
        const uint8_t * QL = ql + 64 * n; const uint8_t * QH = qh + 32 * n; const int8_t * S = sc + 8 * n; uint16_t * Y = o + 128 * n;
        for (int l = 0; l < 32; ++l) {
            const int is = l / 16;
            const int8_t q1 = (int8_t)((QL[l +  0] & 0xF) | (((QH[l] >> 0) & 3) << 4)) - 32;
            const int8_t q2 = (int8_t)((QL[l + 32] & 0xF) | (((QH[l] >> 2) & 3) << 4)) - 32;
            const int8_t q3 = (int8_t)((QL[l +  0] >>  4) | (((QH[l] >> 4) & 3) << 4)) - 32;
            const int8_t q4 = (int8_t)((QL[l + 32] >>  4) | (((QH[l] >> 6) & 3) << 4)) - 32;
            Y[l +  0] = mmb_f2bf(d * S[is + 0] * q1);
            Y[l + 32] = mmb_f2bf(d * S[is + 2] * q2);
            Y[l + 64] = mmb_f2bf(d * S[is + 4] * q3);
            Y[l + 96] = mmb_f2bf(d * S[is + 6] * q4);
        }
    }
}

__global__ void mmb_dq_iq4nl_bf16_kernel(const uint8_t * __restrict__ W, uint16_t * __restrict__ out, const size_t nblocks) {
    const size_t b = (size_t) blockIdx.x * blockDim.x + threadIdx.x;
    if (b >= nblocks) return;
    const uint8_t * p = W + b * 18;
    const float d = mmb_h2f(*(const uint16_t *) p);
    const uint32_t * q = (const uint32_t *) (p + 2);
    uint32_t * o = (uint32_t *) (out + b * 32);
#pragma unroll
    for (int w = 0; w < 4; ++w) {
        const uint32_t v = q[w];
        const float l0 = d * mmb_kv_iq4nl[(v      ) & 0xF], h0 = d * mmb_kv_iq4nl[(v >>  4) & 0xF];
        const float l1 = d * mmb_kv_iq4nl[(v >>  8) & 0xF], h1 = d * mmb_kv_iq4nl[(v >> 12) & 0xF];
        const float l2 = d * mmb_kv_iq4nl[(v >> 16) & 0xF], h2 = d * mmb_kv_iq4nl[(v >> 20) & 0xF];
        const float l3 = d * mmb_kv_iq4nl[(v >> 24) & 0xF], h3 = d * mmb_kv_iq4nl[(v >> 28) & 0xF];
        o[2*w] = mmb_pack2(l0, l1); o[2*w + 1] = mmb_pack2(l2, l3); o[8 + 2*w] = mmb_pack2(h0, h1); o[8 + 2*w + 1] = mmb_pack2(h2, h3);
    }
}
static std::unordered_map<const void *, uint16_t *> g_mmb_shadow;
static std::map<std::pair<const void *, const void *>, uint16_t *> g_mmb_shadow_pair;   // concat(w0, w1) along rows -> BF16 copy
static size_t g_mmb_shadow_bytes = 0;
int    mmb_shadow_mode(){ static const int v = getenv("GGML_CUDA_MMB_SHADOW") ? atoi(getenv("GGML_CUDA_MMB_SHADOW")) : mmb_cfg().shadow_mode; return v; }
bool   mmb_shadow()    { return mmb_shadow_mode() != 0; }
bool   mmb_shadow_q6k(){ return mmb_shadow_mode() >= 1; }
size_t mmb_shadow_cap(){ static const long v = getenv("GGML_CUDA_MMB_SHADOW_MB") ? atol(getenv("GGML_CUDA_MMB_SHADOW_MB")) : mmb_cfg().shadow_cap_mb; return (size_t) v << 20; }
static bool mmb_is_resident_q6k(const ggml_tensor * w) { return w && w->type == GGML_TYPE_Q6_K && w->op == GGML_OP_NONE && w->data && w->buffer && w->ne[2] == 1 && w->ne[3] == 1 && ggml_is_contiguous(w) && w->ne[0] % 256 == 0 && w->ne[1] <= 32768; }
static bool mmb_is_resident_iq4(const ggml_tensor * w) { return w && w->type == GGML_TYPE_IQ4_NL && w->op == GGML_OP_NONE && w->data && w->buffer && w->ne[2] == 1 && w->ne[3] == 1 && ggml_is_contiguous(w); }
static bool mmb_is_row_concat(const ggml_tensor * w) {
    return w && w->op == GGML_OP_CONCAT && w->type == GGML_TYPE_IQ4_NL && ggml_get_op_params_i32(w, 0) == 1 && mmb_is_resident_iq4(w->src[0]) && mmb_is_resident_iq4(w->src[1]) &&
           w->src[0]->ne[0] == w->src[1]->ne[0] && w->ne[0] == w->src[0]->ne[0] && w->ne[1] == w->src[0]->ne[1] + w->src[1]->ne[1];
}
static const uint16_t * mmb_shadow_lookup(const ggml_tensor * w) {
    if (w->op == GGML_OP_CONCAT) { auto it = g_mmb_shadow_pair.find({w->src[0]->data, w->src[1]->data}); return it == g_mmb_shadow_pair.end() ? nullptr : it->second; }
    auto it = g_mmb_shadow.find(w->data); return it == g_mmb_shadow.end() ? nullptr : it->second;
}

// rdna-boosts port of pwilkin's `mmb` bf16-WMMA dequant weight GEMM (branch strix-halo).
// Master gate: GGML_CUDA_MMB=1 (default off). Sub-feature defaults are the measured-optimal
// values from pwilkin's launcher, so a bare GGML_CUDA_MMB=1 gives the full path; each can be
// overridden with GGML_CUDA_MMB_* for A/B. Prefill-only: mmb_min_t() (default 512) keeps the
// whole decode/verify band (n_tokens <= 8) on the existing kernels, so W = 1..8 stays bit-identical
// with the gate on or off (see GREEDY-PURITY.md).
bool mmb_enabled() {
    // DEFAULT ON (maintainer policy 2026-09-21: a beneficial feature is on by default; the env var
    // exists only to disable it for A/B or debugging).  GGML_CUDA_MMB=0 turns the whole campaign off.
    static const int v = getenv("GGML_CUDA_MMB") ? atoi(getenv("GGML_CUDA_MMB")) : 1;
    if (v == 0) return false;
    // The kernels select the WMMA builtin + fragment layout at compile time: gfx11 uses the first-gen
    // `..._bf16_w32` builtin and the 16-half row fragment, gfx12 `..._bf16_w32_gfx12` and the 8-half
    // "two runs of four" fragment (see the shim at the top of the file).  RDNA3_5 (gfx1150/gfx1151),
    // RDNA4 (gfx1200/gfx1201) and RDNA3_0 (gfx1100/1101/1102, validated by the gfx1100 port) are on;
    // GGML_CUDA_MMB_RDNA3=0 disables the RDNA3_0 arm.
    const int cc = ggml_cuda_info().devices[0].cc;
    static const bool allow_rdna3 = getenv("GGML_CUDA_MMB_RDNA3") ? atoi(getenv("GGML_CUDA_MMB_RDNA3")) != 0 : true;
    if (GGML_CUDA_CC_IS_RDNA3_5(cc) || GGML_CUDA_CC_IS_RDNA4(cc) || (allow_rdna3 && GGML_CUDA_CC_IS_RDNA3_0(cc))) { mmb_cfg_dump_once(); return true; }
    return false;
}
int  mmb_min_t()   { static const int v = getenv("GGML_CUDA_MMB_MIN_T") ? atoi(getenv("GGML_CUDA_MMB_MIN_T")) : mmb_cfg().min_t; return v; }
// F32 dense weights: historically DEFAULT OFF (the 2026-09-19 session-4 note); SUPERSEDED by the
// shape-aware default below.  Kept only for the measured history: a 16x256 small-M tile was tried
// and is worse (870/847 vs 895/885 t/s -- BN=256 halves the block count).
// F32 dense weights: SHAPE-AWARE, default ON since 2026-09-19.  These GEMMs are tiny-M and both
// paths cost ~2.1 s at pp8192, but they do not agree on WHICH shape each wins.  Measured per shape
// (the launcher grid is ceil(M/128) x ceil(T/128) workgroups, so the launch identity is exact):
//
//   ffn_gate_inp        M=512 K=2560    rocBLAS 2.044 ms   MMB 0.846 ms   -> MMB 2.4x
//   hc_attn/ffn_inject  M=4   K=10240   rocBLAS 1.332 ms   MMB 1.825 ms   -> rocBLAS 1.37x
//   ssm_alpha/beta      M=48  K=2560    rocBLAS 0.359 ms   MMB (slower)   -> rocBLAS
//
// **The discriminator is M alone -- do not "improve" it with a K rule.**  An earlier version of
// this gate also took MMB for long K (>= 4096), expecting the hc inject pair (K=10240) to benefit
// from the WMMA path; profiling it showed the opposite (1.825 vs 1.332 ms/launch) and cost 375 ms.
// The trap that hid it: a first pass bucketed launches by grid alone, and the M<=128 bucket mixed
// hc_inject (760) + ssm (576) + others into one 1.065 ms average, which looked like a win for MMB.
// Split the bucket before believing a per-shape number.
//
// Why rocBLAS wins the small-M/short-K shapes: its MT32x32x8 kernel split-Ks hard (M=512/K=2560
// launches 262144 blocks for ~1.0M outputs, i.e. ~64 threads per output, so it pays partial-sum
// traffic) and loses the 2.4x there; on M=4/K=10240 the 128-row MMB tile wastes WMMA rows and loses.
// Net with the M>=128 rule: 1947 -> ~1537 ms of F32 (about -3 % prefill).
//
// Modes: 0 = off (all rocBLAS), 1 = shape-aware (default), 2 = every F32 GEMM through MMB (the
// pre-2026-09-19 behaviour, kept for A/B).
int  mmb_f32split_mode(){ static const int v = getenv("GGML_CUDA_MMB_F32SPLIT") ? atoi(getenv("GGML_CUDA_MMB_F32SPLIT")) : mmb_cfg().f32split_mode; return v; }
bool mmb_f32split() { return mmb_f32split_mode() != 0; }
// Defaults: MIN_M = 128 (the router; measured 2.4x better on MMB) and MIN_K = 0 (disabled -- the
// long-K hc inject pair is *worse* on MMB despite the WMMA path; see the table above).
int  mmb_f32split_min_k(){ static const int v = getenv("GGML_CUDA_MMB_F32SPLIT_MIN_K") ? atoi(getenv("GGML_CUDA_MMB_F32SPLIT_MIN_K")) : mmb_cfg().f32split_min_k; return v; }
int  mmb_f32split_min_m(){ static const int v = getenv("GGML_CUDA_MMB_F32SPLIT_MIN_M") ? atoi(getenv("GGML_CUDA_MMB_F32SPLIT_MIN_M")) : mmb_cfg().f32split_min_m; return v; }
// tiny-M F32 (the hc *_inject pair): M=hc=4, K=10240.  Neither tile suits it (see
// mmb_tiny_m_f32_kernel), so it gets its own warp-per-token kernel.  This has to be part of the
// *gate* as well as the launcher -- an M-based rule that only admits M >= 128 would reject the
// shape here and rocBLAS would run, silently, with the kernel never reached.
bool mmb_tiny_m_f32_ok(const int64_t K, const int64_t M) {
    static const int v = getenv("GGML_CUDA_MMB_TINY_M") ? atoi(getenv("GGML_CUDA_MMB_TINY_M")) : mmb_cfg().tiny_m;
    return v != 0 && M >= 1 && M <= 8 && K % 4 == 0;
}
int mmb_tiny_m_f32_tt() {
    static const int v = getenv("GGML_CUDA_MMB_TINY_TT") ? atoi(getenv("GGML_CUDA_MMB_TINY_TT")) : mmb_cfg().tiny_tt;
    return v;
}
bool mmb_bf16w()    { static const int v = getenv("GGML_CUDA_MMB_BF16W") ? atoi(getenv("GGML_CUDA_MMB_BF16W")) : mmb_cfg().bf16w; return v != 0; }
bool mmb_hc16()    { static const int v = getenv("GGML_CUDA_MMB_HC16") ? atoi(getenv("GGML_CUDA_MMB_HC16")) : mmb_cfg().hc16; return v != 0; }
int  mmb_tall_mode(){ static const int v = getenv("GGML_CUDA_MMB_TALL") ? atoi(getenv("GGML_CUDA_MMB_TALL")) : mmb_cfg().tall_mode; return v; }
bool mmb_tall()    { return mmb_tall_mode() != 0; }
bool mmb_gatemix_flag() { static const int v = getenv("LLAMA_HC_GATEMIX") ? atoi(getenv("LLAMA_HC_GATEMIX")) : mmb_cfg().gatemix; return v != 0; }
bool mmb_down16_flag() { static const int v = getenv("GGML_CUDA_MMB_DOWN16") ? atoi(getenv("GGML_CUDA_MMB_DOWN16")) : mmb_cfg().down16; return v != 0; }
bool mmb_glu()     { static const int v = getenv("GGML_CUDA_MMB_GLU") ? atoi(getenv("GGML_CUDA_MMB_GLU")) : mmb_cfg().glu; return v != 0; }
// Path policy for the generic *quantized* dense tile GEMM (a plain MUL_MAT -- attention qkv/o and
// the dense MLP), as opposed to the routed MoE expert path and the qwen4exp HC paths (tall-M /
// tiny-M / gate-mix), which have their own knobs below.  Off by default on RDNA4: the measurements
// in wip/mmb-general/gfx1201-s5s7-mmb-results.md put every RDNA4 win on a routed/HC shape and the
// dense big-M shapes on the losing side, so the two are separable and are kept separate here.
// GGML_CUDA_MMB_DENSE forces it on/off for either arch for A/B.
bool mmb_dense_flag() {
    static const int v = getenv("GGML_CUDA_MMB_DENSE") ? atoi(getenv("GGML_CUDA_MMB_DENSE")) : -1;
    if (v >= 0) return v != 0;
    return !GGML_CUDA_CC_IS_RDNA4(ggml_cuda_info().devices[0].cc);
}
// Tile-class threshold: experts with >= this many rows take the BN=128 tile, the rest the BN=32 one.
// The small tile re-dequantizes the same A panel once per jt tile (4x the weight dequant per output
// for a 128-row expert), so this is the knob that trades B-column padding against dequant volume.
// The routed MoE path (MUL_MAT_ID + its fused gate+up+GLU), the counterpart of mmb_dense_flag().
// OFF makes every routed op keep the delivery's `mul_mat_q_routed_compact` / `mul_mat_q` path and
// costs nothing (the graph's fusion stand-down goes through this predicate).  Measured on RDNA4:
// `35B-A3B UD-Q3_K_M` routed-ON -1.4 %, routed-OFF +-0.0 % (two interleaved rounds).
bool mmb_routed_flag() {
    static const int v = getenv("GGML_CUDA_MMB_ROUTED") ? atoi(getenv("GGML_CUDA_MMB_ROUTED")) : mmb_cfg().routed;
    return v != 0;
}
int mmb_glu_thresh() { static const int v = getenv("GGML_CUDA_MMB_GLU_THRESH") ? atoi(getenv("GGML_CUDA_MMB_GLU_THRESH")) : mmb_cfg().glu_thresh; return v; }
int mmb_routed_thresh() { static const int v = getenv("GGML_CUDA_MMB_ROUTED_THRESH") ? atoi(getenv("GGML_CUDA_MMB_ROUTED_THRESH")) : mmb_cfg().routed_thresh; return v; }
// IQ3_XXS: implemented and correct.  Its MMB *routed* path (individual gate/up MUL_MAT_ID) is a
// win, but the MMB *fused GLU* path is a NET LOSS on the UD-Q3_K_M workload (the fused kernel's
// dequant/loop shape costs more than the bf16-WMMA gain): full-on 1921 vs routed-only 2121 t/s
// pp8192 (both vs 1942 off).  So the fused GLU arm is default-off; GGML_CUDA_MMB_IQ3XXS=1 enables it
// for re-measurement/tuning.
bool mmb_iq3xxs_glu() { static const int v = getenv("GGML_CUDA_MMB_IQ3XXS") ? atoi(getenv("GGML_CUDA_MMB_IQ3XXS")) : mmb_cfg().iq3xxs_glu; return v != 0; }

// ---------------------------------------------------------------------------------------------
// Weight-type policy.  MMB's per-type kernels do NOT transfer between arches: on RDNA4 (gfx12)
// the delivery's MMQ path is far better tuned than it was on gfx1151, and the measurement
// (wip/mmb-general/gfx1201-s5s7-mmb-results.md) is unambiguous about which side each type is on:
//   IQ family (IQ4_NL / IQ3_S / IQ4_XS / IQ3_XXS)  ->  MMB WINS   (+2.7 .. +11.8 %)
//   Q8_0, Q5_1, Q3_K, Q4_K, Q5_K, Q6_K             ->  MMB LOSES  (-4.5 .. -12.7 %)
// 2026-09-23 (gfx1201, wip/closing-the-gap): the S5-S7 Q4Q5 group is NOT uniform.  Q4_1 and Q5_0
// are large DENSE wins on RDNA4 -- interleaved r=3, pp8192/pp32768 (ub 4096), MMB vs the delivery
// MMQ: 27B Q4_1 +6.9 %/+6.1 %, 27B Q5_0 +14.1 %/+12.7 %; 9B +8.0 %/+7.1 % and +14.4 %/+12.8 %;
// 4B +5.1 %/+4.2 % and +10.4 %/+8.7 %.  Q4_0 is the odd one out and LOSES (4B -2.9 %/-1.9 %,
// 9B -0.6 %/-0.3 %, 27B -2.4 %/-1.6 %).  So RDNA4 accepts the IQ family plus Q4_1 + Q5_0 only
// (Q4_0/MXFP4/NVFP4 stay excluded; the Q4_1/Q5_0 result is the delivery's MMQ path being weak on
// those two, not the MMB kernel being better in general).  See the 2026-09-23 record in
// wip/closing-the-gap/.
// So the accepted set is ARCH-SCOPED, and the five hardcoded copies of it (dense/routed/mm/mmid/glu)
// collapse into one mask here.  The graph's MMQ-fusion stand-down goes through these same
// predicates (ggml-cuda.cu), so a type this returns false for costs nothing -- it simply keeps the
// delivery's MMQ path and nothing is stood down for it.
//
// GGML_CUDA_MMB_TYPES=<csv of type names, e.g. iq4_xs,iq3_xxs,q8_0> overrides the set, so a future
// gfx1201 per-type re-tune is validated without a rebuild (and without re-cutting the patch).
static uint64_t mmb_wtype_mask() {
    static uint64_t mask = 0;
    static bool init = false;
    if (init) return mask;
    init = true;
    const uint64_t IQ_FAMILY = (1ull << GGML_TYPE_IQ4_NL) | (1ull << GGML_TYPE_IQ3_S) |
                               (1ull << GGML_TYPE_IQ4_XS) | (1ull << GGML_TYPE_IQ3_XXS);
    const uint64_t K_AND_Q8  = (1ull << GGML_TYPE_Q8_0) | (1ull << GGML_TYPE_Q5_1) |
                               (1ull << GGML_TYPE_Q3_K) | (1ull << GGML_TYPE_Q4_K) |
                               (1ull << GGML_TYPE_Q5_K) | (1ull << GGML_TYPE_Q6_K);
    const uint64_t Q4Q5      = (1ull << GGML_TYPE_Q4_0) | (1ull << GGML_TYPE_Q4_1) | (1ull << GGML_TYPE_Q5_0) | (1ull << GGML_TYPE_MXFP4) | (1ull << GGML_TYPE_NVFP4);
    const uint64_t IQ2       = (1ull << GGML_TYPE_IQ2_S) | (1ull << GGML_TYPE_IQ2_XS) | (1ull << GGML_TYPE_IQ2_XXS);
    const char * e = getenv("GGML_CUDA_MMB_TYPES");
    if (e && *e) {
        static const struct { const char * n; ggml_type t; } tab[] = {
            {"iq4_nl", GGML_TYPE_IQ4_NL}, {"iq3_s", GGML_TYPE_IQ3_S}, {"iq4_xs", GGML_TYPE_IQ4_XS}, {"iq3_xxs", GGML_TYPE_IQ3_XXS},
            {"q8_0", GGML_TYPE_Q8_0}, {"q5_1", GGML_TYPE_Q5_1}, {"q3_k", GGML_TYPE_Q3_K},
            {"q4_k", GGML_TYPE_Q4_K}, {"q5_k", GGML_TYPE_Q5_K}, {"q6_k", GGML_TYPE_Q6_K},
            {"q4_0", GGML_TYPE_Q4_0}, {"q4_1", GGML_TYPE_Q4_1}, {"q5_0", GGML_TYPE_Q5_0}, {"mxfp4", GGML_TYPE_MXFP4}, {"nvfp4", GGML_TYPE_NVFP4}, {"iq2_s", GGML_TYPE_IQ2_S}, {"iq2_xs", GGML_TYPE_IQ2_XS}, {"iq2_xxs", GGML_TYPE_IQ2_XXS},
        };
        for (const char * p = e; *p; ) {
            while (*p == ',' || *p == ' ') ++p;
            const char * q = p;
            while (*q && *q != ',' && *q != ' ') ++q;
            const size_t n = (size_t) (q - p);
            if (n) for (const auto & x : tab) if (strlen(x.n) == n && strncmp(x.n, p, n) == 0) mask |= 1ull << x.t;
            p = q;
        }
        int cnt = 0; for (uint64_t m = mask; m; m &= m - 1) ++cnt;
        fprintf(stderr, "MMB: GGML_CUDA_MMB_TYPES=%s -> %d weight type(s)\n", e, cnt);
        return mask;
    }
    const uint64_t RDNA4_SET = IQ_FAMILY | (1ull << GGML_TYPE_Q4_1) | (1ull << GGML_TYPE_Q5_0);
    mask = GGML_CUDA_CC_IS_RDNA4(ggml_cuda_info().devices[0].cc) ? RDNA4_SET : (IQ_FAMILY | K_AND_Q8 | Q4Q5 | IQ2);
    return mask;
}
static bool mmb_wtype_ok(const ggml_type t) { return (uint64_t) t < 64 && ((mmb_wtype_mask() >> t) & 1ull) != 0; }

// Per-TYPE dense-path policy (S10, wip/mmb-general/gfx1201-s10-dense-geometry.md).  On RDNA4 the
// generic quantized dense tile GEMM is a *per-weight-type* question, not a per-arch one: measured
// on 27B UD-IQ3_S at the 256x128 geometry (pp4096, 1 GPU, kernel time from rocprofv3)
//
//   IQ3_S   mmb_dense 1.856 s  vs  delivery MMQ 1.944 s   -> MMB WINS  -4.5 %  (enabled)
//   IQ3_XXS, IQ4_NL                    break-even at the same geometry     (not enabled)
//   IQ4_XS  mmb_dense 0.919 s  vs  delivery MMQ 0.829 s   -> MMB LOSES +10.9 %  (not enabled)
//
// 2026-09-23 (gfx1201): Q4_1 and Q5_0 are added to the RDNA4 dense set (the Q4_1/Q5_0 dense tile
// beats the delivery MMQ by +6..+14 % on 4B/9B/27B -- see the wtype-mask note above).  IQ3_S keeps
// its S10 slot; Q4_0/MXFP4/NVFP4/IQ2 stay out.
//
// and the whole-model interleaved A/B agrees (27B UD-IQ3_S: IQ3_S-only +0.5..+1.0 % prefill, the
// whole IQ family +0.04 % -- IQ4_XS cancels the win).  So RDNA4 enables only the measured winner;
// every other type keeps the delivery's MMQ path and costs nothing (the graph's fusion stand-down
// goes through this same predicate).  GGML_CUDA_MMB_DENSE=1 forces the whole dense path on for A/B;
// GGML_CUDA_MMB_DENSE_TYPES=<csv> overrides the set for a future per-type re-tune.
static uint64_t mmb_dense_tmask() {
    static uint64_t mask = 0;
    static bool init = false;
    if (init) return mask;
    init = true;
    const char * e = getenv("GGML_CUDA_MMB_DENSE_TYPES");
    if (e && *e) {
        static const struct { const char * n; ggml_type t; } tab[] = {
            {"iq4_nl", GGML_TYPE_IQ4_NL}, {"iq3_s", GGML_TYPE_IQ3_S}, {"iq4_xs", GGML_TYPE_IQ4_XS}, {"iq3_xxs", GGML_TYPE_IQ3_XXS},
            {"q8_0", GGML_TYPE_Q8_0}, {"q5_1", GGML_TYPE_Q5_1}, {"q3_k", GGML_TYPE_Q3_K},
            {"q4_k", GGML_TYPE_Q4_K}, {"q5_k", GGML_TYPE_Q5_K}, {"q6_k", GGML_TYPE_Q6_K},
            {"q4_0", GGML_TYPE_Q4_0}, {"q4_1", GGML_TYPE_Q4_1}, {"q5_0", GGML_TYPE_Q5_0}, {"mxfp4", GGML_TYPE_MXFP4}, {"nvfp4", GGML_TYPE_NVFP4}, {"iq2_s", GGML_TYPE_IQ2_S}, {"iq2_xs", GGML_TYPE_IQ2_XS}, {"iq2_xxs", GGML_TYPE_IQ2_XXS},
        };
        for (const char * p = e; *p; ) {
            while (*p == ',' || *p == ' ') ++p;
            const char * q = p;
            while (*q && *q != ',' && *q != ' ') ++q;
            const size_t n = (size_t) (q - p);
            if (n) for (const auto & x : tab) if (strlen(x.n) == n && strncmp(x.n, p, n) == 0) mask |= 1ull << x.t;
            p = q;
        }
        return mask;
    }
    if (GGML_CUDA_CC_IS_RDNA4(ggml_cuda_info().devices[0].cc)) mask = (1ull << GGML_TYPE_IQ3_S) | (1ull << GGML_TYPE_Q4_1) | (1ull << GGML_TYPE_Q5_0);
    else mask = ~0ull;   // elsewhere the pre-S10 behaviour stands: mmb_dense_flag() alone gates it
    return mask;
}
// The generic quantized dense tile GEMM may run for this weight type: arch policy (mmb_dense_flag)
// OR the per-type S10 winner.  The HC tall-M tile (mmb_tall_shape) is model-specific and separate.
static bool mmb_dense_type_ok(const ggml_type t) {
    if (mmb_dense_flag()) return true;
    return (uint64_t) t < 64 && ((mmb_dense_tmask() >> t) & 1ull) != 0;
}

// One dense-tile launch for the whole weight-type set, with the geometry as a template parameter so
// the per-arch tile (S10) can be selected without duplicating the type chain.
// The `default` arm is WTYPE 2 (a BF16 weight, or the BF16 *shadow* copy of an IQ4_NL/Q6_K weight).
template <int BM, int BN, int WTM, int WTN>
static void mmb_dense_launch_t(const ggml_type t, dim3 grid, cudaStream_t stream, const uint8_t * W,
        const uint16_t * xhp, float * D, uint16_t * Dh, const bool store_f32, const int M, const int K, const int T) {
    switch ((int) t) {
        case GGML_TYPE_IQ4_NL:  mmb_dense_kernel<BM,BN,WTM,WTN, 0><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T); break;
        case GGML_TYPE_Q8_0:    mmb_dense_kernel<BM,BN,WTM,WTN, 1><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T); break;
        case GGML_TYPE_Q4_K:    mmb_dense_kernel<BM,BN,WTM,WTN, 3><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T); break;
        case GGML_TYPE_Q5_1:    mmb_dense_kernel<BM,BN,WTM,WTN, 4><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T); break;
        case GGML_TYPE_IQ3_S:   mmb_dense_kernel<BM,BN,WTM,WTN, 5><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T); break;
        case GGML_TYPE_Q5_K:    mmb_dense_kernel<BM,BN,WTM,WTN, 6><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T); break;
        case GGML_TYPE_Q6_K:    mmb_dense_kernel<BM,BN,WTM,WTN, 7><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T); break;
        case GGML_TYPE_IQ4_XS:  mmb_dense_kernel<BM,BN,WTM,WTN, 8><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T); break;
        case GGML_TYPE_Q3_K:    mmb_dense_kernel<BM,BN,WTM,WTN, 9><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T); break;
        case GGML_TYPE_IQ3_XXS: mmb_dense_kernel<BM,BN,WTM,WTN,10><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T); break;
        case GGML_TYPE_Q4_0:    mmb_dense_kernel<BM,BN,WTM,WTN,11><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T); break;
        case GGML_TYPE_Q4_1:    mmb_dense_kernel<BM,BN,WTM,WTN,12><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T); break;
        case GGML_TYPE_Q5_0:    mmb_dense_kernel<BM,BN,WTM,WTN,13><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T); break;
        case GGML_TYPE_MXFP4:   mmb_dense_kernel<BM,BN,WTM,WTN,14><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T); break;
        case GGML_TYPE_NVFP4:   mmb_dense_kernel<BM,BN,WTM,WTN,15><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T); break;
        case GGML_TYPE_IQ2_S:   mmb_dense_kernel<BM,BN,WTM,WTN,16><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T); break;
        case GGML_TYPE_IQ2_XS:  mmb_dense_kernel<BM,BN,WTM,WTN,17><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T); break;
        case GGML_TYPE_IQ2_XXS: mmb_dense_kernel<BM,BN,WTM,WTN,18><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T); break;
        default:                mmb_dense_kernel<BM,BN,WTM,WTN, 2><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T); break;
    }
}
// IQ3_XXS: its *routed* path is a win but its fused GLU / dense arm is a net loss on the UD-Q3_K_M
// workload (see the mmb_iq3xxs_glu note above), so those two callers additionally require the env.
static bool mmb_wtype_ok_glu(const ggml_type t) { return mmb_wtype_ok(t) && (t != GGML_TYPE_IQ3_XXS || mmb_iq3xxs_glu()); }

} // namespace

const uint16_t * ggml_cuda_mmb_cache_lookup(const ggml_tensor * t) {
    const ggml_tensor * root = mmb_root(t);
    for (auto & e : mmb_state().slots) if (e.buf && e.root == root && e.data == t->data) return e.buf->get();
    for (auto & e : mmb_state().cache) if (e.root == root && e.data == t->data) return e.buf->get();
    return nullptr;
}
uint16_t * ggml_cuda_mmb_slot_reserve(ggml_backend_cuda_context & ctx, int slot, const ggml_tensor * t, size_t n) {
    mmb_cache_entry & e = mmb_state().slots[slot];
    size_t & cap = mmb_state().slot_cap[slot];
    if (e.buf && cap < n) { delete e.buf; e.buf = nullptr; }
    if (!e.buf) { e.buf = new ggml_cuda_pool_alloc<uint16_t>(ctx.pool(), n); cap = n; }
    e.root = mmb_root(t); e.data = t->data; e.n = n;
    return e.buf->get();
}
void ggml_cuda_mmb_set_active_ctx(const ggml_backend_cuda_context * ctx) { g_mmb_active_ctx = ctx; }
// returns true (and clears the marks) when the active context starts a new graph's optimize pass.
bool ggml_cuda_mmb_optimize_begin(const void * graph_key) {
    mmb_ctx_state & m = mmb_state();
    const bool clear = m.after_compute || m.first_split == nullptr || graph_key == m.first_split;
    if (clear) {
        m.bf16_only.clear(); m.bf16_copy.clear(); m.bf16_slot.clear();
        m.first_split = graph_key; m.after_compute = false;
    }
    return clear;
}
void ggml_cuda_mmb_compute_done() {
    mmb_state().after_compute = true;
}
void ggml_cuda_mmb_marks_clear() {
    mmb_ctx_state & m = mmb_state();
    m.bf16_only.clear(); m.bf16_copy.clear(); m.bf16_slot.clear();
}
size_t ggml_cuda_mmb_marks_count() { return mmb_state().bf16_only.size(); }
void ggml_cuda_mmb_mark_bf16_only(const ggml_tensor * t) {
    static const int lg = getenv("GGML_CUDA_MMB_MARK_LOG") ? atoi(getenv("GGML_CUDA_MMB_MARK_LOG")) : 0;
    if (lg) fprintf(stderr, "MMB mark bf16_only: %-40s op=%s ne=[%lld,%lld,%lld,%lld] nb=[%zu,%zu,%zu,%zu]\n", t->name, ggml_op_name(t->op),
        (long long) t->ne[0], (long long) t->ne[1], (long long) t->ne[2], (long long) t->ne[3], t->nb[0], t->nb[1], t->nb[2], t->nb[3]);
    mmb_state().bf16_only.insert(t);
}
bool ggml_cuda_mmb_is_bf16_only(const ggml_tensor * t) { return mmb_state().bf16_only.count(t) > 0; }
void ggml_cuda_mmb_mark_bf16_copy(const ggml_tensor * t) { mmb_state().bf16_copy.insert(t); }
bool ggml_cuda_mmb_wants_bf16_copy(const ggml_tensor * t) { return mmb_state().bf16_copy.count(t) > 0; }
void ggml_cuda_mmb_mark_bf16_slot(const ggml_tensor * t, int slot) { mmb_state().bf16_slot[t] = slot; }
int  ggml_cuda_mmb_bf16_slot(const ggml_tensor * t) { auto & m = mmb_state(); auto it = m.bf16_slot.find(t); return it == m.bf16_slot.end() ? -1 : it->second; }
// Producer-side reserve for a BF16 copy: honours the graph-assigned dedicated slot (xn) so the copy
// is not clobbered by the next generic producer, falling back to slot 0.
uint16_t * ggml_cuda_mmb_reserve_auto(ggml_backend_cuda_context & ctx, const ggml_tensor * t, size_t n) {
    const int slot = ggml_cuda_mmb_bf16_slot(t);
    return ggml_cuda_mmb_slot_reserve(ctx, slot >= 0 && slot < MMB_SLOT_COUNT ? slot : 0, t, n);
}

void ggml_cuda_mmb_begin_graph() {
    mmb_ctx_state & s = mmb_state();
    for (auto & e : s.cache) delete e.buf;
    s.cache.clear();
    for (auto & e : s.slots) { e.root = nullptr; e.data = nullptr; e.n = 0; }
}
void ggml_cuda_mmb_release_all() {
    ggml_cuda_mmb_begin_graph();
    // free every context's cache/slots, including any that is not the active one
    for (auto & kv : g_mmb_state) {
        mmb_ctx_state & s = kv.second;
        for (auto & e : s.cache) delete e.buf;
        s.cache.clear();
        for (int i = 0; i < MMB_SLOT_COUNT; ++i) { if (s.slots[i].buf) delete s.slots[i].buf; s.slots[i].buf = nullptr; s.slot_cap[i] = 0; }
    }
    g_mmb_state.clear();
    g_mmb_active_ctx = nullptr;
    // the shadow weights are raw cudaMalloc, keyed by data pointer and held for the life of the
    // process. A model has finitely many weights so this never mattered, but a long-lived process
    // that sees many distinct tensors (test-backend-ops) keeps every one of them.
    for (auto & e : g_mmb_shadow)      { if (e.second) { (void) cudaFree(e.second); } }
    for (auto & e : g_mmb_shadow_pair) { if (e.second) { (void) cudaFree(e.second); } }
    g_mmb_shadow.clear();
    g_mmb_shadow_pair.clear();
    g_mmb_shadow_bytes = 0;
}
uint16_t * ggml_cuda_mmb_cache_reserve(ggml_backend_cuda_context & ctx, const ggml_tensor * t, size_t n) {
    if (!mmb_enabled() || ggml_nrows(t) < mmb_min_t()) return nullptr;
    return ggml_cuda_mmb_slot_reserve(ctx, 0, t, n);
}

// The qwen4exp HC down/inject "tall-M" tile (IQ4_NL, small M, long K) -- a model-specific path, not
// the generic dense GEMM, so it survives mmb_dense_flag() (see the note at that flag).
static bool mmb_tall_shape(const ggml_type t, const int64_t K, const int64_t M, const int64_t T) {
    return mmb_tall() && t == GGML_TYPE_IQ4_NL && M <= 384 && K >= 4096 && T >= 2048;
}

bool ggml_cuda_mmb_supported_mm(const ggml_tensor * src0, const ggml_tensor * src1, const ggml_tensor * dst) {
    if (!mmb_enabled()) return false;
    if (src1->type != GGML_TYPE_F32 || dst->type != GGML_TYPE_F32) return false;
    if (src0->ne[2] != 1 || src0->ne[3] != 1) return false;
    if (!ggml_is_contiguous(src0) || !ggml_is_contiguous(src1) || !ggml_is_contiguous(dst)) return false;
    const int64_t K = src0->ne[0], M = src0->ne[1];
    const int64_t T = src1->ne[1] * src1->ne[2] * src1->ne[3];
    // The quantized types go through the generic dense tile GEMM, which on RDNA4 is a per-type
    // decision (see mmb_dense_type_ok / the S10 note); the HC tall-M tile is model-specific and
    // keeps its path.
    const bool quant   = mmb_wtype_ok_glu(src0->type) && (mmb_dense_type_ok(src0->type) || mmb_tall_shape(src0->type, K, M, T));
    const bool bf16w   = src0->type == GGML_TYPE_BF16 && mmb_bf16w() && mmb_dense_flag();
    const bool f32cand = src0->type == GGML_TYPE_F32;
    if (!quant && !bf16w && !f32cand) return false;
    // shape-aware F32 (mode 1, default): take only the shapes where MMB beat rocBLAS (see the
    // F32SPLIT comment above).  Mode 2 forces every F32 GEMM here; mode 0 never gets this far.
    // S13: the two F32 paths are INDEPENDENT policies.  `mmb_f32split_mode()` governs only the split
    // *tile* and `mmb_tiny_m_f32_ok()` only the tiny-M warp-per-token kernel -- they used to share
    // one predicate, so turning the split off silently killed the tiny-M kernel too (and S7 had
    // gated the split behind mmb_dense_flag(), which is off on RDNA4, so neither ran there).
    //
    // On RDNA4 the two carry different amounts and scale differently with depth (Flash-Next IQ4_XS,
    // 3-GPU tensor, interleaved r=3, vs the delivery):
    //     tiny-M kernel   +4.5 % pp8192 .. +5.2 % pp32768, flat to depth
    //     split tile      -0.3 % pp8192, +0.9 % pp32768, +1.00 % pp65536, +1.22 % pp98304
    // so BOTH stay on.  On gfx1151 it was the other way round (the M=512 router won 2.4x on the split
    // tile and the hc inject pair was worse on the tiny-M kernel -- see the F32SPLIT comment above);
    // that is why these are per-arch defaults now.
    const bool f32w = f32cand && (mmb_tiny_m_f32_ok(K, M) ||
                       (mmb_f32split() && (mmb_f32split_mode() >= 2 || (M >= mmb_f32split_min_m() && K >= mmb_f32split_min_k()))));
    if (f32cand && !f32w) return false;   // this F32 shape is better on rocBLAS -- no fusion stand-down either
    if ((f32cand ? K % 32 : K % 64) != 0 || src1->ne[0] != K || dst->ne[0] != M) return false;
    if ((src0->type == GGML_TYPE_Q4_K || src0->type == GGML_TYPE_Q5_K || src0->type == GGML_TYPE_Q6_K || src0->type == GGML_TYPE_Q3_K || src0->type == GGML_TYPE_IQ4_XS || src0->type == GGML_TYPE_IQ3_S || src0->type == GGML_TYPE_IQ3_XXS || src0->type == GGML_TYPE_IQ2_S || src0->type == GGML_TYPE_IQ2_XS || src0->type == GGML_TYPE_IQ2_XXS) && K % 256 != 0) return false;   // whole 256-value super-blocks
    if (T < mmb_min_t() || T > INT32_MAX / 4) return false;
    return ggml_nrows(dst) == T;
}

// True when this MMB GEMM reads its activation through the bf16 activation cache (i.e. the
// activation's F32 buffer is not required): every weight type except F32 does, and of the F32
// weights only the tiny-M warp-per-token kernel (the hc *_inject pair) does -- the WMMA f32-split
// tile reads the F32 tensor directly.  The graph optimizer uses this to decide whether an
// activation can be marked BF16-only (producer skips its F32 store).
bool ggml_cuda_mmb_reads_bf16_act(const ggml_tensor * src0, const ggml_tensor * src1, const ggml_tensor * dst) {
    if (!ggml_cuda_mmb_supported_mm(src0, src1, dst)) return false;
    if (src0->type != GGML_TYPE_F32) return true;
    return mmb_tiny_m_f32_ok(src0->ne[0], src0->ne[1]);
}

bool ggml_cuda_mmb_supported_mmid(const ggml_tensor * src0, const ggml_tensor * src1, const ggml_tensor * ids, const ggml_tensor * dst) {
    if (!mmb_enabled() || !mmb_routed_flag()) return false;
    const bool wtype = mmb_wtype_ok(src0->type);
    if (!wtype || src1->type != GGML_TYPE_F32 || dst->type != GGML_TYPE_F32 || ids->type != GGML_TYPE_I32) return false;
    if (!ggml_is_contiguous(src0) || !ggml_is_contiguous(src1) || !ggml_is_contiguous(dst)) return false;
    const int64_t K = src0->ne[0], M = src0->ne[1], E = src0->ne[2];
    if (src0->ne[3] != 1 || K % 64 != 0 || E < 1 || E > 1024) return false;
    if ((src0->type == GGML_TYPE_Q4_K || src0->type == GGML_TYPE_Q5_K || src0->type == GGML_TYPE_Q6_K || src0->type == GGML_TYPE_Q3_K || src0->type == GGML_TYPE_IQ4_XS || src0->type == GGML_TYPE_IQ3_S || src0->type == GGML_TYPE_IQ3_XXS || src0->type == GGML_TYPE_IQ2_S || src0->type == GGML_TYPE_IQ2_XS || src0->type == GGML_TYPE_IQ2_XXS) && K % 256 != 0) return false;
    const int64_t n_used = ids->ne[0], T = ids->ne[1];
    if (src1->ne[0] != K || src1->ne[3] != 1 || src1->ne[2] != T) return false;
    if (src1->ne[1] != 1 && src1->ne[1] != n_used) return false;
    if (dst->ne[0] != M || dst->ne[1] != n_used || dst->ne[2] != T || dst->ne[3] != 1) return false;
    if (ids->nb[0] != sizeof(int32_t) || ids->ne[2] != 1 || ids->ne[3] != 1) return false;
    if (T < mmb_min_t() || n_used > 64 || (T * n_used) >> 16 >= 1024) return false;   // tile index must fit in 16 bits per expert
    return true;
}

void ggml_cuda_mul_mat_mmb(ggml_backend_cuda_context & ctx, const ggml_tensor * src0, const ggml_tensor * src1, ggml_tensor * dst) {
    cudaStream_t stream = ctx.stream();
    const int K = (int) src0->ne[0], M = (int) src0->ne[1];
    const int T = (int) (src1->ne[1] * src1->ne[2] * src1->ne[3]);
    { static const int lg = getenv("GGML_CUDA_MMB_LOG") ? atoi(getenv("GGML_CUDA_MMB_LOG")) : 0;
      if (lg) { static unsigned cnt = 0; if (cnt++ < 80) fprintf(stderr, "MMB_DENSE %s type=%s M=%d K=%d T=%d\n", src0->name, ggml_type_name(src0->type), M, K, T); } }
    if (src0->type == GGML_TYPE_F32) {
        // tiny-M (the hc *_inject pair, M=hc=4): a warp-per-token kernel beats both tiles by a
        // wide margin -- see the mmb_tiny_m_f32_kernel comment.  Wide-M F32 (the MoE router,
        // M=512) keeps the WMMA f16-split tile, which is 2.4x faster than rocBLAS there.
        if (mmb_tiny_m_f32_ok(K, M)) {
            constexpr int NWARPS = MMB_NT / 32;
            const int tt = mmb_tiny_m_f32_tt();
            const bool wide = M > 4;   // M <= 4 fits TT accumulators with MMAX=4; above that, one token
            const int eff  = wide ? 1 : (tt >= 4 ? 4 : (tt == 2 ? 2 : 1));
            dim3 tg((T + NWARPS * eff - 1) / (NWARPS * eff));
            const float * Wf = (const float *) src0->data;
            // HC16: if the producer marked this activation BF16-only, the F32 tensor was never
            // written; read the bf16 copy out of the activation cache instead.  Walk the whole view
            // chain: the mark is on the fully-rooted producer output.
            const ggml_tensor * x_root = src1;
            while (x_root->view_src) x_root = x_root->view_src;
            const uint16_t * Xh = ggml_cuda_mmb_is_bf16_only(x_root) ? ggml_cuda_mmb_cache_lookup(x_root) : nullptr;
            const float * Xf = Xh ? (const float *) Xh : (const float *) src1->data;
            float * Df = (float *) dst->data;
            if (wide) {
                if (Xh)  mmb_tiny_m_f32_kernel<8, 1, true ><<<tg, MMB_NT, 0, stream>>>(Wf, Xf, Df, M, K, T);
                else     mmb_tiny_m_f32_kernel<8, 1, false><<<tg, MMB_NT, 0, stream>>>(Wf, Xf, Df, M, K, T);
            } else if (eff == 4) {
                if (Xh)  mmb_tiny_m_f32_kernel<4, 4, true ><<<tg, MMB_NT, 0, stream>>>(Wf, Xf, Df, M, K, T);
                else     mmb_tiny_m_f32_kernel<4, 4, false><<<tg, MMB_NT, 0, stream>>>(Wf, Xf, Df, M, K, T);
            } else if (eff == 2) {
                if (Xh)  mmb_tiny_m_f32_kernel<4, 2, true ><<<tg, MMB_NT, 0, stream>>>(Wf, Xf, Df, M, K, T);
                else     mmb_tiny_m_f32_kernel<4, 2, false><<<tg, MMB_NT, 0, stream>>>(Wf, Xf, Df, M, K, T);
            } else {
                if (Xh)  mmb_tiny_m_f32_kernel<4, 1, true ><<<tg, MMB_NT, 0, stream>>>(Wf, Xf, Df, M, K, T);
                else     mmb_tiny_m_f32_kernel<4, 1, false><<<tg, MMB_NT, 0, stream>>>(Wf, Xf, Df, M, K, T);
            }
            CUDA_CHECK(cudaGetLastError()); return;
        }
        dim3 grid((M + 127) / 128, (T + 127) / 128);
        static const bool two = getenv("LLAMA_MMB_F32SPLIT") && atoi(getenv("LLAMA_MMB_F32SPLIT")) >= 2;
        if (two) mmb_f32split_kernel<128, 128, 32, 64, true ><<<grid, MMB_NT, 0, stream>>>((const float *) src0->data, (const float *) src1->data, (float *) dst->data, M, K, T);
        else     mmb_f32split_kernel<128, 128, 32, 64, false><<<grid, MMB_NT, 0, stream>>>((const float *) src0->data, (const float *) src1->data, (float *) dst->data, M, K, T);
        CUDA_CHECK(cudaGetLastError()); return;
    }
    const uint16_t * xhp = mmb_bf16_activation(ctx, src1, (size_t) T * K, stream);
    const uint8_t * W = (const uint8_t *) src0->data; float * D = (float *) dst->data;
    // The tall tile is a 384-row panel, so an M below this wastes most of it.  The model emits an M=4
    // (HC inject) GEMM that otherwise takes the tall tile for 4 of 384 rows; the reference routes it
    // outside the tall path and is faster for it.  Default 16 keeps every real tall-M shape (M=320
    // here) and sends the M=4 inject to the dense path.  Overridable for A/B (0 = old behaviour).
    static const int tall_min_m = []() { const char * e = getenv("GGML_CUDA_MMB_TALL_MIN_M"); return e ? atoi(e) : 16; }();
    if (mmb_tall() && src0->type == GGML_TYPE_IQ4_NL && M <= 384 && M >= tall_min_m && K >= 4096 && T >= 2048) {   // tall-M tile: HC down|inject [10240 -> 324], activations read once
        static const int wide = mmb_tall_mode() >= 2;
        if (wide) { dim3 grid(1, (T + 63) / 64); mmb_dense_kernel<384, 64, 96, 32, 0><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, (uint16_t *) nullptr, true, M, K, T); }
        else      { dim3 grid(1, (T + 31) / 32); mmb_dense_kernel<384, 32, 96, 16, 0><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, (uint16_t *) nullptr, true, M, K, T); }
        CUDA_CHECK(cudaGetLastError());
        static unsigned hits = 0; if (hits++ < 2) fprintf(stderr, "MMB_TALL%s dense M=%d K=%d T=%d\n", wide ? "(wide 384x64)" : "(384x32)", M, K, T);
        return;
    }
    const uint16_t * shadow_pre = ((src0->type == GGML_TYPE_IQ4_NL && mmb_shadow()) || src0->type == GGML_TYPE_Q6_K) ? mmb_shadow_lookup(src0) : nullptr;
    static const int tile_ov = getenv("GGML_CUDA_MMB_TILE") ? atoi(getenv("GGML_CUDA_MMB_TILE")) : -1;
    const bool big = tile_ov >= 0 ? (tile_ov != 0) : ((M >= 6144 && K >= 2560) || (shadow_pre && K >= 2560 && T >= 4096));
    // HC gate (K=320, M=10240).  "producer slots ... 1 = HC gate" -- when the graph marks this
    // output BF16-only, the gate is written ONCE into the pinned slot 1 and the dsv4_hc_pre
    // consumer reads that BF16 copy instead of round-tripping the F32 tensor.  The mark alone is
    // enough to allocate the slot; mmb_hc16() keeps the historical "always allocate" behaviour
    // for A/B (with an F32 store left in place because the mark is what suppresses it).
    uint16_t * Dh = ((mmb_hc16() || ggml_cuda_mmb_is_bf16_only(dst)) && K == 320 && M == 10240) ? ggml_cuda_mmb_slot_reserve(ctx, 1, dst, (size_t) T * M) : nullptr;
    bool store_f32 = !(Dh && ggml_cuda_mmb_is_bf16_only(dst));
    if (ggml_cuda_mmb_blk16() && !Dh && ggml_cuda_mmb_is_bf16_only(dst) && (M & 7) == 0) {
        Dh = (uint16_t *) dst->data; store_f32 = false;
        static unsigned h = 0; if (h++ < 2) fprintf(stderr, "MMB_BLK16 dense BF16 in place: M=%d K=%d T=%d\n", M, K, T);
    }
    dim3 grid((M + 127) / 128, big ? (T + 255) / 256 : (T + 127) / 128);
    const uint16_t * shadow = shadow_pre;
    // S10 per-arch dense geometry.  On RDNA4 the gfx1151-tuned 128x256/128x128 split is replaced by
    // a single 256x128 tile (WTM=64, WTN=64, TM=TN=4, LDS 55296): the 256-row A panel lets all 256
    // threads take part in the weight dequant instead of 128, which is what makes the IQ3_S tile
    // beat the delivery's MMQ (1.856 s vs 1.944 s, pp4096/1 GPU) where the 128-row panel did not
    // (3.985 s vs 3.887 s, pp8192).  The geometry is numerics-neutral -- every valid geometry
    // reproduces the same same-seed text hash -- so this needs no purity re-validation.
    // (Validity rule, easy to get wrong: BN must equal (8/(BM/WTM))*WTN.  A NxM mismatch silently
    // computes only part of the output and *looks* fast; rocprofv3 grid + a same-seed hash catch it.)
    const ggml_type wtype = shadow ? GGML_TYPE_BF16 : src0->type;
    if (mmb_cfg().dense_geom == MMB_GEOM_R4_256x128) {
        dim3 grid4((M + 255) / 256, (T + 127) / 128);
        mmb_dense_launch_t<256, 128, 64, 64>(wtype, grid4, stream, shadow ? (const uint8_t *) shadow : W, xhp, D, Dh, store_f32, M, K, T);
        CUDA_CHECK(cudaGetLastError());
        return;
    }
    if (shadow) {
        if (big) mmb_dense_kernel<128, 256, 64, 64, 2><<<grid, MMB_NT, 0, stream>>>((const uint8_t *) shadow, xhp, D, Dh, store_f32, M, K, T);
        else     mmb_dense_kernel<128, 128, 32, 64, 2><<<grid, MMB_NT, 0, stream>>>((const uint8_t *) shadow, xhp, D, Dh, store_f32, M, K, T);
    } else if (src0->type == GGML_TYPE_IQ4_NL) {
        if (big) mmb_dense_kernel<128, 256, 64, 64, 0><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
        else     mmb_dense_kernel<128, 128, 32, 64, 0><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
    } else if (src0->type == GGML_TYPE_Q8_0) {
        if (big) mmb_dense_kernel<128, 256, 64, 64, 1><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
        else     mmb_dense_kernel<128, 128, 32, 64, 1><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
    } else if (src0->type == GGML_TYPE_Q4_K) {
        if (big) mmb_dense_kernel<128, 256, 64, 64, 3><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
        else     mmb_dense_kernel<128, 128, 32, 64, 3><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
    } else if (src0->type == GGML_TYPE_Q5_1) {
        if (big) mmb_dense_kernel<128, 256, 64, 64, 4><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
        else     mmb_dense_kernel<128, 128, 32, 64, 4><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
    } else if (src0->type == GGML_TYPE_IQ3_S) {
        if (big) mmb_dense_kernel<128, 256, 64, 64, 5><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
        else     mmb_dense_kernel<128, 128, 32, 64, 5><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
    } else if (src0->type == GGML_TYPE_Q5_K) {
        if (big) mmb_dense_kernel<128, 256, 64, 64, 6><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
        else     mmb_dense_kernel<128, 128, 32, 64, 6><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
    } else if (src0->type == GGML_TYPE_Q6_K) {
        if (big) mmb_dense_kernel<128, 256, 64, 64, 7><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
        else     mmb_dense_kernel<128, 128, 32, 64, 7><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
    } else if (src0->type == GGML_TYPE_IQ4_XS) {
        if (big) mmb_dense_kernel<128, 256, 64, 64, 8><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
        else     mmb_dense_kernel<128, 128, 32, 64, 8><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
    } else if (src0->type == GGML_TYPE_Q3_K) {
        if (big) mmb_dense_kernel<128, 256, 64, 64, 9><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
        else     mmb_dense_kernel<128, 128, 32, 64, 9><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
    } else if (src0->type == GGML_TYPE_IQ3_XXS) {
        if (big) mmb_dense_kernel<128, 256, 64, 64, 10><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
        else     mmb_dense_kernel<128, 128, 32, 64, 10><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
    } else if (src0->type == GGML_TYPE_Q4_0) {
        if (big) mmb_dense_kernel<128, 256, 64, 64, 11><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
        else     mmb_dense_kernel<128, 128, 32, 64, 11><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
    } else if (src0->type == GGML_TYPE_Q4_1) {
        if (big) mmb_dense_kernel<128, 256, 64, 64, 12><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
        else     mmb_dense_kernel<128, 128, 32, 64, 12><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
    } else if (src0->type == GGML_TYPE_Q5_0) {
        if (big) mmb_dense_kernel<128, 256, 64, 64, 13><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
        else     mmb_dense_kernel<128, 128, 32, 64, 13><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
    } else if (src0->type == GGML_TYPE_MXFP4) {
        if (big) mmb_dense_kernel<128, 256, 64, 64, 14><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
        else     mmb_dense_kernel<128, 128, 32, 64, 14><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
    } else if (src0->type == GGML_TYPE_NVFP4) {
        if (big) mmb_dense_kernel<128, 256, 64, 64, 15><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
        else     mmb_dense_kernel<128, 128, 32, 64, 15><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
    } else if (src0->type == GGML_TYPE_IQ2_S) {
        if (big) mmb_dense_kernel<128, 256, 64, 64, 16><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
        else     mmb_dense_kernel<128, 128, 32, 64, 16><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
    } else if (src0->type == GGML_TYPE_IQ2_XS) {
        if (big) mmb_dense_kernel<128, 256, 64, 64, 17><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
        else     mmb_dense_kernel<128, 128, 32, 64, 17><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
    } else if (src0->type == GGML_TYPE_IQ2_XXS) {
        if (big) mmb_dense_kernel<128, 256, 64, 64, 18><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
        else     mmb_dense_kernel<128, 128, 32, 64, 18><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
    } else {
        if (big) mmb_dense_kernel<128, 256, 64, 64, 2><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
        else     mmb_dense_kernel<128, 128, 32, 64, 2><<<grid, MMB_NT, 0, stream>>>(W, xhp, D, Dh, store_f32, M, K, T);
    }
    CUDA_CHECK(cudaGetLastError());
}

// Per-TYPE dense-path policy (S10, wip/mmb-general/gfx1201-s10-dense-geometry.md) is defined above
// mmb_dense_launch_t, next to the weight-type mask.

bool ggml_cuda_mmb_gatemix() { return mmb_gatemix_flag(); }
// True while the MMB gate is on; used by the graph optimizer to stand its own IQ4_NL MoE MMQ
// fusions down so the ops reach the mmb dispatch in ggml_cuda_mul_mat / _mul_mat_id instead.
bool ggml_cuda_mmb_active() { return mmb_enabled(); }
bool ggml_cuda_mmb_dense_will_take(const ggml_tensor * w) {
    if (!mmb_enabled() || !w) return false;
    return mmb_wtype_ok(w->type) && mmb_dense_type_ok(w->type);
}
bool ggml_cuda_mmb_routed_will_take(const ggml_tensor * w) {
    if (!mmb_enabled() || !w) return false;
    return mmb_routed_flag() && mmb_wtype_ok(w->type);
}
bool ggml_cuda_mmb_down16() { return mmb_down16_flag(); }
bool ggml_cuda_mmb_blk16() { static const int v = getenv("LLAMA_HC_BLK16") ? atoi(getenv("LLAMA_HC_BLK16")) : mmb_cfg().blk16; return v != 0; }
bool ggml_cuda_mmb_res16()  { static const int v = getenv("LLAMA_HC_RES16") ? atoi(getenv("LLAMA_HC_RES16")) : mmb_cfg().res16; return v != 0; }
bool ggml_cuda_hc_gate_mix(ggml_backend_cuda_context & ctx, const ggml_tensor * w, const ggml_tensor * lo, const ggml_tensor * xn, ggml_tensor * dst,
        const int hc, const float scale, const float bias) {
    static const int gdbg = getenv("LLAMA_HC_GATEMIX_DEBUG") ? atoi(getenv("LLAMA_HC_GATEMIX_DEBUG")) : 0;
    if (!mmb_gatemix_flag() || hc != 4 || w->type != GGML_TYPE_IQ4_NL || lo->type != GGML_TYPE_F32 || !ggml_is_contiguous(lo) || !ggml_is_contiguous(dst)) {
        if (gdbg) fprintf(stderr, "HC_GATEMIX reject head: flag=%d hc=%d wtype=%s lotype=%s\n", (int) mmb_gatemix_flag(), hc, ggml_type_name(w->type), ggml_type_name(lo->type));
        return false;
    }
    const int K = (int) w->ne[0], M = (int) w->ne[1], E = (int) dst->ne[0]; const int T = (int) ggml_nrows(dst);
    if (K % MMB_BK != 0 || M != hc * E || E % 32 != 0 || lo->ne[0] != K || ggml_nrows(lo) != T || xn->ne[0] != M || ggml_nrows(xn) != T || T < mmb_min_t()) {
        if (gdbg) fprintf(stderr, "HC_GATEMIX reject shape: K=%d M=%d E=%d T=%d lo=[%lld,%lld] xn=[%lld,%lld] min_t=%d\n",
                K, M, E, T, (long long) lo->ne[0], (long long) ggml_nrows(lo), (long long) xn->ne[0], (long long) ggml_nrows(xn), mmb_min_t());
        return false;
    }
    const uint16_t * xn16 = ggml_cuda_mmb_cache_lookup(xn);
    if (!xn16) {
        if (gdbg) fprintf(stderr, "HC_GATEMIX reject cache: xn=%s not in the BF16 activation cache (HC16 marks absent?)\n", xn->name);
        return false;
    }
    if (gdbg) fprintf(stderr, "HC_GATEMIX FIRED xn=%s w=%s K=%d M=%d E=%d T=%d\n", xn->name, w->name, K, M, E, T);
    cudaStream_t stream = ctx.stream();
    const uint16_t * lo16 = mmb_bf16_activation(ctx, lo, (size_t) T * K, stream);
    uint16_t * outh = ggml_cuda_mmb_slot_reserve(ctx, 3, dst, (size_t) T * E);
    const bool store_f32 = !(outh && ggml_cuda_mmb_is_bf16_only(dst));
    dim3 grid(E / 32, (T + 127) / 128);
    hc_gate_mix_kernel<4><<<grid, MMB_NT, 0, stream>>>((const uint8_t *) w->data, lo16, xn16, (float *) dst->data, outh, store_f32, E, K, T, scale, bias);
    CUDA_CHECK(cudaGetLastError());
    return true;
}

static void mmb_routed_kernel_dispatch(const ggml_tensor * w, dim3 gbig, dim3 gsmall, cudaStream_t stream,
        const uint8_t * W, const size_t eb, const uint16_t * xhp, float * D, uint16_t * Dh, const bool store_f32,
        const int32_t * ids_src, const int32_t * ids_dst, const int32_t * bounds,
        const uint32_t * desc_big, const uint32_t * desc_small, const int M, const int K, const int, const int) {
    if (w->type == GGML_TYPE_Q4_K) {
        mmb_routed_kernel<128, 128, 32, 64, 3><<<gbig,   MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_kernel<128,  32, 32, 16, 3><<<gsmall, MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else if (w->type == GGML_TYPE_Q5_1) {
        mmb_routed_kernel<128, 128, 32, 64, 4><<<gbig,   MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_kernel<128,  32, 32, 16, 4><<<gsmall, MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else if (w->type == GGML_TYPE_IQ3_S) {
        mmb_routed_kernel<128, 128, 32, 64, 5><<<gbig,   MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_kernel<128,  32, 32, 16, 5><<<gsmall, MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else if (w->type == GGML_TYPE_Q8_0) {
        mmb_routed_kernel<128, 128, 32, 64, 1><<<gbig,   MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_kernel<128,  32, 32, 16, 1><<<gsmall, MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else if (w->type == GGML_TYPE_Q5_K) {
        mmb_routed_kernel<128, 128, 32, 64, 6><<<gbig,   MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_kernel<128,  32, 32, 16, 6><<<gsmall, MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else if (w->type == GGML_TYPE_Q6_K) {
        mmb_routed_kernel<128, 128, 32, 64, 7><<<gbig,   MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_kernel<128,  32, 32, 16, 7><<<gsmall, MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else if (w->type == GGML_TYPE_IQ4_XS) {
        mmb_routed_kernel<128, 128, 32, 64, 8><<<gbig,   MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_kernel<128,  32, 32, 16, 8><<<gsmall, MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else if (w->type == GGML_TYPE_Q3_K) {
        mmb_routed_kernel<128, 128, 32, 64, 9><<<gbig,   MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_kernel<128,  32, 32, 16, 9><<<gsmall, MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else if (w->type == GGML_TYPE_IQ3_XXS) {
        mmb_routed_kernel<128, 128, 32, 64, 10><<<gbig,   MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_kernel<128,  32, 32, 16, 10><<<gsmall, MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else if (w->type == GGML_TYPE_Q4_0) {
        mmb_routed_kernel<128, 128, 32, 64, 11><<<gbig,   MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_kernel<128,  32, 32, 16, 11><<<gsmall, MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else if (w->type == GGML_TYPE_Q4_1) {
        mmb_routed_kernel<128, 128, 32, 64, 12><<<gbig,   MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_kernel<128,  32, 32, 16, 12><<<gsmall, MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else if (w->type == GGML_TYPE_Q5_0) {
        mmb_routed_kernel<128, 128, 32, 64, 13><<<gbig,   MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_kernel<128,  32, 32, 16, 13><<<gsmall, MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else if (w->type == GGML_TYPE_MXFP4) {
        mmb_routed_kernel<128, 128, 32, 64, 14><<<gbig,   MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_kernel<128,  32, 32, 16, 14><<<gsmall, MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else if (w->type == GGML_TYPE_NVFP4) {
        mmb_routed_kernel<128, 128, 32, 64, 15><<<gbig,   MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_kernel<128,  32, 32, 16, 15><<<gsmall, MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else if (w->type == GGML_TYPE_IQ2_S) {
        mmb_routed_kernel<128, 128, 32, 64, 16><<<gbig,   MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_kernel<128,  32, 32, 16, 16><<<gsmall, MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else if (w->type == GGML_TYPE_IQ2_XS) {
        mmb_routed_kernel<128, 128, 32, 64, 17><<<gbig,   MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_kernel<128,  32, 32, 16, 17><<<gsmall, MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else if (w->type == GGML_TYPE_IQ2_XXS) {
        mmb_routed_kernel<128, 128, 32, 64, 18><<<gbig,   MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_kernel<128,  32, 32, 16, 18><<<gsmall, MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else {
        mmb_routed_kernel<128, 128, 32, 64, 0><<<gbig,   MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_kernel<128,  32, 32, 16, 0><<<gsmall, MMB_NT, 0, stream>>>(W, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    }
}

void ggml_cuda_mul_mat_id_mmb(ggml_backend_cuda_context & ctx, const ggml_tensor * src0, const ggml_tensor * src1, const ggml_tensor * ids, ggml_tensor * dst) {
    cudaStream_t stream = ctx.stream();
    const int K = (int) src0->ne[0], M = (int) src0->ne[1], E = (int) src0->ne[2];
    const int ne11 = (int) src1->ne[1], T = (int) src1->ne[2], n_used = (int) ids->ne[0];
    const int n_rows_x = ne11 * T, n_rows = n_used * T;
    constexpr int BN = 128;

    const uint16_t * xhp = mmb_bf16_activation(ctx, src1, (size_t) n_rows_x * K, stream);

    ggml_cuda_pool_alloc<int32_t> ids_src1(ctx.pool(), n_rows);
    ggml_cuda_pool_alloc<int32_t> ids_dst(ctx.pool(), n_rows);
    ggml_cuda_pool_alloc<int32_t> bounds(ctx.pool(), E + 1);
    const int si1  = (int) (ids->nb[1] / sizeof(int32_t));
    const int sis1 = (int) (src1->nb[2] / src1->nb[1]);
    ggml_cuda_launch_mm_ids_helper((const int32_t *) ids->data, ids_src1.get(), ids_dst.get(), bounds.get(),
        E, T, n_used, ne11, si1, sis1, /*write_inverse=*/false, stream);
    constexpr int BN_SMALL = 32; const int THRESH = mmb_routed_thresh();
    const int nbig_max   = n_rows / BN + E + 1;
    const int nsmall_max = E * ((THRESH + BN_SMALL - 1) / BN_SMALL) + 1;
    ggml_cuda_pool_alloc<uint32_t> desc_big(ctx.pool(), nbig_max);
    ggml_cuda_pool_alloc<uint32_t> desc_small(ctx.pool(), nsmall_max);
    mmb_build_desc2<<<1, 1024, 0, stream>>>(bounds.get(), desc_big.get(), desc_small.get(), E, nbig_max, nsmall_max, BN, BN_SMALL, THRESH);

    const uint8_t * W = (const uint8_t *) src0->data; float * D = (float *) dst->data; const size_t eb = (size_t) src0->nb[2];
    uint16_t * Dh = (mmb_down16_flag() && ggml_cuda_mmb_is_bf16_only(dst)) ? (uint16_t *) dst->data : nullptr;
    const bool store_f32 = Dh == nullptr;
    if (Dh) { static unsigned hits = 0; if (hits++ < 2) fprintf(stderr, "MMB_DOWN16 routed down BF16 in place: M=%d K=%d rows=%d\n", M, K, n_rows); }
    dim3 gbig((M + 127) / 128, nbig_max), gsmall((M + 127) / 128, nsmall_max);
    mmb_routed_kernel_dispatch(src0, gbig, gsmall, stream, W, eb, xhp, D, Dh, store_f32, ids_src1.get(), ids_dst.get(), bounds.get(), desc_big.get(), desc_small.get(), M, K, BN, BN_SMALL);
    CUDA_CHECK(cudaGetLastError());
}

bool ggml_cuda_mmb_supported_glu(const ggml_tensor * gw, const ggml_tensor * uw, const ggml_tensor * src1, const ggml_tensor * ids, const ggml_tensor * glu) {
    if (!mmb_enabled() || !mmb_routed_flag() || !mmb_glu() || !gw || !uw || !src1 || !ids || !glu) return false;
        if (gw->type != uw->type) return false;
    const bool wtype = mmb_wtype_ok_glu(gw->type);
    if (!wtype) return false;
    if (!ggml_are_same_shape(gw, uw) || gw->nb[1] != uw->nb[1] || gw->nb[2] != uw->nb[2]) return false;
    if (glu->op != GGML_OP_GLU || ggml_get_glu_op(glu) != GGML_GLU_OP_SWIGLU || ggml_get_op_params_i32(glu, 1) != 0) return false;
    if (glu->type != GGML_TYPE_F32 || !ggml_is_contiguous(glu) || !glu->src[0] || !glu->src[1]) return false;
    if (glu->src[0]->op != GGML_OP_MUL_MAT_ID || glu->src[1]->op != GGML_OP_MUL_MAT_ID) return false;
    if (glu->src[0]->src[0] != gw || glu->src[1]->src[0] != uw || glu->src[0]->src[1] != src1 || glu->src[1]->src[1] != src1 || glu->src[0]->src[2] != ids || glu->src[1]->src[2] != ids) return false;
    if (ggml_nelements(glu) != ggml_nelements(glu->src[0]) || glu->ne[0] != gw->ne[1]) return false;
    return ggml_cuda_mmb_supported_mmid(gw, src1, ids, glu->src[0]) && ggml_cuda_mmb_supported_mmid(uw, src1, ids, glu->src[1]);
}

static void mmb_routed_glu_kernel_dispatch(const ggml_tensor * gw, dim3 gbig, dim3 gsmall, cudaStream_t stream,
        const uint8_t * Wg, const uint8_t * Wu, const size_t eb, const uint16_t * xhp, float * D, uint16_t * Dh, const bool store_f32,
        const int32_t * ids_src, const int32_t * ids_dst, const int32_t * bounds,
        const uint32_t * desc_big, const uint32_t * desc_small, const int M, const int K, const int, const int) {
    if (gw->type == GGML_TYPE_Q4_K) {
        mmb_routed_glu_kernel<64, 128, 32, 32, 3><<<gbig,   MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_glu_kernel<64,  32, 16, 16, 3><<<gsmall, MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else if (gw->type == GGML_TYPE_Q5_1) {
        mmb_routed_glu_kernel<64, 128, 32, 32, 4><<<gbig,   MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_glu_kernel<64,  32, 16, 16, 4><<<gsmall, MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else if (gw->type == GGML_TYPE_IQ3_S) {
        mmb_routed_glu_kernel<64, 128, 32, 32, 5><<<gbig,   MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_glu_kernel<64,  32, 16, 16, 5><<<gsmall, MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else if (gw->type == GGML_TYPE_Q8_0) {
        mmb_routed_glu_kernel<64, 128, 32, 32, 1><<<gbig,   MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_glu_kernel<64,  32, 16, 16, 1><<<gsmall, MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else if (gw->type == GGML_TYPE_Q5_K) {
        mmb_routed_glu_kernel<64, 128, 32, 32, 6><<<gbig,   MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_glu_kernel<64,  32, 16, 16, 6><<<gsmall, MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else if (gw->type == GGML_TYPE_Q6_K) {
        mmb_routed_glu_kernel<64, 128, 32, 32, 7><<<gbig,   MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_glu_kernel<64,  32, 16, 16, 7><<<gsmall, MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else if (gw->type == GGML_TYPE_IQ4_XS) {
        mmb_routed_glu_kernel<64, 128, 32, 32, 8><<<gbig,   MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_glu_kernel<64,  32, 16, 16, 8><<<gsmall, MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else if (gw->type == GGML_TYPE_Q3_K) {
        mmb_routed_glu_kernel<64, 128, 32, 32, 9><<<gbig,   MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_glu_kernel<64,  32, 16, 16, 9><<<gsmall, MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else if (gw->type == GGML_TYPE_IQ3_XXS) {
        mmb_routed_glu_kernel<64, 128, 32, 32, 10><<<gbig,   MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_glu_kernel<64,  32, 16, 16, 10><<<gsmall, MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else if (gw->type == GGML_TYPE_Q4_0) {
        mmb_routed_glu_kernel<64, 128, 32, 32, 11><<<gbig,   MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_glu_kernel<64,  32, 16, 16, 11><<<gsmall, MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else if (gw->type == GGML_TYPE_Q4_1) {
        mmb_routed_glu_kernel<64, 128, 32, 32, 12><<<gbig,   MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_glu_kernel<64,  32, 16, 16, 12><<<gsmall, MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else if (gw->type == GGML_TYPE_Q5_0) {
        mmb_routed_glu_kernel<64, 128, 32, 32, 13><<<gbig,   MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_glu_kernel<64,  32, 16, 16, 13><<<gsmall, MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else if (gw->type == GGML_TYPE_MXFP4) {
        mmb_routed_glu_kernel<64, 128, 32, 32, 14><<<gbig,   MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_glu_kernel<64,  32, 16, 16, 14><<<gsmall, MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else if (gw->type == GGML_TYPE_NVFP4) {
        mmb_routed_glu_kernel<64, 128, 32, 32, 15><<<gbig,   MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_glu_kernel<64,  32, 16, 16, 15><<<gsmall, MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else if (gw->type == GGML_TYPE_IQ2_S) {
        mmb_routed_glu_kernel<64, 128, 32, 32, 16><<<gbig,   MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_glu_kernel<64,  32, 16, 16, 16><<<gsmall, MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else if (gw->type == GGML_TYPE_IQ2_XS) {
        mmb_routed_glu_kernel<64, 128, 32, 32, 17><<<gbig,   MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_glu_kernel<64,  32, 16, 16, 17><<<gsmall, MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else if (gw->type == GGML_TYPE_IQ2_XXS) {
        mmb_routed_glu_kernel<64, 128, 32, 32, 18><<<gbig,   MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_glu_kernel<64,  32, 16, 16, 18><<<gsmall, MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    } else {
        mmb_routed_glu_kernel<64, 128, 32, 32, 0><<<gbig,   MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_big,   M, K);
        mmb_routed_glu_kernel<64,  32, 16, 16, 0><<<gsmall, MMB_NT, 0, stream>>>(Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src, ids_dst, bounds, desc_small, M, K);
    }
}

void ggml_cuda_mul_mat_id_mmb_glu(ggml_backend_cuda_context & ctx, const ggml_tensor * gw, const ggml_tensor * uw, const ggml_tensor * src1, const ggml_tensor * ids, ggml_tensor * glu) {
    cudaStream_t stream = ctx.stream();
    const int K = (int) gw->ne[0], M = (int) gw->ne[1], E = (int) gw->ne[2];
    const int ne11 = (int) src1->ne[1], T = (int) src1->ne[2], n_used = (int) ids->ne[0];
    const int n_rows_x = ne11 * T, n_rows = n_used * T;
    constexpr int BN = 128;
    const uint16_t * xhp = mmb_bf16_activation(ctx, src1, (size_t) n_rows_x * K, stream);
    ggml_cuda_pool_alloc<int32_t> ids_src1(ctx.pool(), n_rows);
    ggml_cuda_pool_alloc<int32_t> ids_dst(ctx.pool(), n_rows);
    ggml_cuda_pool_alloc<int32_t> bounds(ctx.pool(), E + 1);
    const int si1  = (int) (ids->nb[1] / sizeof(int32_t));
    const int sis1 = (int) (src1->nb[2] / src1->nb[1]);
    ggml_cuda_launch_mm_ids_helper((const int32_t *) ids->data, ids_src1.get(), ids_dst.get(), bounds.get(),
        E, T, n_used, ne11, si1, sis1, /*write_inverse=*/false, stream);
    constexpr int BN_SMALL = 32; const int THRESH = mmb_glu_thresh();
    const int nbig_max   = n_rows / BN + E + 1;
    const int nsmall_max = E * ((THRESH + BN_SMALL - 1) / BN_SMALL) + 1;
    ggml_cuda_pool_alloc<uint32_t> desc_big(ctx.pool(), nbig_max);
    ggml_cuda_pool_alloc<uint32_t> desc_small(ctx.pool(), nsmall_max);
    mmb_build_desc2<<<1, 1024, 0, stream>>>(bounds.get(), desc_big.get(), desc_small.get(), E, nbig_max, nsmall_max, BN, BN_SMALL, THRESH);
    uint16_t * Dh = ggml_cuda_mmb_slot_reserve(ctx, 2, glu, (size_t) n_rows * M);
    const bool store_f32 = !ggml_cuda_mmb_is_bf16_only(glu);
    static unsigned hits = 0; if (hits++ < 2) fprintf(stderr, "MMB_GLU fused gate/up+swiglu: M=%d K=%d rows=%d store_f32=%d\n", M, K, n_rows, (int) store_f32);
    const uint8_t * Wg = (const uint8_t *) gw->data, * Wu = (const uint8_t *) uw->data; float * D = (float *) glu->data; const size_t eb = (size_t) gw->nb[2];
    dim3 gbig((M + 63) / 64, nbig_max), gsmall((M + 63) / 64, nsmall_max);
    mmb_routed_glu_kernel_dispatch(gw, gbig, gsmall, stream, Wg, Wu, eb, xhp, D, Dh, store_f32, ids_src1.get(), ids_dst.get(), bounds.get(), desc_big.get(), desc_small.get(), M, K, BN, BN_SMALL);
    CUDA_CHECK(cudaGetLastError());
}

// Called from graph_optimize (outside stream capture): create the shadow for an eligible IQ4_NL dense weight.
void ggml_cuda_mmb_shadow_prepare(ggml_backend_cuda_context & ctx, const ggml_tensor * w) {
    if (!w) return;
    if (mmb_is_resident_q6k(w)) {
        if (!mmb_shadow_q6k() || g_mmb_shadow.count(w->data) > 0) return;
        const size_t n = (size_t) w->ne[0] * w->ne[1], bytes = n * 2;
        if (g_mmb_shadow_bytes + bytes > mmb_shadow_cap()) { fprintf(stderr, "MMB_SHADOW cap reached; %s stays Q6_K\n", w->name); return; }
        uint16_t * buf = nullptr;
        if (cudaMalloc((void **) &buf, bytes) != cudaSuccess) { fprintf(stderr, "MMB_SHADOW alloc failed (%zu bytes)\n", bytes); return; }
        mmb_dq_q6k_bf16_kernel<<<(unsigned) ((n / 256 + 255) / 256), 256, 0, ctx.stream()>>>((const uint8_t *) w->data, buf, n / 256);
        CUDA_CHECK(cudaGetLastError());
        g_mmb_shadow[w->data] = buf; g_mmb_shadow_bytes += bytes;
        static unsigned q6 = 0; if (q6++ < 3) fprintf(stderr, "MMB_SHADOW Q6_K %s [%lld x %lld] -> BF16 (%.1f MB total)\n", w->name, (long long) w->ne[0], (long long) w->ne[1], g_mmb_shadow_bytes / 1048576.0);
        return;
    }
    if (mmb_shadow_mode() != 1) return;               // mode 2: Q6_K only
    const bool concat = mmb_is_row_concat(w);
    if (!concat && !mmb_is_resident_iq4(w)) return;
    if (concat ? g_mmb_shadow_pair.count({w->src[0]->data, w->src[1]->data}) > 0 : g_mmb_shadow.count(w->data) > 0) return;
    const size_t n = (size_t) w->ne[0] * w->ne[1];
    const size_t bytes = n * 2;
    if (g_mmb_shadow_bytes + bytes > mmb_shadow_cap()) { static bool warned = false; if (!warned) { fprintf(stderr, "MMB_SHADOW cap reached at %.1f MB; further weights stay IQ4_NL\n", g_mmb_shadow_bytes / 1048576.0); warned = true; } return; }
    uint16_t * buf = nullptr;
    if (cudaMalloc((void **) &buf, bytes) != cudaSuccess) { fprintf(stderr, "MMB_SHADOW alloc failed (%zu bytes)\n", bytes); return; }
    if (concat) {
        const size_t n0 = (size_t) w->src[0]->ne[0] * w->src[0]->ne[1], n1 = (size_t) w->src[1]->ne[0] * w->src[1]->ne[1];
        mmb_dq_iq4nl_bf16_kernel<<<(unsigned) ((n0 / 32 + 255) / 256), 256, 0, ctx.stream()>>>((const uint8_t *) w->src[0]->data, buf, n0 / 32);
        mmb_dq_iq4nl_bf16_kernel<<<(unsigned) ((n1 / 32 + 255) / 256), 256, 0, ctx.stream()>>>((const uint8_t *) w->src[1]->data, buf + n0, n1 / 32);
        g_mmb_shadow_pair[{w->src[0]->data, w->src[1]->data}] = buf;
    } else {
        mmb_dq_iq4nl_bf16_kernel<<<(unsigned) ((n / 32 + 255) / 256), 256, 0, ctx.stream()>>>((const uint8_t *) w->data, buf, n / 32);
        g_mmb_shadow[w->data] = buf;
    }
    CUDA_CHECK(cudaGetLastError());
    g_mmb_shadow_bytes += bytes;
    static unsigned hits = 0; if (hits++ < 3 || (hits % 50) == 0) fprintf(stderr, "MMB_SHADOW %s [%lld x %lld] -> BF16 (%.1f MB total)\n", w->name, (long long) w->ne[0], (long long) w->ne[1], g_mmb_shadow_bytes / 1048576.0);
}
